Clever Lead Dog

Medium

# The protocol allows borrowing small positions that can create bad debt

## Summary

According to the [readme](https://github.com/sherlock-audit/2024-08-sentiment-v2?tab=readme-ov-file#q-are-there-any-limitations-on-values-set-by-admins-or-other-roles-in-the-codebase-including-restrictions-on-array-lengths), the `Sentiment` protocol allows the `minDebt` and `minBorrow` to be set too small or even 0.
> Min Debt = from 0 to 0.05 ETH = from 0 to 50000000000000000  
> Min Borrow = from 0 to 0.05 ETH = from 0 to 50000000000000000

That opens up an opportunity for a malicious borrower to create small (or dust) borrowing positions, disincentivizing liquidators from liquidating them as liquidation profit may not cover liquidation costs (gas). As a result, small liquidatable positions will not be liquidated, leaving bad debt to the protocol.

## Vulnerability Detail

The `Pool::borrow()` has checks for the [`minBorrow`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L429) (`@1` in the snippet below) and [`minDebt`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L452) (`@2`), whereas the `Pool::repay()` has a check for the [`minDebt`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L511) (`@3`). However, the protocol allows the `minDebt` and `minBorrow` to be set too small or even 0.

This allows a malicious borrower to create small (or dust) borrowing positions. Furthermore, even if the `minBorrow` isn't set too small but as long as the `minDebt` is set too small, a borrower can still create a small position by borrowing the position at or above the `minBorrow` and then immediately repay the position, leaving only a small value in the position.

```solidity
    function borrow(uint256 poolId, address position, uint256 amt) external returns (uint256 borrowShares) {
        PoolData storage pool = poolDataFor[poolId];

        if (pool.isPaused) revert Pool_PoolPaused(poolId);

        // revert if the caller is not the position manager
        if (msg.sender != positionManager) revert Pool_OnlyPositionManager(poolId, msg.sender);

        //@audit @1 -- minBorrow can be set too small or even 0
        //
        // revert if borrow amount is too low
@1      if (_getValueOf(pool.asset, amt) < minBorrow) revert Pool_BorrowAmountTooLow(poolId, pool.asset, amt);

        // update state to accrue interest since the last time accrue() was called
        accrue(pool, poolId);

        // pools cannot share liquidity among themselves, revert if borrow amt exceeds pool liquidity
        uint256 assetsInPool = pool.totalDepositAssets - pool.totalBorrowAssets;
        if (assetsInPool < amt) revert Pool_InsufficientBorrowLiquidity(poolId, assetsInPool, amt);

        // compute borrow shares equivalant for notional borrow amt
        // [ROUND] round up shares minted, to ensure they capture the borrowed amount
        borrowShares = _convertToShares(amt, pool.totalBorrowAssets, pool.totalBorrowShares, Math.Rounding.Up);

        // revert if borrow amt is too small
        if (borrowShares == 0) revert Pool_ZeroSharesBorrow(poolId, amt);

        // check that final debt amount is greater than min debt
        uint256 newBorrowAssets = _convertToAssets(
            borrowSharesOf[poolId][position] + borrowShares,
            pool.totalBorrowAssets + amt,
            pool.totalBorrowShares + borrowShares,
            Math.Rounding.Down
        );

        //@audit @2 -- minDebt can be set too small or even 0
@2      if (_getValueOf(pool.asset, newBorrowAssets) < minDebt) {
            revert Pool_DebtTooLow(poolId, pool.asset, newBorrowAssets);
        }

        // update total pool debt, denominated in notional asset units and shares
        pool.totalBorrowAssets += amt;
        pool.totalBorrowShares += borrowShares;

        // update position debt, denominated in borrow shares
        borrowSharesOf[poolId][position] += borrowShares;

        // compute origination fee amt
        // [ROUND] origination fee is rounded down, in favor of the borrower
        uint256 fee = amt.mulDiv(pool.originationFee, 1e18);

        address asset = pool.asset;
        // send origination fee to owner
        if (fee > 0) IERC20(asset).safeTransfer(feeRecipient, fee);

        // send borrowed assets to position
        IERC20(asset).safeTransfer(position, amt - fee);

        emit Borrow(position, poolId, asset, amt);
    }

    function repay(uint256 poolId, address position, uint256 amt) external returns (uint256 remainingShares) {
        PoolData storage pool = poolDataFor[poolId];

        // the only way to call repay() is through the position manager
        // PositionManager.repay() MUST transfer the assets to be repaid before calling Pool.repay()
        // this function assumes the transfer of assets was completed successfully

        // there is an implicit assumption that assets were transferred in the same txn lest
        // the call to Pool.repay() is not frontrun allowing debt repayment for another position

        // revert if the caller is not the position manager
        if (msg.sender != positionManager) revert Pool_OnlyPositionManager(poolId, msg.sender);

        // update state to accrue interest since the last time accrue() was called
        accrue(pool, poolId);

        // compute borrow shares equivalent to notional asset amt
        // [ROUND] burn fewer borrow shares, to ensure excess debt isn't pushed to others
        uint256 borrowShares = _convertToShares(amt, pool.totalBorrowAssets, pool.totalBorrowShares, Math.Rounding.Down);

        // revert if repaid amt is too small
        if (borrowShares == 0) revert Pool_ZeroSharesRepay(poolId, amt);

        // check that final debt amount is greater than min debt
        remainingShares = borrowSharesOf[poolId][position] - borrowShares;
        if (remainingShares > 0) {
            uint256 newBorrowAssets = _convertToAssets(
                remainingShares, pool.totalBorrowAssets - amt, pool.totalBorrowShares - borrowShares, Math.Rounding.Down
            );

            //@audit @3 -- minDebt can be set too small or even 0
@3          if (_getValueOf(pool.asset, newBorrowAssets) < minDebt) {
                revert Pool_DebtTooLow(poolId, pool.asset, newBorrowAssets);
            }
        }

        // update total pool debt, denominated in notional asset units, and shares
        pool.totalBorrowAssets -= amt;
        pool.totalBorrowShares -= borrowShares;

        // update and return remaining position debt, denominated in borrow shares
        borrowSharesOf[poolId][position] = remainingShares;

        emit Repay(position, poolId, pool.asset, amt);

        return remainingShares;
    }
```
- `@1`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L429
- `@2`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L452
- `@3`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L511

## Proof of Concept

This section provides a coded PoC. 

Place the `testPoCZeroMinDebtAndMinBorrowDisincentivizingLiquidator()` in the `./protocol-v2/test/integration/LiquidationTest.t.sol` file and run the test using the command: `forge t --nmt "Fork|invariant" --mt testPoCZeroMinDebtAndMinBorrowDisincentivizingLiquidator`.

With the `minDebt` and `minBorrow` configured to 0, the PoC shows that a borrower can create a borrowing position with a very small/dust value in ETH, `0.00000000001` ETH (i.e., `1e7`). When converted to the current USD price, the position is only `$0.00000003`. A liquidator would have to spend more on the gas cost than the seizable collateral's value to liquidate the position, eventually causing a loss.

```solidity
function testPoCZeroMinDebtAndMinBorrowDisincentivizingLiquidator() public {
    // Set minDebt and minBorrow to 0
    vm.startPrank(protocolOwner);
    pool.setMinDebt(0);
    pool.setMinBorrow(0);
    vm.stopPrank();

    assertEq(pool.minDebt(), 0);
    assertEq(pool.minBorrow(), 0);

    // All asset1, asset2, and asset3 have 18 token decimals
    assertEq(asset1.decimals(), 18);
    assertEq(asset2.decimals(), 18);
    assertEq(asset3.decimals(), 18);

    vm.startPrank(user);
    asset2.approve(address(positionManager), 0.00000000001e18); // 1e7

    // Deposit 0.00000000001e18 asset2 (token decimals: 18), borrow 0.00000000001e18 asset1
    Action[] memory actions = new Action[](7);
    (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
    actions[1] = deposit(address(asset2), 0.00000000001e18); // 1e7
    actions[2] = addToken(address(asset2));
    actions[3] = borrow(fixedRatePool, 0.00000000001e18); // 1e7
    actions[4] = approve(address(mockswap), address(asset1), 0.00000000001e18);
    bytes memory data = abi.encodeWithSelector(SWAP_FUNC_SELECTOR, address(asset1), address(asset3), 0.00000000001e18);
    actions[5] = exec(address(mockswap), 0, data);
    actions[6] = addToken(address(asset3));
    positionManager.processBatch(position, actions);
    vm.stopPrank();
    assertTrue(riskEngine.isPositionHealthy(position)); // Position is healthy

    (uint256 totalAssetValue, uint256 totalDebtValue, uint256 minReqAssetValue) = riskEngine.getRiskData(position);

    // URL: https://www.coinbase.com/converter/eth/usd (try it yourself to convert ETH to USD)
    // 1 ETH == 2625.36 USD
    // 0.00000000001 ETH == 0.00000003 USD (Position size)
    assertEq(totalAssetValue, 0.00000000002e18);  // 2e7 (Collateral)
    assertEq(totalDebtValue, 0.00000000001e18);   // 1e7 (Debt)
    assertEq(minReqAssetValue, 0.00000000002e18); // 2e7 (Min Collateral)
}
```

## Impact

With a small borrowing position (and small collateral), there is no incentive for a liquidator to liquidate the position, as the liquidation profit may not cover the liquidation cost (gas). As a result, small liquidatable positions will not be liquidated, leaving bad debt to the protocol.

## Code Snippet

- `@1`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L429

- `@2`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L452

- `@3`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L511

## Tool used

Manual Review

## Recommendation

Restrict the proper `minDebt` and `minBorrow` parameters on each chain to limit the minimum size of borrowing positions.