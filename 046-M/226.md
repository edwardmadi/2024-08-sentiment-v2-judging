Smooth Opal Chameleon

High

# Users may be liquidated right after taking maximal debt

## Summary
Issue High: Users may be liquidated right after taking maximal debt

## Vulnerability Detail

Since there's no gap between the collateral LTV and the liquidation LTV, user positions may be liquidated right after maximal debt is taken, without leaving room for collateral and debt token prices fluctuations. Users have no chance to add more collateral or reduce debt before being liquidated. This will lead to users' losses.

The `ltvFor[poolId][asset]` determines the LTV ratio for the maximal debt asset in pool of `pooId` that can be borrowed with the collateral `asset`. This check is implemented in `_getMinReqAssetValue()` as shown below.

[RiskModule](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/RiskModule.sol#L250-L279)

```solidity
    function _getMinReqAssetValue(
        uint256[] memory debtPools,
        uint256[] memory debtValuleForPool,
        address[] memory positionAssets,
        uint256[] memory wt,
        address position
    ) internal view returns (uint256) {
        uint256 minReqAssetValue;

        // O(pools.len * positionAssets.len)
        uint256 debtPoolsLength = debtPools.length;
        uint256 positionAssetsLength = positionAssets.length;
        for (uint256 i; i < debtPoolsLength; ++i) {
            for (uint256 j; j < positionAssetsLength; ++j) {
                uint256 ltv = riskEngine.ltvFor(debtPools[i], positionAssets[j]);

                // revert with pool id and the asset that is not supported by the pool
                if (ltv == 0) revert RiskModule_UnsupportedAsset(position, debtPools[i], positionAssets[j]);

                // debt is weighted in proportion to value of position assets. if your position
                // consists of 60% A and 40% B, then 60% of the debt is assigned to be backed by A
                // and 40% by B. this is iteratively computed for each pool the position borrows from
                minReqAssetValue += debtValuleForPool[i].mulDiv(wt[j], ltv, Math.Rounding.Up);
            }
        }

        if (minReqAssetValue == 0) revert RiskModule_ZeroMinReqAssets();
        return minReqAssetValue;
    }
```

However, the function liquidate also use the `ltvFor[poolId][asset]` to determine whether the position can be liquidated.

[Pair](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L111-L120)

```solidity
    function validateLiquidation(
        address position,
        DebtData[] calldata debtData,
        AssetData[] calldata assetData
    ) external view {
        // position must breach risk thresholds before liquidation
        if (isPositionHealthy(position)) revert RiskModule_LiquidateHealthyPosition(position);

        _validateSeizedAssetValue(position, debtData, assetData, LIQUIDATION_DISCOUNT);
    }
```

The check of liquidation will eventually invoke the function `_getMinReqAssetValue()` which uses `ltvFor[poolId][asset]`. This means the collateral LTV and the liquidation LTV is the same LTV. And user positions may be liquidated as soon as maximal debt is taken.

[Pair](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L67-L85)

```solidity
    function isPositionHealthy(address position) public view returns (bool) {
        // a position can have four states:
        // 1. (zero debt, zero assets) -> healthy
        // 2. (zero debt, non-zero assets) -> healthy
        // 3. (non-zero debt, zero assets) -> unhealthy
        // 4. (non-zero assets, non-zero debt) -> determined by weighted ltv

        (uint256 totalDebtValue, uint256[] memory debtPools, uint256[] memory debtValueForPool) =
            _getPositionDebtData(position);
        if (totalDebtValue == 0) return true; // (zero debt, zero assets) AND (zero debt, non-zero assets)

        (uint256 totalAssetValue, address[] memory positionAssets, uint256[] memory positionAssetWeight) =
            _getPositionAssetData(position);
        if (totalAssetValue == 0) return false; // (non-zero debt, zero assets)

        uint256 minReqAssetValue =
            _getMinReqAssetValue(debtPools, debtValueForPool, positionAssets, positionAssetWeight, position);
        return totalAssetValue >= minReqAssetValue; // (non-zero debt, non-zero assets)
    }
```

## Proof of Concept

1. The pool has asset1 to be lent. And the LTV ratio for asset2 is 0.8e18, which means `ltvFor[poolId][asset2]` is 0.8e18.
2. The price of asset1 and asset2 is same.
3. A user's position currently has 1e18 asset2 as collaterals.
4. At the time of `block.timestamp`, the user borrows 0.8e18 asset1 from the pool with max LTV ratio of `ltvFor[poolId][asset2]`.
5. Since the interest will accrue of the user's debt as time goes by.
6. At the time of `block.timestamp+1`, the user's position will be liquidatable.
7. Thus, the user's position got liquidated right after maximal debt is taken.

Add this poc in test/integration/LiquidationTest.t.sol

```solidity
    function testMaxDebt_poc() public {
      
        uint256 TIMELOCK_DURATION = 24 * 60 * 60;

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset2), riskEngine.maxLtv()); // maxltv 0.8e18
        vm.warp(block.timestamp+TIMELOCK_DURATION);
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset2));
        vm.stopPrank();
        
        vm.startPrank(user);
        asset2.approve(address(positionManager), 1e18);

        // deposit 1e18 asset2, borrow 1e18 asset1
        Action[] memory actions = new Action[](4);
        (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
        actions[1] = deposit(address(asset2), 1e18);
        actions[2] = addToken(address(asset2));
        actions[3] = borrow(fixedRatePool, 0.8e18);
        positionManager.processBatch(position, actions);
        assertTrue(riskEngine.isPositionHealthy(position));//position health True

        vm.stopPrank();

        vm.warp(block.timestamp+1);// The debt will be 

        asset1.mint(liquidator, 1e18);

        vm.startPrank(liquidator);
        assertFalse(riskEngine.isPositionHealthy(position));

        // construct liquidator data
        DebtData memory debtData = DebtData({ poolId: fixedRatePool, amt: type(uint256).max });
        DebtData[] memory debts = new DebtData[](1);
        debts[0] = debtData;
        AssetData memory asset1Data = AssetData({ asset: address(asset2), amt: 1e18 });
        AssetData[] memory assets = new AssetData[](1);
        assets[0] = asset1Data;

        // liquidate
        asset1.approve(address(positionManager), 1e18);
        positionManager.liquidate(position, debts, assets);
        vm.stopPrank();

    }
```



## Impact

Users may be liquidated right after taking maximal debt. This will lead to users' losses.


## Code Snippet

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/RiskModule.sol#L250-L279

## Tool used
Manual Review

## Recommendation

Consider adding a liquidation threshold that's bigger than the maximal borrow LTV.