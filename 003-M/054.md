Tiny Cloth Sardine

Medium

# Incorrect division in `ChainlinkUsdOracle::getValueInEth` will result in innacurate asset price calculation

### Summary

For assets with more than 18 decimals, the oracle price is scaled down to 18 decimals, the scaling logic is incorrectly implemented and will result in smaller USD denominated values for these assets, thus allowing borrowers to get an additional amount of the asset, or attackers to trigger liquidations for healthy users.

### Root Cause
The decimals are adjusted as shown in the next snippet:

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/oracle/ChainlinkUsdOracle.sol#L86

`decimals - 18` should be enclosed within parenthesis, otherwise the resulting value will be approximately $10^{(decimals-18)}$ orders of magnitude smaller.

### Internal pre-conditions

A pool must exist for at least one token with more than 18 decimals or the token should be added to a position as collateral.

### External pre-conditions

A token with a high number of decimals (more than 18 for this case) must exist.

### Attack Path

Attacker can borrow from the pool with a bigger LTV than what was set during deployment or seize healthy users funds by sending the corresponding transactions through the position manager.

### Impact

Liquidation Vulnerability: Borrowers using assets with more than 18 decimals as collateral are at risk of premature liquidation. Attackers can exploit this flaw to liquidate otherwise healthy positions and seize the collateral.

Over-Borrowing Risk: Borrowers drawing from a pool with an asset that has more than 18 decimals can potentially borrow more than their collateral should allow. This discrepancy could destabilize the lending protocol, leading to a systemic risk.

### PoC

For this test I modified the tets in `RiskModule.t.sol`:

<details>

<summary> See PoC </summary>

```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {BaseTest, MockSwap} from "../BaseTest.t.sol";
import {console2} from "forge-std/console2.sol";
import {MockERC20} from "../mocks/MockERC20.sol";
import {Pool} from "src/Pool.sol";
import {Action} from "src/PositionManager.sol";
import {PositionManager} from "src/PositionManager.sol";
import {RiskEngine} from "src/RiskEngine.sol";
import {RiskModule} from "src/RiskModule.sol";
import {FixedPriceOracle} from "src/oracle/FixedPriceOracle.sol";
import {FixedAggregator} from "../mocks/MockOracle.sol";
import {FixedAggregatorSeq} from "../mocks/MockOracleSeq.sol";
import {FixedPriceOracle} from "src/oracle/FixedPriceOracle.sol";
import {ChainlinkUsdOracle} from "src/oracle/ChainlinkUsdOracle.sol";
contract RiskModuleUnitTests is BaseTest {
    Pool pool;
    address position;
    RiskEngine riskEngine;
    RiskModule riskModule;
    PositionManager positionManager;
    ChainlinkUsdOracle chainlinkUsdOracle;
    FixedAggregator mockAggregator;
    FixedAggregatorSeq mockAggregatorSeq;
    FixedPriceOracle oneEthOracle;

    function setUp() public override {
        super.setUp();

        oneEthOracle = new FixedPriceOracle(1e18);
        vm.warp(block.timestamp + 3601); // Avoid error when deploying due to underflow
        mockAggregator = new FixedAggregator();
        mockAggregatorSeq = new FixedAggregatorSeq();
        chainlinkUsdOracle = new ChainlinkUsdOracle(
            address(this),
            address(mockAggregatorSeq),
            address(mockAggregator),
            0
        );

        pool = protocol.pool();
        riskEngine = protocol.riskEngine();
        riskModule = protocol.riskModule();
        positionManager = protocol.positionManager();

        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset1), address(oneEthOracle)); // 1 asset1 = 1 eth
        riskEngine.setOracle(address(asset2), address(oneEthOracle)); // 1 asset2 = 1 eth
        riskEngine.setOracle(address(asset3), address(oneEthOracle)); // 1 asset3 = 1 eth
        vm.stopPrank();

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset3), 0.5e18); // 2x lev
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset3));
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset2), 0.5e18); // 2x lev
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset2));
        vm.stopPrank();

        asset1.mint(lender, 100e18);
        asset2.mint(user, 10e18);

        vm.startPrank(lender);
        asset1.approve(address(pool), 100e18);
        pool.deposit(fixedRatePool, 100e18, lender);
        vm.stopPrank();
    }
...
    function testOracleDecimals() public {
        MockERC20 mockERC20HighDecimals = new MockERC20("TOKEN", "TOK", 20);
        chainlinkUsdOracle.setFeed(
            address(mockERC20HighDecimals),
            address(mockAggregator),
            3600
        );
        vm.startPrank(protocolOwner);
        protocol.positionManager().toggleKnownAsset(
            address(mockERC20HighDecimals)
        );
        riskEngine.setOracle(
            address(mockERC20HighDecimals),
            address(chainlinkUsdOracle)
        );
        vm.stopPrank();
        mockERC20HighDecimals.mint(user, 1e20);
        vm.startPrank(user);
        mockERC20HighDecimals.approve(address(positionManager), 1e20);
        Action[] memory actions = new Action[](3);
        (position, actions[0]) = newPosition(
            user,
            bytes32(uint256(0x123456789))
        );
        actions[1] = deposit(address(mockERC20HighDecimals), 1e20);
        actions[2] = addToken(address(mockERC20HighDecimals));
        positionManager.processBatch(position, actions);
        vm.stopPrank();
        assertEq(riskModule.getTotalAssetValue(position), 1);
    }
...
```
</details>

And I used the following mock aggregators returning a fixed value:

<details>

<summary> Price Feed </summary>
Fiile `MockOracle.sol`:

```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IAggregatorV3 {
    function latestRoundData()
        external
        view
        returns (
            uint80 roundId,
            int256 answer,
            uint256 startedAt,
            uint256 updatedAt,
            uint80 answeredInRound
        );

    function decimals() external view returns (uint256);
}

contract FixedAggregator is IAggregatorV3 {
    uint80 private fixedRoundId = 1;
    int256 private fixedAnswer = 100000000;
    uint256 private fixedStartedAt = 0;
    uint256 private fixedUpdatedAt = block.timestamp;
    uint80 private fixedAnsweredInRound = 1;
    uint256 private fixedDecimals = 8;

    function latestRoundData()
        external
        view
        override
        returns (
            uint80 roundId,
            int256 answer,
            uint256 startedAt,
            uint256 updatedAt,
            uint80 answeredInRound
        )
    {
        return (
            fixedRoundId,
            fixedAnswer,
            fixedStartedAt,
            fixedUpdatedAt,
            fixedAnsweredInRound
        );
    }

    function decimals() external view override returns (uint256) {
        return fixedDecimals;
    }
}

```

</details>

<details>

<summary> Sequencer Feed </summary>
Fiile `MockOracleSeq.sol`:

```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IAggregatorV3 {
    function latestRoundData()
        external
        view
        returns (
            uint80 roundId,
            int256 answer,
            uint256 startedAt,
            uint256 updatedAt,
            uint80 answeredInRound
        );

    function decimals() external view returns (uint256);
}

contract FixedAggregatorSeq is IAggregatorV3 {
    uint80 private fixedRoundId = 1;
    int256 private fixedAnswer = 0;
    uint256 private fixedStartedAt = 1;
    uint256 private fixedUpdatedAt = 0;
    uint80 private fixedAnsweredInRound = 1;
    uint256 private fixedDecimals = 8;

    function latestRoundData()
        external
        view
        override
        returns (
            uint80 roundId,
            int256 answer,
            uint256 startedAt,
            uint256 updatedAt,
            uint80 answeredInRound
        )
    {
        return (
            fixedRoundId,
            fixedAnswer,
            fixedStartedAt,
            fixedUpdatedAt,
            fixedAnsweredInRound
        );
    }

    function decimals() external view override returns (uint256) {
        return fixedDecimals;
    }
}
```

</details>

### Mitigation

Enclose `decimals - 18` within parenthesis:

```diff
-(amt / (10 ** decimals - 18)).mulDiv(
+(amt / (10 ** (decimals - 18))).mulDiv(
    uint256(assetUsdPrice),
    uint256(ethUsdPrice)
);
```