Keen Jetblack Turtle

High

# Wrong decimals conversion in `ChainlinkUsdOracle.sol` leading to lowered Asset Feed Prices

## Summary
In the `getValueInEth()` function which is used  to get the price of an asset in ETH, from both an ETH/usd and asset/usd feed.
There is however a wrong conversion math leading to prices, being significantly lower as their Real Value.
## Vulnerability Detail

```js
    function getValueInEth(address asset, uint256 amt) external view returns (uint256) {
        _checkSequencerFeed();
        uint256 ethUsdPrice = _getPriceWithSanityChecks(ETH);
        uint256 assetUsdPrice = _getPriceWithSanityChecks(asset);

        uint256 decimals = IERC20Metadata(asset).decimals();

        // [ROUND] price is rounded down. this is used for both debt and asset math, no effect
        if (decimals <= 18) return (amt * 10 ** (18 - decimals)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
@>>        else return (amt / (10 ** decimals - 18)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
    }
```
The vulnerable Line is the following
```js
else return (amt / (10 ** decimals - 18)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
```
This is wrong, because `10 ** decimals - 18` is equivalent to `(10**decimals) - (18)`, the correct formula to complete the decimals conversions as it is the case in the line above, should be `10 ** (decimals - 18)`
## Impact
Prices are significantly lowered for assets with decimals bigger than 18 decimals.

e.g token A has 20 decimals token price is 0.1eth per token A
The price of the token now, due to the before mentioned bug, will be approximatly `0.1/1e2 eth ~= 0.001 eth`. 
The bigger the number of decimals that the price has the more the asset price will be devalued.
The impacts of this bug, includes:
- Creating loan positions that are worth a lot more than the collateral, and effectively draining the protocol
- Users who back their loans with such assets will be instantly liquidated, and the liquidators will extract a lot of value from them

## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/oracle/ChainlinkUsdOracle.sol#L76-L87

## Tool used

Manual Review

## Recommendation
To fix this issue, we simply need add the correct parentheses

```diff
    function getValueInEth(address asset, uint256 amt) external view returns (uint256) {
        _checkSequencerFeed();

        uint256 ethUsdPrice = _getPriceWithSanityChecks(ETH);
        uint256 assetUsdPrice = _getPriceWithSanityChecks(asset);

        uint256 decimals = IERC20Metadata(asset).decimals();

        // [ROUND] price is rounded down. this is used for both debt and asset math, no effect
        if (decimals <= 18) return (amt * 10 ** (18 - decimals)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
-        else return (amt / (10 ** decimals - 18)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
+        else return (amt / (10 ** (decimals - 18))).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
    }
```