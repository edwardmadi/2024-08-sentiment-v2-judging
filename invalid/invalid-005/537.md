Fantastic Blonde Albatross

High

# Attacker could update RedstoneOracle's price to an older one, to liquidate another position or benefit himself

## Summary
In the **RedstoneOracle**, a malicious actor could update the price with an older one to benefit themselves or cause losses to other users of the protocol.

## Vulnerability Detail

Redstone oracle uses an "on-demand" model, which allows for lower fees as the price is not constantly pushed. Instead, anyone can submit off-chain signed data that can be validated with the Redstone SDK. Check out the docs [here](https://docs.redstone.finance/docs/get-started/models/redstone-core).

In the **RedstoneOracle**, we are missing any validation on the actual `timestamp` of the price that’s submitted via `calldata` to the `updatePrice()` function.  
Thus, we could submit a price with an older timestamp.

## Impact
This issue has multiple impacts:
- If there was an older price that makes a certain position unhealthy, a malicious actor could submit it and liquidate the position.
- Since oracles are also used to check if a position is below `Pool.minDebt`, the malicious actor could use an older price to borrow less than `Pool.minDebt` compared to the real price. 
- Avoid liquidation of his position, if the price favours his LTV.

## Proof of Concept
All of the oracles in the protocol provide the `getValueInEth` function, and one of its main purposes is to ensure that the `LTV` ratio is healthy. Thus, someone being able to use an older price can take advantage of this.

`PositionManager.liquidate()` is validating the position health using an oracle -> [liquidate code snippet.](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L435)  and [validate code snippet](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/RiskModule.sol#L117)

 isPositionHealthy is using `_getPositionDebtData` and `_getPositionAssetData` which use `getAssetValue` and `getDebtValueForPool`. And the values are in ETH, which depends on the Oracle.
 
```solidity
    function getDebtValueForPool(address position, uint256 poolId) public view returns (uint256) {
        address asset = pool.getPoolAssetFor(poolId);
        IOracle oracle = IOracle(riskEngine.getOracleFor(asset));
@>>     return oracle.getValueInEth(asset, pool.getBorrowsOf(poolId, position));
    }
```
```solidity
    function getAssetValue(address position, address asset) public view returns (uint256) {
        IOracle oracle = IOracle(riskEngine.getOracleFor(asset));
        uint256 amt = IERC20(asset).balanceOf(position);
@>>     return oracle.getValueInEth(asset, amt);
    }
```

In the [RedstoneDefaultsLib.sol](https://github.com/redstone-finance/redstone-oracles-monorepo/blob/94ac46f41be52ee9132bede9d13897f5922c800d/packages/evm-connector/contracts/core/RedstoneDefaultsLib.sol#L18-L39), which is part of the Oracle SDK, we don't have any checks comparing an old timestamp.
We only validate that it’s **not too old** or **too far in the future**.  
```solidity
    if (block.timestamp < receivedTimestampSeconds) {
      if ((receivedTimestampSeconds - block.timestamp) > DEFAULT_MAX_DATA_TIMESTAMP_AHEAD_SECONDS) {
@>>     revert TimestampFromTooLongFuture(receivedTimestampSeconds, block.timestamp);
      }
    } else if ((block.timestamp - receivedTimestampSeconds) > DEFAULT_MAX_DATA_TIMESTAMP_DELAY_SECONDS) {
@>>   revert TimestampIsTooOld(receivedTimestampSeconds, block.timestamp);
    }
```
The `Redstone SDK` itself does not store the timestamp, just validates it from the `calldata` and signatures submitted.

Possible scenario:
-> Position becomes unhealthy for a short period of time due to price volatility.
-> Position's LTV is healthy after price is stable too.
-> RedstoneOracle is updated with the most, recent, stable price.
-> Malicious actor calls updatePrice() and submits an price with older timestamp, i.e. older price.
-> Malicious actor liquidates the unhealthy Position.

## Tool used

Manual Review

## Recommendation
Override the validateTimestamp() function and process the checks yourself. 
Update the updatePrice() function, as well.
```diff
-    function updatePrice() external {
-        // values[0] -> price of ASSET/USD
-        // values[1] -> price of ETH/USD
-        // values are scaled to 8 decimals
-        uint256[] memory values = getOracleNumericValuesFromTxMsg(dataFeedIds);
-
-        assetUsdPrice = values[0];
-        ethUsdPrice = values[1];
-
-        // RedstoneDefaultLibs.sol enforces that prices are not older than 3 mins. since it is not
-        // possible to retrieve timestamps for individual prices being passed, we consider the worst
-       // case and assume both prices are 3 mins old
-        priceTimestamp = block.timestamp - THREE_MINUTES;
-    }
+    uint256 constant DEFAULT_MAX_DATA_TIMESTAMP_DELAY_SECONDS = 3 minutes;
+    uint256 constant DEFAULT_MAX_DATA_TIMESTAMP_AHEAD_SECONDS = 1 minutes;

+    function updatePrice(uint48 timestamp) external {
+        // Validate timestamp is not older than the current price timestamp
+        if (timestamp <= priceTimestamp) return;
+
+        if (block.timestamp < timestamp) {
+            if ((timestamp - block.timestamp) > DEFAULT_MAX_DATA_TIMESTAMP_AHEAD_SECONDS) revert();
+        } else if ((block.timestamp - timestamp) > DEFAULT_MAX_DATA_TIMESTAMP_DELAY_SECONDS) {
+            revert();
+        }
+
+        priceTimestamp = timestamp;
+
+        uint256[] memory values = getOracleNumericValuesFromTxMsg(dataFeedIds);
+        assetUsdPrice = values[0];
+        ethUsdPrice = values[1];
+    }
+
+    function validateTimestamp(uint256 timestampMillis) public view virtual override {
+        uint256 timestamp = timestampMillis / 1000;
+        if (timestamp != priceTimestamp) revert();
+   }

```