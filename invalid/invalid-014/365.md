Scruffy Raspberry Gerbil

Medium

# Reallocate fails to deposit when deposit amount causes a pool to reach its pool cap

### Summary

A missing "equals sign" (i.e. "=") will prevent `reallocate()` from functioning properly for Superpool owners resulting in failed deposits and lenders losing out on interest income.

### Root Cause

In `Superpool.sol:449`, the "less than" (i.e. "<") sign doesn't allow the pool cap to be reached:

<https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L449>

### Internal pre-conditions

1. Superpool owner needs to call `reallocate()` and attempt to deposit an amount into a pool that makes the pool reach its `poolCap`

### External pre-conditions

1. Lenders must deposit enough `asset` to reach the poolCap of any given pool in a Superpool.

### Attack Path

1. `Superpool` has two pools, Pool_1 and Pool_2.
2. Pool_1 holds X amount of `asset` and Pool_2 holds 0 amount of `asset`.
3. `poolCap` for Pool_2 is X.
4. SuperPool owner calls `reallocate()` attempting to withdraw X amount of `asset` from Pool_1 and deposit it into Pool_2.
5. `reallocate()` successfully withdraws X amount of asset from Pool_1 to `Superpool`, but `pool.deposit()` is never triggered because the deposit amount, X, is equal to Pool_2's poolCap.
6. Funds stay in `Superpool` causing lenders to miss out on interest income.

### Impact

Lenders miss out on interest income.

### PoC

Create a new file under the `/protocol-v2/test` directory (e.g ReallocateFailsTest.t.sol) and paste in the POC code below.

Run the following command:
`forge test --mt testReallocateFailsDeposit -vv`

POC:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import "./BaseTest.t.sol";
import { MockERC20 } from "./mocks/MockERC20.sol";
import { OwnableUpgradeable } from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import { Action, Operation } from "../src/PositionManager.sol";
import { IOracle } from "../src/interfaces/IOracle.sol";
import { FixedPriceOracle } from "../src/oracle/FixedPriceOracle.sol";

contract ReallocateFailsDeposit is BaseTest {
     
    bytes32 public constant SENTIMENT_POOL_KEY = 0x1a99cbf6006db18a0e08427ff11db78f3ea1054bc5b9d48122aae8d206c09728;
    bytes32 public constant SENTIMENT_RISK_ENGINE_KEY = 0x5b6696788621a5d6b5e3b02a69896b9dd824ebf1631584f038a393c29b6d7555;
    bytes32 public constant SENTIMENT_POSITION_BEACON_KEY =
        0x6e7384c78b0e09fb848f35d00a7b14fc1ad10ae9b10117368146c0e09b6f2fa2;

    Pool pool;
    Registry registry;
    address payable position;
    RiskEngine riskEngine;
    PositionManager positionManager;

    address public positionOwner = makeAddr("positionOwner");
    FixedPriceOracle asset1Oracle;
    FixedPriceOracle asset2Oracle;
    FixedPriceOracle asset3Oracle;

    uint256 initialDepositAmt = 1e5;
    SuperPool superPool;
    SuperPoolFactory superPoolFactory;
    address public feeTo = makeAddr("FeeTo");

    function setUp() public override {
        super.setUp();
        pool = protocol.pool();
        registry = protocol.registry();
        riskEngine = protocol.riskEngine();
        positionManager = protocol.positionManager();

        // Oracle setup
        vm.startPrank(protocolOwner);    
        asset1Oracle = new FixedPriceOracle(1e18);
        asset2Oracle = new FixedPriceOracle(0.5e18);
        asset3Oracle = new FixedPriceOracle(1e18);
        riskEngine.setOracle(address(asset1), address(asset1Oracle));
        riskEngine.setOracle(address(asset2), address(asset2Oracle));
        riskEngine.setOracle(address(asset3), address(asset3Oracle));
        vm.stopPrank();

        // Create new position
        Action[] memory actions = new Action[](1);
        (position, actions[0]) = newPosition(positionOwner, bytes32(uint256(3_492_932_942)));
        PositionManager(positionManager).processBatch(position, actions);
        
        // Deploy Superpool
        superPoolFactory = protocol.superPoolFactory();
        vm.prank(protocolOwner);
        asset1.mint(address(this), initialDepositAmt);
        asset1.approve(address(superPoolFactory), initialDepositAmt);
        superPool = SuperPool(
            superPoolFactory.deploySuperPool(
                poolOwner, address(asset1), feeTo, 0.01 ether, 1_000_000 ether, initialDepositAmt, "test", "test"
            )
        );
    
        // Set LTVs for asset3
        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(linearRatePool, address(asset3), 0.75e18);
        riskEngine.acceptLtvUpdate(linearRatePool, address(asset3));
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset3), 0.75e18);
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset3));
        vm.stopPrank();
    }

    function testReallocateFailsDeposit() public {

        // 1. Superpool owner adds fixedRatePool and linearRatePool to SuperPool
        vm.startPrank(poolOwner);
        superPool.addPool(fixedRatePool, 10e18); // poolCap set to 10e18
        superPool.addPool(linearRatePool, 10e18); // poolCap set to 10e18
        vm.stopPrank();


        // 2. Lender deposits 10e18 of asset1 to SuperPool
        vm.startPrank(lender);
        asset1.mint(lender, 100e18);
        asset1.approve(address(superPool), 10e18);
        superPool.deposit(10e18, lender);
        vm.stopPrank();

        // 3. SuperPool owner reallocates 10e18 of asset1 from fixedRatePool to linearRatePool
        vm.startPrank(poolOwner);
        SuperPool.ReallocateParams[] memory reAllocateWithdrawals = new SuperPool.ReallocateParams[](1);
        reAllocateWithdrawals[0] = (SuperPool.ReallocateParams(fixedRatePool, 10e18));
        SuperPool.ReallocateParams[] memory reAllocateDeposits = new SuperPool.ReallocateParams[](1);
        reAllocateDeposits[0] = (SuperPool.ReallocateParams(linearRatePool, 10e18));
        
        
        console2.log("Asset1 balances BEFORE reallocation:");
        console2.log("SuperPool: ", asset1.balanceOf(address(superPool)));
        console2.log("fixedRatePool: ", pool.getTotalAssets(fixedRatePool));
        console2.log("linearRatePool: ", pool.getTotalAssets(linearRatePool));
        console2.log(" ");
        
        superPool.reallocate(reAllocateWithdrawals, reAllocateDeposits);

        console2.log("Asset1 balances AFTER reallocation:");
        console2.log("SuperPool: ", asset1.balanceOf(address(superPool)));
        console2.log("fixedRatePool: ", pool.getTotalAssets(fixedRatePool));
        console2.log("linearRatePool: ", pool.getTotalAssets(linearRatePool));

        assertEq(pool.getTotalAssets(fixedRatePool), 0);
        assertEq(pool.getTotalAssets(linearRatePool), 0);
        assertGt(asset1.balanceOf(address(superPool)), initialDepositAmt);
        vm.stopPrank();
    }
}
```

### Mitigation

Update `Superpool.sol:449` to the following:
```solidity
if (assetsInPool + deposits[i].assets <= poolCap) {
```
