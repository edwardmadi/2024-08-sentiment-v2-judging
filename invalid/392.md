Overt Wintergreen Rabbit

High

# Residual assets in a position during bad debt liquidation are not added back to a base pool will cause the lenders taking a larger loss

### Summary

Residual assets in a position during bad debt liquidation are not added back to a base pool will cause the lenders taking a larger loss.

### Root Cause

In `Pool.sol`, there is a missing of a function for the protocol to add back residual assets during bad debt liquidation to a base pool.

### Internal pre-conditions

_No response_

### External pre-conditions

External conditions causing bad debt liquidation to happen

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/RiskModule.sol#L123-L127

```solidity
    function validateBadDebt(address position) external view {
        uint256 totalDebtValue = getTotalDebtValue(position);
        uint256 totalAssetValue = getTotalAssetValue(position);
        if (totalAssetValue > totalDebtValue) revert RiskModule_NoBadDebt(position);
    }
```
such as:
- Pool asset oracle increases
- Collateral asset oracle decreases
- Interest accrued

### Attack Path

The protocol can liquidate bad debt of a position, which has `totalDebtValue` is greater than or equal to `totalAssetValue`, by calling `PositionManager#liquidateBadDebt`.

The protocol will take **all** the residual assets of the position

```solidity
    function liquidateBadDebt(address position) external onlyOwner {
	...
        for (uint256 i; i < positionAssetsLength; ++i) {
            uint256 amt = IERC20(positionAssets[i]).balanceOf(position);
>>          try Position(payable(position)).transfer(owner(), positionAssets[i], amt) { } catch { }
        }
	...
    }
```

then will clear all debts of the position from the base pools

```solidity
    function liquidateBadDebt(address position) external onlyOwner {
	...
        for (uint256 i; i < debtPoolsLength; ++i) {
>>          pool.rebalanceBadDebt(debtPools[i], position);
            Position(payable(position)).repay(debtPools[i], type(uint256).max);
        }
    }
```

During the whole process, the residual assets from the liquidated position are not added back to the base pools. Moreover, there is no function `Pool.sol` for the protocol to add back the residual assets to the base pools.

### Impact

Bad debt liquidation will cause the base pools taking larger loss.

### PoC

Let's have a base pool `fixedRatePool`:
- Has `100e18` tokens `asset1`
- `ltvFor[fixedRatePool][asset2] = 0.8e18`
- Has fixed interest rate `1e18` per year

Oracles:
- `asset1` has value in ETH `1e18`
- `asset2` has value in ETH `1e18`

A borrower:
- Has `10e18` tokens `asset2` in their position
- Borrow `8e18` tokens `asset1` from `fixedRatePool`

After a year,
- The value of the debt is `16e18`. The debt will became a bad debt.
- The total assets of `fixedRatePool` is `108e18`

When the protocol calls `PositionManager#liquidateBadDebt`
- The debt is cleared from the position
- The protocol will take the residual asset, which is `10e18` tokens `asset2`
- The debt is deducted from `fixedRatePool`, which will make the total assets of `fixedRatePool` became `92e18`

At the end:
- The protocol benefits `10e18` tokens `asset2`
- While `fixedRatePool` takes a loss of `16e18` token `asset1`

We believe the correct behavior should be `10e18` tokens `asset2` get swapped to `asset1` and add back to `fixedRatePool`.

<details>
   <summary> Add this POC to `test/POC/POC.t.sol`</summary>

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import "../BaseTest.t.sol";
import { MockERC20 } from "../mocks/MockERC20.sol";
import { Action } from "src/PositionManager.sol";
import { IOracle } from "src/interfaces/IOracle.sol";
import { Math } from "@openzeppelin/contracts/utils/math/Math.sol";

import "forge-std/console.sol";
contract MockOracle is IOracle {
    using Math for uint256;
    uint256 public price;
    constructor() {}

    function getValueInEth(address, uint256 amt) external view returns (uint256) {
        return amt.mulDiv(price, 1 ether);
    }

    function setPrice(uint256 _price) external {
        price = _price;
    }
}

contract POC is BaseTest {
    uint256 public constant SECONDS_PER_YEAR = 31_557_600;

    Pool pool;
    Registry registry;
    address payable position;
    RiskEngine riskEngine;
    PositionManager positionManager;

    address public borrower = makeAddr("borrower");
    MockOracle asset1Oracle;
    MockOracle asset2Oracle;

    function setUp() public override {
        super.setUp();

        asset1Oracle = new MockOracle();
        asset2Oracle = new MockOracle();

        asset1Oracle.setPrice(1 ether);
        asset2Oracle.setPrice(1 ether);

        pool = protocol.pool();
        registry = protocol.registry();
        riskEngine = protocol.riskEngine();
        positionManager = protocol.positionManager();

        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset1), address(asset1Oracle));
        riskEngine.setOracle(address(asset2), address(asset2Oracle));
        vm.stopPrank();

        asset1.mint(address(this), 100 ether);
        asset1.approve(address(pool), 100 ether);

        pool.deposit(fixedRatePool, 100 ether, address(this));

        asset2.mint(borrower, 10 ether);
        vm.prank(borrower);
        asset2.approve(address(positionManager), 10 ether);

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset2), 0.8e18);
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset2));
        vm.stopPrank();
    }

    function testPOC() public {
        {
            Action[] memory actions = new Action[](4);
            (position, actions[0]) = newPosition(borrower, bytes32(uint256(1337)));
            actions[1] = deposit(address(asset2), 10 ether);
            actions[2] = addToken(address(asset2));
            actions[3] = borrow(fixedRatePool, 8 ether);

            vm.prank(borrower);
            PositionManager(positionManager).processBatch(position, actions);
        }

        vm.warp(block.timestamp + SECONDS_PER_YEAR);

        console.log("Pool asset before bad debt liquidation: %e", pool.getTotalAssets(fixedRatePool));

        vm.prank(protocolOwner);
        positionManager.liquidateBadDebt(position);

        console.log("Pool asset after bad debt liquidation: %e", pool.getTotalAssets(fixedRatePool));
    }
}
```
Logs:
```bash
  Pool asset before bad debt liquidation: 1.08e20
  Pool asset after bad debt liquidation: 9.2e19
```

</details>


### Mitigation

Add a function in `Pool.sol` for the protocol to add back the residual assets to the base pool

```solidity
contract Pool is OwnableUpgradeable, ERC6909 {
	...
	function addAssets(uint256 poolId, uint256 amt) external onlyOwner {
		PoolData storage pool = poolDataFor[poolId];
	
		IERC20(pool.asset).safeTransferFrom(msg.sender, address(this), amt);
		pool.totalDepositAssets += amt;	
	}
}
```