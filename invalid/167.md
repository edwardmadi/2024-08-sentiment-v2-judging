Hollow Smoke Manatee

Medium

# KinkedRateModel zero optimalUtil parameter renders Pools unusable

### Summary

The `KinkedRateModel` contract allows the `optimalUtil` parameter to be set to zero during initialization, leading to division by zero errors and render the pool unusable.

### Root Cause

In [`KinkedRateModel.sol`](https://github.com/sentimentxyz/protocol-v2/blob/04bf15565165396608cc0aedacf05897235518fd/src/irm/KinkedRateModel.sol#L30-L39), the `KinkedRateModel` constructor does not prevent `optimalUtil` from being set to zero. This makes a pool using a kinked rate model with an optimal utilization set to 0 unusable as it prevent deposit from any lender.

At initialization, the pool `totalDepositAssets` is set to 0. When calling the `Pool::deposit` function in order to make the first deposit, the `Pool::accrue` function will be called before the state is updated, following the CEI pattern.
`totalDepositAssets` and `totalDepositShares` are therefore still 0 when accruing.
```solidity
function deposit(uint256 poolId, uint256 assets, address receiver) public returns (uint256 shares) {
    // ...

    // update state to accrue interest since the last time accrue() was called
    accrue(pool, poolId);

    // ...
    
    pool.totalDepositAssets += assets;
    pool.totalDepositShares += shares;

   // ...
}
```

In the `accrue` function, we compute the `interestAccrued` through `simulateAccrue` by calling the Rate model `getInterestAccrued` function with `pool.totalBorrowAssets` & `pool.totalDepositAssets` equaling 0.

```solidity
function simulateAccrue(PoolData storage pool) internal view returns (uint256, uint256) {
    uint256 interestAccrued = IRateModel(pool.rateModel).getInterestAccrued(
        pool.lastUpdated, pool.totalBorrowAssets, pool.totalDepositAssets
    );

    // ...

function accrue(PoolData storage pool, uint256 id) internal {
    (uint256 interestAccrued, uint256 feeShares) = simulateAccrue(pool);

    // ...
}
```

Finally, we attempt to compute the interest rate via the `KinkedRateModel::getInterestRate` function. We first fetch the pool `util` ratio which is 0 at initialization as `totalAssets` is 0.

However, since `0 <= 0`, the conditional statement `if (util <= OPTIMAL_UTIL)` will be evaluated to true and the contract will attempt to perform a division by 0 which will revert.

```solidity
function getInterestAccrued(/* ... */) external view returns (uint256) {
    uint256 rateFactor = ((block.timestamp - lastUpdated)).mulDiv(
        getInterestRate(totalBorrows, totalAssets), SECONDS_PER_YEAR, Math.Rounding.Up
    );

    // ...
}

function getInterestRate(uint256 totalBorrows, uint256 totalAssets) public view returns (uint256) {
@>  uint256 util = (totalAssets == 0) ? 0 : totalBorrows.mulDiv(1e18, totalAssets, Math.Rounding.Up);

@>  if (util <= OPTIMAL_UTIL) return MIN_RATE_1 + SLOPE_1.mulDiv(util, OPTIMAL_UTIL, Math.Rounding.Down);
    else return MIN_RATE_2 + SLOPE_2.mulDiv((util - OPTIMAL_UTIL), MAX_EXCESS_UTIL, Math.Rounding.Down);
}
```

The lenders deposit will always revert and therefore, the pool will always remain empty and nobody will be able to use it.

### Internal pre-conditions

_Note:_ We assume the admins would deploy the `KinkedRateModel` the same way they deploy the `LinearRateModel` or the `FixedRateModel` based on the [deployment scripts](https://github.com/sentimentxyz/protocol-v2/tree/master/script/irm) in the `script` folder

- Admin deploy a `KinkedRateModel` with an `optimalUtil` set to 0, either by mistake or because of a parsing error from the function `vm.parseJsonUint()`
- A user deploy a pool using this kinked rate model
- Nobody is able to deposit in the pool, rendering it unusable

### Impact

If `optimalUtil` is set to zero, the `KinkedRateModel::getInterestRate` function will revert due to division by zero when utilization is zero, which is always the case at initialization, breaking core functionality.

While the likelihood of this occurring is low due to typical testing and review processes, the impact is severe enough that it warrants attention. Moreover, the fact that the contract allows this state to occur at all represents a design flaw that should be addressed to improve the robustness of the system.

### PoC

Copy the following proof of code in the test suite and run it with `forge test  --mt testKinkedPoolOptimalUtilZero -vvvv`:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {BaseTest} from "../BaseTest.t.sol";
import {KinkedRateModel} from "src/irm/KinkedRateModel.sol";
import {Registry} from "src/Registry.sol";
import {Pool} from "src/Pool.sol";


contract PoolSupplyCapTest is BaseTest {
    Pool pool;

    address kinkedRateModel;
    uint128 constant SUPPLY_CAP = 1000e18;


    function setUp() public override {
        super.setUp();
        pool = protocol.pool();

        // Mint tokens to the lender
        asset1.mint(lender, SUPPLY_CAP);
    }

    function initializeKinkedPool(
        uint256 minRate,
        uint256 slope1,
        uint256 slope2,
        uint256 optimalUtil
    ) public {
        vm.assume(optimalUtil < 1e18); // To respect the curent implementation

        bytes32 KINKED_RATE_MODEL_KEY = keccak256("testKinkedRateModelWith0OptimUtil");
        kinkedRateModel = address(
            new KinkedRateModel(minRate, slope1, slope2, optimalUtil)
        );

        // Registering the rate model in the Registry
        vm.startPrank(protocolOwner);
        Registry(protocol.registry()).setRateModel(
            KINKED_RATE_MODEL_KEY,
            kinkedRateModel
        );
        vm.stopPrank();

        kinkedRatePool = pool.initializePool(
            poolOwner,
            address(asset1),
            SUPPLY_CAP,
            KINKED_RATE_MODEL_KEY
        );
    }

    function testKinkedPoolOptimalUtilZero() public {
        initializeKinkedPool({
            minRate: 0.05e18,
            slope1: 1e18,
            slope2: 2e18,
            optimalUtil: 0 // We set the optimal utilization to 0
        });

        vm.startPrank(lender);
        asset1.approve(address(pool), SUPPLY_CAP);

        // Expect a panic due to division by zero
        vm.expectRevert(abi.encodeWithSignature("Panic(uint256)", 0x12));
        pool.deposit(kinkedRatePool, SUPPLY_CAP, lender);

        vm.stopPrank();
    }
}
```

### Mitigation

Modify the constructor to prevent `optimalUtil` from being set to zero:
```diff
constructor(uint256 minRate, uint256 slope1, uint256 slope2, uint256 optimalUtil) {
-    assert(optimalUtil < 1e18);
+    assert(optimalUtil > 0 && optimalUtil < 1e18);
    // ... 
}
```