Massive Slate Dalmatian

Medium

# Users can't partially repay their debt if they are liquidatable, blocking them from protecting part of their collateral

### Summary

When a user's position is liquidatable a portion of their collateral is vulnerable to liquidation, this portion fully depends on the value of the position's debt, the higher the debt the higher collateral in danger. When a position is liquidatable all actions are blocked if the position "ends up liquidatable", this is done in:
* https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L231
* https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L245

In case the user is willing to repay a part of their debt, to minimize his collateral loss in liquidation, but the position will stay liquidatable afterward they're blocked from doing so. 

This unfairly puts a huge portion of the user's collateral in danger.

### Root Cause

In both `PositionManager::process` and `PositionManager::processBatch`, they're checking if, at the end of actions, a position is liquidatable, if so the TX reverts.

### Impact

Users are unable to protect a portion of their collateral, leaving them unfairly vulnerable to full liquidation.

### PoC

Replace the content of `protocol-v2/src/oracle/FixedPriceOracle.sol` with the following to allow manual price manipulation:
```solidity
contract FixedPriceOracle is IOracle {
    using Math for uint256;

    /// @notice Fixed price of the asset in ETH terms scaled to 18 decimals
    uint256 public PRICE;

    /// @param price Fixed price of the asset in ETH terms scaled to 18 decimals
    constructor(uint256 price) {
        PRICE = price;
    }

    /// @notice Compute the equivalent ETH value for a given amount of a particular asset
    /// @param asset Address of the asset to be priced
    /// @param amt Amount of the given asset to be priced
    /// @return valueInEth Equivalent ETH value for the given asset and amount, scaled by 18 decimals
    function getValueInEth(
        address asset,
        uint256 amt
    ) external view returns (uint256) {
        // [ROUND] price is rounded down. this is used for both debt and asset math, neutral effect
        // value = amt * price % asset.decimals()
        return amt.mulDiv(PRICE, (10 ** IERC20Metadata(asset).decimals()));
    }

    function setPrice(uint256 price) external {
        PRICE = price;
    }
}
```

Test:
```solidity
contract ContestTest3 is BaseTest {
    bytes32 LINEAR_RATE_MODEL_KEY =
        0x7922391f605f567c8e61c33be42b581e2f71019b5dce3c47110ad332b7dbd68c;

    Pool pool;
    address payable position;
    RiskEngine riskEngine;
    PositionManager positionManager;

    FixedPriceOracle asset1Oracle;
    FixedPriceOracle asset2Oracle;

    function setUp() public override {
        super.setUp();

        pool = protocol.pool();
        riskEngine = protocol.riskEngine();
        positionManager = protocol.positionManager();

        asset1Oracle = new FixedPriceOracle(1e18);
        asset2Oracle = new FixedPriceOracle(0.5e18);

        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset1), address(asset1Oracle));
        riskEngine.setOracle(address(asset2), address(asset2Oracle));
        vm.stopPrank();

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset2), 0.75e18);
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset2));
        vm.stopPrank();
    }

    function testCantRepayWhileLiquidatable() public {
        uint256 amount = 10 ether;

        deal(address(asset1), lender, amount);
        deal(address(asset2), user, amount);

        // Lender deposits asset1 into the pool
        vm.startPrank(lender);
        asset1.approve(address(pool), amount);
        pool.deposit(fixedRatePool, amount, lender);
        vm.stopPrank();

        // User creates a new position
        // User deposits asset2 into the position
        // User borrows 3 ETH of asset1 from the pool
        vm.startPrank(user);
        Action[] memory actions = new Action[](4);
        (position, actions[0]) = newPosition(
            user,
            bytes32(uint256(3_492_932_942))
        );
        actions[1] = addToken(address(asset2));
        actions[2] = deposit(address(asset2), amount);
        actions[3] = borrow(fixedRatePool, 3 ether);
        asset2.approve(address(positionManager), amount);
        PositionManager(positionManager).processBatch(position, actions);
        vm.stopPrank();

        // Verifying the position is healthy
        assertTrue(riskEngine.isPositionHealthy(position));

        // Price of asset2 drops
        asset2Oracle.setPrice(0.3e18);

        // Verifying the position is liquidatable
        assertFalse(riskEngine.isPositionHealthy(position));

        // User can't partially repay the loan
        vm.prank(user);
        vm.expectRevert(
            abi.encodeWithSelector(
                PositionManager.PositionManager_HealthCheckFailed.selector,
                position
            )
        );
        PositionManager(positionManager).process(
            position,
            Action({
                op: Operation.Repay,
                data: abi.encodePacked(fixedRatePool, uint256(0.5 ether))
            })
        );
    }
}
```

### Mitigation

Allow repayments even if the position even if the position is in a liquidatable state.