Tricky Felt Lizard

Medium

# Borrower can not repay his debt when PositionManager is paused but he can be liquidated and interest accrues.

### Summary

Owner pausing the PositionManager will lead to borrowers being forced to deposit already added assets since they can not repay their debt, while they can be liquidated.

### Root Cause

A borrower can only repay his Position debt by calling the ```repay()``` function of ```PositionManager``` contract. In order to do that, he must call ```proccess()``` or ```proccessBatch()``` function which, as we can see, both have the ```whenNotPaused``` modifier. As a result, it is impossible for a borrower to repay his debt when the ```PositionManager``` is paused. 
```solidity
    // ...
    function process(address position, Action calldata action) external nonReentrant whenNotPaused {
        // ...
    }

    // ...
    function processBatch(address position, Action[] calldata actions) external nonReentrant whenNotPaused {
        // ...
    }
```
[Link to code](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L229-L246)

We understand that if a borrower wants to eventually repay his debt, he must wait for the owner to unpause the ```PositionManager```. However, the interest on his Position can accrue and inflate his debt, while he can not do anything about it. In the meantime, anyone can ```liquidate()``` him since that function does not have the ```whenNotPaused``` modifier. In this case, the borrower is forced to either provide more collaterals or lose his Position from someone who is willing to liquidate him after his ```Position``` becomes unhealthy.

### Internal pre-conditions

1. Owner to pause the ```PositionManager``` due to an emergency or any other reason.

### External pre-conditions

1. Collateral value of the ```Position``` drops.
2. Liquidator willing to liquidate the Position and get collaterals on a discounted price.

### Attack Path

1. Borrower borrows. 
2. Owner pauses PositionManager.
3. Borrower unable to repay and interest accrues so he is forced to deposited already added collateral via direct transfer.
4. Liquidator liquidate the borrower if he stops.

### Impact

Borrowers are forced to support with collateral a Position that they actually want to repay since it can be liquidated while the interest accrues on it and increase the amount borrowed.

### PoC

You can add this test to ```LiquidationTest.t.sol``` and run ```forge test --mt testLiquidateWhenPaused``` :

```solidity
function testLiquidateWhenPaused() public {
        vm.startPrank(user);
        asset2.approve(address(positionManager), 1e18);

        // User borrows asset1 and asset2 from Pool.
        Action[] memory actions = new Action[](7);
        (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
        actions[1] = deposit(address(asset2), 1e18);
        actions[2] = addToken(address(asset2));
        actions[3] = borrow(fixedRatePool, 1e18);
        actions[4] = approve(address(mockswap), address(asset1), 1e18);
        bytes memory data = abi.encodeWithSelector(SWAP_FUNC_SELECTOR, address(asset1), address(asset3), 1e18);
        actions[5] = exec(address(mockswap), 0, data);
        actions[6] = addToken(address(asset3));
        positionManager.processBatch(position, actions);
        vm.stopPrank();

        // Something happens and the PositionManager be paused by the owner.
        vm.startPrank(positionManager.owner());
        positionManager.togglePause();
        vm.stopPrank();

        // Modify asset2 price from 1eth to 0.1eth so to make the Position not healthy.
        FixedPriceOracle pointOneEthOracle = new FixedPriceOracle(0.1e18);
        vm.prank(protocolOwner);
        riskEngine.setOracle(address(asset2), address(pointOneEthOracle));
        assertFalse(riskEngine.isPositionHealthy(position));

        // Now Position owner tries to repay but he can't since the PositionManager is paused.
        vm.startPrank(user);
        asset2.approve(address(positionManager), 10e18);
        bytes memory dataRepay = abi.encode(fixedRatePool, 10 ether);
        Action memory action = Action({ op: Operation.Repay, data: dataRepay });
        vm.expectRevert();
        positionManager.process(position, action);
        vm.stopPrank();

        // Construct liquidator data.
        DebtData memory debtData = DebtData({ poolId: fixedRatePool, amt: type(uint256).max });
        DebtData[] memory debts = new DebtData[](1);
        debts[0] = debtData;
        AssetData memory asset1Data = AssetData({ asset: address(asset3), amt: 1e18 });
        AssetData memory asset2Data = AssetData({ asset: address(asset2), amt: 1e18 });
        AssetData[] memory assets = new AssetData[](2);
        assets[0] = asset1Data;
        assets[1] = asset2Data;

        asset1.mint(liquidator, 10e18);
        vm.startPrank(liquidator);
        asset1.approve(address(positionManager), 1e18);
        vm.stopPrank();

        // Liquidator can easily liquidate him successfully.
        vm.startPrank(liquidator);
        asset1.approve(address(positionManager), 1e18);
        positionManager.liquidate(position, debts, assets);
        vm.stopPrank();
    }
```

### Mitigation

Allow repayments and collateral additions when PositionManager is paused.