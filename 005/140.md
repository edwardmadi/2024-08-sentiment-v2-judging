Massive Slate Dalmatian

Medium

# Liquidation is possible when the position manager is paused, leaving users vulnerable to unfair liquidations

### Summary

`PositionManager` is a pausable contract, when it is paused all user actions are blocked, this is done by the `whenNotPaused` modifier added for both `PositionManager::process` and `PositionManager::processBatch`.
However, `PositionManager::liquidate` is missing this modifier, allowing any user to liquidate "paused" positions, where position owners have to protect their collateral while having no way of repaying their debt.

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L430-L444

When asking the project sponsor about it, their reply was:
>everything else is paused because we have no way to determine what could be the vuln function or reason for pausing the PositionManager. the liquidate is left open to handle edge cases where a lot of bad debt accrues

According to the above, normal liquidation should be blocked when paused while allowing bad debt liquidation to handle edge cases, knowing that it will be carried out by the trusted owner.

This leaves users vulnerable to unfair liquidations. 

### Root Cause

`PositionManager::liquidate` is missing the `whenNotPaused` modifier.

### Attack Path

1. User takes a loan
2. `PositionManager` is paused for whatever reason
3. Collateral price drops
4. The user is liquidatable and has no way of protecting his collateral and repaying his debt
5. Any other user can come in and liquidate the borrower's position

### Impact

Borrowers will be vulnerable to unfair liquidation.

### PoC

```solidity
contract ContestTest3 is BaseTest {
    bytes32 LINEAR_RATE_MODEL_KEY =
        0x7922391f605f567c8e61c33be42b581e2f71019b5dce3c47110ad332b7dbd68c;

    Pool pool;
    address payable position;
    RiskEngine riskEngine;
    PositionManager positionManager;

    FixedPriceOracle asset1Oracle;
    FixedPriceOracle asset2Oracle;

    function setUp() public override {
        super.setUp();

        pool = protocol.pool();
        riskEngine = protocol.riskEngine();
        positionManager = protocol.positionManager();

        asset1Oracle = new FixedPriceOracle(1e18);
        asset2Oracle = new FixedPriceOracle(0.5e18);

        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset1), address(asset1Oracle));
        riskEngine.setOracle(address(asset2), address(asset2Oracle));
        vm.stopPrank();

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset2), 0.75e18);
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset2));
        vm.stopPrank();
    }

    function testCanLiquidateWhilePaused() public {
        uint256 amount = 10 ether;

        deal(address(asset1), lender, amount);
        deal(address(asset1), address(this), amount * 2);
        deal(address(asset2), user, amount);

        // Not paused
        assertFalse(PositionManager(positionManager).paused());

        // Lender deposits asset1 into the pool
        vm.startPrank(lender);
        asset1.approve(address(pool), amount);
        pool.deposit(fixedRatePool, amount, lender);
        vm.stopPrank();

        // User creates a new position
        // User deposits asset2 into the position
        // User borrows 3 ETH of asset1 from the pool
        vm.startPrank(user);
        Action[] memory actions = new Action[](4);
        (position, actions[0]) = newPosition(
            user,
            bytes32(uint256(3_492_932_942))
        );
        actions[1] = addToken(address(asset2));
        actions[2] = deposit(address(asset2), amount);
        actions[3] = borrow(fixedRatePool, 3 ether);
        asset2.approve(address(positionManager), amount);
        PositionManager(positionManager).processBatch(position, actions);
        vm.stopPrank();

        vm.prank(protocolOwner);
        PositionManager(positionManager).togglePause();

        assertTrue(PositionManager(positionManager).paused());

        // Verifying the position is healthy
        assertTrue(riskEngine.isPositionHealthy(position));

        // Price of asset2 drops
        asset2Oracle.setPrice(0.3e18);

        // Verifying the position is liquidatable
        assertFalse(riskEngine.isPositionHealthy(position));

        // User can't repay the loan
        vm.prank(user);
        vm.expectRevert(bytes("Pausable: paused"));
        PositionManager(positionManager).process(
            position,
            Action({
                op: Operation.Repay,
                data: abi.encodePacked(fixedRatePool, uint256(0.5 ether))
            })
        );

        // Position is still liquidatable
        asset1.approve(address(positionManager), amount * 2);

        DebtData[] memory debts = new DebtData[](1);
        debts[0] = DebtData({poolId: fixedRatePool, amt: type(uint256).max});
        AssetData[] memory assets = new AssetData[](1);
        assets[0] = AssetData({asset: address(asset2), amt: 3 ether});

        PositionManager(positionManager).liquidate(position, debts, assets);
    }
}
```

### Mitigation

Block normal liquidation when the position manager is paused, while still allowing bad debt liquidation.