High Carmine Rattlesnake

High

# Positions can be liquidated when protocol is paused

## Summary
When protocol is paused in [PosotionManager.sol](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol) the liquidations can still occur but the repayment for borrowers can't be done which will lead to user loss of funds.
## Vulnerability Detail
PoC:
First lets test if repayment can be made when `PositionManager` is paused. In the [PositionManager.t.sol](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/test/core/PositionManager.t.sol) paste the following test:
```js
    function testRepayWhenPositionManagerIsPausedWillRevert() public {
        testSimpleBorrow();


        // pause the position manager
        vm.startPrank(protocolOwner);
        positionManager.togglePause();
        assertTrue(positionManager.paused());


        bytes memory data = abi.encode(linearRatePool, 1 ether);

        Action memory action = Action({ op: Operation.Repay, data: data });
        Action[] memory actions = new Action[](1);
        actions[0] = action;

        // protocol should revert repayment when it is paused
        vm.expectRevert();
        PositionManager(positionManager).processBatch(position, actions);
        
    }
```
This test will pass, which mean that repaying when it is paused will not occur.
```terminal
[PASS] testRepayWhenPositionManagerIsPausedWillRevert() (gas: 560603)
Logs:
  Position Balance: 2000000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 26.88ms (4.67ms CPU time)
```

Now paste the following test in [LiquidationTest.t.sol](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/test/integration/LiquidationTest.t.sol) to see that liquidation can still happens when protocol is paused and borrowers are not able to repay their debt:
```js
function testLiquidateWhenProtocolIsPaused() public {
        vm.startPrank(user);
        asset2.approve(address(positionManager), 1e18);

        // deposit 1e18 asset2, borrow 1e18 asset1
        Action[] memory actions = new Action[](7);
        (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
        actions[1] = deposit(address(asset2), 1e18);
        actions[2] = addToken(address(asset2));
        actions[3] = borrow(fixedRatePool, 1e18);
        actions[4] = approve(address(mockswap), address(asset1), 1e18);
        bytes memory data = abi.encodeWithSelector(SWAP_FUNC_SELECTOR, address(asset1), address(asset3), 1e18);
        actions[5] = exec(address(mockswap), 0, data);
        actions[6] = addToken(address(asset3));
        positionManager.processBatch(position, actions);
        vm.stopPrank();
        assertTrue(riskEngine.isPositionHealthy(position));

        (uint256 totalAssetValue, uint256 totalDebtValue, uint256 minReqAssetValue) = riskEngine.getRiskData(position);

        assertEq(totalAssetValue, 2e18);
        assertEq(totalDebtValue, 1e18);
        assertEq(minReqAssetValue, 2e18);

        // construct liquidator data
        DebtData memory debtData = DebtData({ poolId: fixedRatePool, amt: type(uint256).max });
        DebtData[] memory debts = new DebtData[](1);
        debts[0] = debtData;
        AssetData memory asset1Data = AssetData({ asset: address(asset3), amt: 1e18 });
        AssetData memory asset2Data = AssetData({ asset: address(asset2), amt: 1e18 });
        AssetData[] memory assets = new AssetData[](2);
        assets[0] = asset1Data;
        assets[1] = asset2Data;

        // attempt to liquidate before price moves
        asset1.mint(liquidator, 10e18);
        vm.startPrank(liquidator);
        asset1.approve(address(positionManager), 1e18);
        vm.expectRevert(abi.encodeWithSelector(RiskModule.RiskModule_LiquidateHealthyPosition.selector, position));
        positionManager.liquidate(position, debts, assets);
        vm.stopPrank();

        // modify asset2 price from 1eth to 0.1eth
        FixedPriceOracle pointOneEthOracle = new FixedPriceOracle(0.1e18);
        vm.prank(protocolOwner);
        riskEngine.setOracle(address(asset2), address(pointOneEthOracle));
        assertFalse(riskEngine.isPositionHealthy(position));

        // pause the position manager
        vm.prank(protocolOwner);
        positionManager.togglePause();
        assertTrue(positionManager.paused());


        // liquidate
        vm.startPrank(liquidator);
        asset1.approve(address(positionManager), 1e18);
        positionManager.liquidate(position, debts, assets);
        vm.stopPrank();
    }
```
The following test will pass which mean that liquidation can happen even the protocol is paused. ( in both scenarios no matter if pause occur before or after position became unhealthy)
```terminal
[PASS] testLiquidateWhenProtocolIsPaused() (gas: 1263809)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 34.15ms (16.66ms CPU time)
```

## Impact
This will make borrowers be liquidated and their funds taken when they are not able to repay their debt, which will occur in loss of funds.
## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2-mrMorningstar96/blame/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L430
## Tool used

Manual Review

## Recommendation
Add `whenNotPaused` modifier to liquidation function to prevent users being liquidated when the protocol is paused and they are not able to repay their debt.
```diff
    function liquidate(
        address position,
        DebtData[] calldata debtData,
        AssetData[] calldata assetData
+    ) external nonReentrant whenNotPaused {
```
Also I would recommend to add grace period after protocol is unpaused again to give a chance to borrowers (who became liquidatable in period where protocol was paused) repay their debt otherwise they can be liquidated (if someone try to liquidate them before they tried to repay or front-run them).