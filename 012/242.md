Abundant Maroon Pangolin

Medium

# Temporary borrowing DOS due to incorrect stalePriceThreshold initialization

### Summary

Incorect `stalePriceThresholdFor[ethUsdFeed]` initialization  in `ChainlinkUsdOracle.sol`'s constructor will cause a temporary protocol DOS

### Root Cause

In `ChainlinkUsdOracle.sol:68` the [threshold](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/oracle/ChainlinkUsdOracle.sol#L68) is wrongly set for `ETH`  alias address instead for `ethUsdFeed`.
```solidity
    constructor(address owner, address arbSeqFeed, address ethUsdFeed, uint256 ethUsdThreshold) Ownable() {
        ARB_SEQ_FEED = IAggegregatorV3(arbSeqFeed);
        ETH_USD_FEED = IAggegregatorV3(ethUsdFeed);
        priceFeedFor[ETH] = ethUsdFeed;
        stalePriceThresholdFor[ETH] = ethUsdThreshold; // @audit threshold should be set for `ethUsdFeed` feed

        _transferOwnership(owner);
    }
```
The `stalePriceThresholdFor[ehtUsdFee]` remains 0. 


### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

1. Borrower deposit collateral in their position.
2.  Borrower call `PositionManager.process` with `borrow` action. When his position's health factor is verified, `ChainlinkUsdOracle.getValueInEth()` is invoked.
3. `stalePriceThresholdFor[ethUsdFeed]` is not set and will be 0.
4. `_getPriceWithSanityChecks` will verify if price is stale: because `stalePriceThresholdFor` for ethUsd feed is 0, the 
`if (updatedAt < block.timestamp - stalePriceThresholdFor[feed])` will almost always be true and tx reverts.
Pricefeed updates its onchain price according to its deviation and heartbeat settings. 
For Ethereum L1 these are 0.5% price deviation or it must pass 3600s since last price update. 

```solidity
    function _getPriceWithSanityChecks(address asset) private view returns (uint256) {
        address feed = priceFeedFor[asset];
        (, int256 price,, uint256 updatedAt,) = IAggegregatorV3(feed).latestRoundData();
        if (price <= 0) revert ChainlinkUsdOracle_NonPositivePrice(asset);
// @audit while stalePriceThresholdFor is not set for ETH/usd price feed
// this will  almost always reverts becasue prices are not updated in all blocks. 
        if (updatedAt < block.timestamp - stalePriceThresholdFor[feed]) revert ChainlinkUsdOracle_StalePrice(asset);
        return uint256(price);
    }
```


### Impact

The core protocol functionality  will be unusable until `owner` figures out what the problem is and correctly updates the stale price threshold for Eth/Usd feed. 

### PoC

_No response_

### Mitigation

Set the ethUsd stale price threshold in constructor: 
```diff
    constructor(address owner, address arbSeqFeed, address ethUsdFeed, uint256 ethUsdThreshold) Ownable() {
        ARB_SEQ_FEED = IAggegregatorV3(arbSeqFeed);
        ETH_USD_FEED = IAggegregatorV3(ethUsdFeed);
        priceFeedFor[ETH] = ethUsdFeed;
-        stalePriceThresholdFor[ETH] = ethUsdThreshold;
+        stalePriceThresholdFor[ethUsdFeed] = ethUsdThreshold;

        _transferOwnership(owner);
    }
```