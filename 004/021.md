Scrawny Blonde Guppy

Medium

# `ChainlinkUsdOracle` Incorrect Price Computation For Any Token Over 18 Decimals

## Summary

All prices returned from the [`ChainlinkUsdOracle`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/oracle/ChainlinkUsdOracle.sol) are incorrect for any asset over 18 decimals.

## Vulnerability Detail

The [`ChainlinkUsdOracle`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/oracle/ChainlinkUsdOracle.sol) uses [the following calculation](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/oracle/ChainlinkUsdOracle.sol#L84C9-L86C104) to determine an asset price denominated in ETH:

```solidity
// [ROUND] price is rounded down. this is used for both debt and asset math, no effect
if (decimals <= 18) return (amt * 10 ** (18 - decimals)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
else return (amt / (10 ** decimals - 18)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
```

This issue regards specifically for the line, which attempts to convert an `amt` of the specified asset into a corresponding amount of ether for assets over `18` decimals:

```solidity
else return (amt / (10 ** decimals - 18)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
```

This code is executed for all assets which have a decimal precision over `18`.

However, the sequence of operations is incorrect, **because we compute** `(10 ** decimals - 18)` **instead of** `(10 ** (decimals - 18))`. Due to the order of evaluation of arithmetic operators in Solidity, **the price is incorrectly calculated**.

We can confirm this in `chisel`.

For example, we may expect `10 ** decimals - 18` to return unity for a `decimals` of `18`, however:

```shell
Welcome to Chisel! Type `!help` to show available commands.
➜ 10 ** 18 - 18
Type: uint256
├ Hex: 0x0000000000000000000000000000000000000000000000000de0b6b3a763ffee
├ Hex (full word): 0x0000000000000000000000000000000000000000000000000de0b6b3a763ffee
└ Decimal: 999999999999999982
➜ 10 ** (18 - 18)
Type: uint256
├ Hex: 0x0000000000000000000000000000000000000000000000000000000000000001
├ Hex (full word): 0x0000000000000000000000000000000000000000000000000000000000000001
└ Decimal: 1
```

The exponent is calculated first. Consequently, implementation is completely broken for any asset over 18 decimals.

### Proof of Concept

Add the following file `SherlockTest.t.sol` to the `tests/` directory and execute using `forge test --match-test "testSherlockInvalidPriceCalculation" -vv`:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.24;

import {Test, console} from "forge-std/Test.sol";
import { Math } from "@openzeppelin/contracts/utils/math/Math.sol";

contract SherlockTest is Test {

    using Math for uint256;

    function testSherlockInvalidPriceCalculation() external {
        /// @dev Assuming the price of ETH is $3000:
        int256 ethUsdPrice = 3_000 * (10 ** 8); // _getPriceWithSanityChecks(ETH);
        /// @dev Assuming the price of a nominal asset is $1:
        int256 assetUsdPrice = 1 * (10 ** 8); // _getPriceWithSanityChecks(asset);
        /// @dev Assuming 18 decimals:
        uint256 decimals = 18; // IERC20Metadata(asset).decimals();
        /// @dev Let's assume we have $1 of the asset (1 nominal is 1 dollar):
        uint256 amt = 1 ether;

        /// @dev The price of one dollar in eth should be ($1 / $3000) = 0.000333333333333333 (0.000333333333333333 ether)
        uint256 priceInEth = (amt / (10 ** decimals - 18)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
        assertEq(priceInEth, 0); /// @audit Price is undervalued.

        uint256 correctedPriceInEth = (amt / (10 ** (decimals - 18))).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
        assertEq(correctedPriceInEth, 0.000333333333333333 ether); /// @audit Correct price is returned.
    }

}
```

Returns the output:

```shell
Ran 1 test for test/Sherlock.t.sol:SherlockTest
[PASS] testSherlockInvalidPriceCalculation() (gas: 4601)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 4.57ms (1.18ms CPU time)

Ran 1 test suite in 145.43ms (4.57ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

This confirms the price is calculated incorrectly.

## Impact

Any asset using over 18 decimal places is devalued by the oracle, resulting in an inability to price positions correctly, which could result in the unfair liquidations of solvent positions.

We have confirmed with the sponsor that assets over `18` decimals are considered within scope.

## Code Snippet

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/oracle/ChainlinkUsdOracle.sol#L84C9-L86C104

## Tool used

Manual Review

## Recommendation

```diff
   // [ROUND] price is rounded down. this is used for both debt and asset math, no effect
   if (decimals <= 18) return (amt * 10 ** (18 - decimals)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
-  else return (amt / (10 ** decimals - 18)).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
+  else return (amt / (10 ** (decimals - 18))).mulDiv(uint256(assetUsdPrice), uint256(ethUsdPrice));
```
