Hollow Smoke Manatee

High

# Incorrect price scaling in FixedPriceOracle::getValueInEth for tokens with more or less than 18 decimals

### Summary

The [`FixedPriceOracle::getValueInEth`](https://github.com/sentimentxyz/protocol-v2/blob/04bf15565165396608cc0aedacf05897235518fd/src/oracle/FixedPriceOracle.sol#L29) function contains a critical error when handling tokens with a different number of decimals than 18.
This error results in incorrect value calculations, returning a value significantly different than expected.

### Root Cause

The `FixedPriceOracle::getValueInEth` function does not correctly account for tokens with more or less than 18 decimals.
The current implementation incorrectly scales the `PRICE`, leading to an wrong value in ETH.

```solidity
   function getValueInEth(address asset, uint256 amt) external view returns (uint256) {
@>    return amt.mulDiv(PRICE, (10 ** IERC20Metadata(asset).decimals()));
   }
```

Indeed, the fixed `PRICE` of the asset is expressed "in ETH terms scaled to 18 decimals" and is therefore independent of the number of decimals the token has.
```solidity
/// @notice Fixed price of the asset in ETH terms scaled to 18 decimals
uint256 public immutable PRICE;
```

Then, the product of the amount and the price should be divided by `10^18` and not `10^decimals` 

### Impact

The impact of this vulnerability is severe as it can lead to collateral overvaluation: assets with fewer than 18 decimals (such as USDC & USDT) will be significantly overvalued, allowing users to borrow much more than they should against their collateral.
Moreover, positions that should be liquidated may appear healthy due to inflated collateral values.

Conversely, assets with more than 18 decimals will be undervalued, potentially preventing users from borrowing as much as they should be able to.


### PoC
The following PoC is a real case scenario of overvaluation when a user deposits a token with 6 decimals into a Superpool (such as USDC). It illustrates how the `getUserMultiDepositData()`  function returns the wrong valuation because of the scaling error in the `FixedPriceOracle` contract.

Copy the following test file in the test suite and run it with `forge test --mt testWrongValueInEthForFixedPriceOracleWithDifferentDecimals`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import "../BaseTest.t.sol";
import {console2} from "forge-std/console2.sol";
import {SuperPoolLens} from "src/lens/SuperPoolLens.sol";
import {FixedPriceOracle} from "src/oracle/FixedPriceOracle.sol";

import {console} from "forge-std/console.sol";

contract AuditTests is BaseTest {
    SuperPool public superPool1;
    SuperPool public superPool2;
    address[] public superPoolList;

    /*//////////////////////////////////////////////////////////////
                                SETUP 
    //////////////////////////////////////////////////////////////*/

    function setUp() public override {
        super.setUp();


         /*//////////////////////////////////////////////////////////////
                        DECLARE ASSETS AND PRICE ORACLE
        //////////////////////////////////////////////////////////////*/
        FixedPriceOracle oneEthOracle = new FixedPriceOracle(1e18);
        asset1 = new MockERC20("Asset1", "ASSET1", 18);
        asset2 = new MockERC20("Asset2", "ASSET2", 6);

        // Asset 2 is 6 decimals but same price as asset 1

        vm.startPrank(protocolOwner);
        protocol.positionManager().toggleKnownAsset(address(asset1));
        protocol.positionManager().toggleKnownAsset(address(asset2));
        protocol.riskEngine().setOracle(address(asset1), address(oneEthOracle)); // 1 asset1 = 1 eth
        protocol.riskEngine().setOracle(address(asset2), address(oneEthOracle)); // 1 asset2 = 1 eth
        vm.stopPrank();

        /*//////////////////////////////////////////////////////////////
                        INITIALIZE SUPERPOOLS FOR NEW ASSETS
        //////////////////////////////////////////////////////////////*/
        uint256 initialDepositAmt = 1e5;

        vm.prank(protocolOwner);
        asset1.mint(address(this), initialDepositAmt);
        asset1.approve(address(protocol.superPoolFactory()), initialDepositAmt);

        address feeTo = makeAddr("FeeTo");

        superPool1 = SuperPool(
            protocol.superPoolFactory().deploySuperPool(
                poolOwner, address(asset1), feeTo, 0.01 ether, 1_000_000 ether, initialDepositAmt, "TEST1", "TEST1"
            )
        );
        superPoolList.push(address(superPool1));

        vm.prank(protocolOwner);
        asset2.mint(address(this), initialDepositAmt);
        asset2.approve(address(protocol.superPoolFactory()), initialDepositAmt);

        superPool2 = SuperPool(
            protocol.superPoolFactory().deploySuperPool(
                poolOwner, address(asset2), feeTo, 0.01 ether, 1_000_000 ether, initialDepositAmt, "TEST2", "TEST2"
            )
        );
        superPoolList.push(address(superPool2));

        bytes32 FIXED_RATE_MODEL_KEY = 0xeba2c14de8b8ca05a15d7673453a0a3b315f122f56770b8bb643dc4bfbcf326b;

        fixedRatePool =
            protocol.pool().initializePool(poolOwner, address(asset1), type(uint128).max, FIXED_RATE_MODEL_KEY);
        alternateAssetPool =
            protocol.pool().initializePool(poolOwner, address(asset2), type(uint128).max, FIXED_RATE_MODEL_KEY);
    
        vm.startPrank(poolOwner);

        superPool1.addPool(fixedRatePool, 50 ether);
        superPool2.addPool(alternateAssetPool, 50 ether);

        vm.stopPrank();
    }

    function testWrongValueInEthForFixedPriceOracleWithDifferentDecimals() public {
        vm.startPrank(user);

        asset1.mint(user, 50 ether);
        asset1.approve(address(superPool1), 50 ether);
        superPool1.deposit(50 ether, user);

        asset2.mint(user, 50 ether);
        asset2.approve(address(superPool2), 50 ether);
        superPool2.deposit(50 ether, user);

        // Asset 1 price is $1e18, so asset value is 50 * 1e18 = 50e18
        // Asset 2 price is $1e18, so asset value is 50 * 1e18 = 50e18
        // Total value should be 50e18 + 50e18 = 100e18

        vm.stopPrank();

        SuperPoolLens.UserMultiDepositData
            memory userMultiDepositData = protocol
                .superPoolLens()
                .getUserMultiDepositData(user, superPoolList);

        assert(userMultiDepositData.totalValueInEth != uint256(100e18)); // Should be 100e18
        assertEq(userMultiDepositData.totalValueInEth, uint256(50e18 + 50e30));
    }
}
```


### Mitigation

Modify the `getValueInEth` function to correctly scale the `PRICE` down to 18 decimals before applying the price:
```diff
- function getValueInEth(address asset, uint256 amt) external view returns (uint256) {
+ function getValueInEth(address /* asset */, uint256 amt) external view returns (uint256) {
    // [ROUND] price is rounded down. this is used for both debt and asset math, neutral effect
    // value = amt * price % asset.decimals()
-   return amt.mulDiv(PRICE, (10 ** IERC20Metadata(asset).decimals()));
+   return amt.mulDiv(PRICE, 1e18);
}
```

This ensures correct valuation regardless of the token's decimal places. 