Scruffy Raspberry Gerbil

Medium

# Superpool owner can't pause deposits on Superpool

### Summary

The `Superpool.sol` contract inherits from Pausable contract and includes a `togglePause()` function, but does not use `whenNotPaused` or `whenPaused` anywhere in the contract. 

Protocol team confirmed in Discord PT that Superpool owner should be able to pause deposits just like Pool owner can, but because the `whenNotPaused` modifier isn't used on `_deposit()`, pausing is impossible.

### Root Cause

`whenNotPaused` modifier is not used on `_deposit()` function.

<https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L497-L506>

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

1. If a Superpool owner calls `togglePause()`, it does nothing.
2. Lenders can still call `deposit()` or `mint()`, which calls `_deposit()`, allowing them to deposit assets to Superpool even when Superpool owner intends to pause deposits. 

### Impact

Superpool owners cannot pause deposits like protocol intends.

### PoC

Create a new file under the `/protocol-v2/test` directory (e.g TogglePauseFailsTest.t.sol) and paste in the POC code below.

Run the following command:
`forge test --mt testPauseFailsOnSuperpool`

POC:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import "./BaseTest.t.sol";
import { MockERC20 } from "./mocks/MockERC20.sol";
import { OwnableUpgradeable } from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import { Action, Operation } from "../src/PositionManager.sol";
import { IOracle } from "../src/interfaces/IOracle.sol";
import { FixedPriceOracle } from "../src/oracle/FixedPriceOracle.sol";

contract PauseFailsOnSuperpoolTest is BaseTest {
     
    bytes32 public constant SENTIMENT_POOL_KEY = 0x1a99cbf6006db18a0e08427ff11db78f3ea1054bc5b9d48122aae8d206c09728;
    bytes32 public constant SENTIMENT_RISK_ENGINE_KEY = 0x5b6696788621a5d6b5e3b02a69896b9dd824ebf1631584f038a393c29b6d7555;
    bytes32 public constant SENTIMENT_POSITION_BEACON_KEY =
        0x6e7384c78b0e09fb848f35d00a7b14fc1ad10ae9b10117368146c0e09b6f2fa2;

    Pool pool;
    Registry registry;
    address payable position;
    RiskEngine riskEngine;
    PositionManager positionManager;

    uint256 initialDepositAmt = 1e5;
    SuperPool superPool;
    SuperPoolFactory superPoolFactory;
    address public feeTo = makeAddr("FeeTo");

    function setUp() public override {
        super.setUp();
        pool = protocol.pool();
        registry = protocol.registry();
        riskEngine = protocol.riskEngine();
        positionManager = protocol.positionManager();

        // Deploy Superpool
        superPoolFactory = protocol.superPoolFactory();
        vm.prank(protocolOwner);
        asset1.mint(address(this), initialDepositAmt);
        asset1.approve(address(superPoolFactory), initialDepositAmt);
        superPool = SuperPool(
            superPoolFactory.deploySuperPool(
                poolOwner, address(asset1), feeTo, 0.01 ether, 1_000_000 ether, initialDepositAmt, "test", "test"
            )
        );
    }

    function testPauseFailsOnSuperpool() public {

        // 1. Superpool calls togglePause
        vm.startPrank(poolOwner);
        superPool.togglePause();
        vm.stopPrank();

        assertEq(superPool.paused(), true);

        // 2. Lender deposits 10e18 of asset1 to SuperPool
        vm.startPrank(lender);
        asset1.mint(lender, 100e18);
        asset1.approve(address(superPool), 10e18);
        superPool.deposit(10e18, lender);
        vm.stopPrank();

        assertEq(asset1.balanceOf(address(superPool)), 10e18 + initialDepositAmt);
    }
}
```

### Mitigation

Add `whenNotPaused` modifier to `_deposit()`.