Loud Rosewood Platypus

High

# Borrowers Cannot Fully Utilize Collateral Due to Exclusion of Borrowed Assets in Health Calculations

### Summary

In the current implementation, borrowed assets are not recognized as part of a user's position assets when calculating the health of a position using the `isPositionHealthy` function. This limitation restricts users from borrowing up to their collateral value multiplied by the Loan-to-Value (LTV) ratio if the brrowing process is done without immediately swapping the borrowed asset to another in a batch process.


### Root Cause

The core issue stems from the `RiskModule::isPositionHealthy` function's methodology for assessing the health of a user's position. This function calculates the minimum required asset value necessary for a position to remain healthy by considering both the position's debt and assets. However, it excludes borrowed assets from being counted towards the total asset value because these assets cannot have their Loan-to-Value (LTV) ratio set within the system. This exclusion arises due to the design of the `RiskEngine::requestLtvUpdate` function, which prevents setting an LTV for assets that are being lent out by the pool, under the rationale that positions cannot borrow against the same asset they are lending out. As a result, users are unable to add borrowed assets as position assets through an `addToken Action` using the PositionManager contract, leading to these assets not being considered in the health assessment calculations. This limitation forces users to immediately swap borrowed assets to another asset in a batch process if they wish to borrow up to their collateral value multiplied by the LTV ratio, thereby restricting borrowing flexibility and potentially leading to unfair liquidations.

The code snippet from the `isPositionHealthy` [function](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/RiskModule.sol#L67) illustrates how the function retrieves position asset data and calculates the minimum required asset value for the position to stay healthy. However, it does not account for borrowed assets in this calculation:
```javascript
function isPositionHealthy(address position) public view returns (bool) {

        (
            uint256 totalDebtValue,
            uint256[] memory debtPools,
            uint256[] memory debtValueForPool
        ) = _getPositionDebtData(position);
        if (totalDebtValue == 0) return true; // (zero debt, zero assets) AND (zero debt, non-zero assets)

@>      (
@>          uint256 totalAssetValue,
@>          address[] memory positionAssets,
@>          uint256[] memory positionAssetWeight
@>      ) = _getPositionAssetData(position);
@>      if (totalAssetValue == 0) return false; // (non-zero debt, zero assets)

        uint256 minReqAssetValue = _getMinReqAssetValue(
            debtPools,
            debtValueForPool,
            positionAssets,
            positionAssetWeight,
            position
        );
        return totalAssetValue >= minReqAssetValue; // (non-zero debt, non-zero assets)
    }
```
Here It is demonstrated that if the ltv is zero for an asset the `_getMinReqAssetValue` will revert.
```javascript
    function _getMinReqAssetValue(
        uint256[] memory debtPools,
        uint256[] memory debtValuleForPool,
        address[] memory positionAssets,
        uint256[] memory wt,
        address position
    ) internal view returns (uint256) {
        uint256 minReqAssetValue;

        // O(pools.len * positionAssets.len)
        uint256 debtPoolsLength = debtPools.length;
        uint256 positionAssetsLength = positionAssets.length;
        for (uint256 i; i < debtPoolsLength; ++i) {
            for (uint256 j; j < positionAssetsLength; ++j) {
                uint256 ltv = riskEngine.ltvFor(
                    debtPools[i],
                    positionAssets[j]
                );

                // revert with pool id and the asset that is not supported by the pool
@>              if (ltv == 0)
@>                  revert RiskModule_UnsupportedAsset(
@>                      position,
@>                      debtPools[i],
@>                      positionAssets[j]
@>                  );

                // debt is weighted in proportion to value of position assets. if your position
                // consists of 60% A and 40% B, then 60% of the debt is assigned to be backed by A
                // and 40% by B. this is iteratively computed for each pool the position borrows from
                minReqAssetValue += debtValuleForPool[i].mulDiv(
                    wt[j],
                    ltv,
                    Math.Rounding.Up
                );
            }
        }

        if (minReqAssetValue == 0) revert RiskModule_ZeroMinReqAssets();
        return minReqAssetValue;
    }
```
Furthermore, the `RiskEngine::requestLtvUpdate` function enforces that an asset being lent out by the pool cannot have its LTV updated, as shown in the following code snippet:
```javascript
    function requestLtvUpdate(
        uint256 poolId,
        address asset,
        uint256 ltv
    ) external {
        if (msg.sender != pool.ownerOf(poolId))
            revert RiskEngine_OnlyPoolOwner(poolId, msg.sender);

        // set oracle before ltv so risk modules don't have to explicitly check if an oracle exists
        if (oracleFor[asset] == address(0))
            revert RiskEngine_NoOracleFound(asset);

        // ensure new ltv is within global limits. also enforces that an existing ltv cannot be updated to zero
        if (ltv < minLtv || ltv > maxLtv)
            revert RiskEngine_LtvLimitBreached(ltv);

        // Positions cannot borrow against the same asset that is being lent out
@>      if (pool.getPoolAssetFor(poolId) == asset)
@>          revert RiskEngine_CannotBorrowPoolAsset(poolId);

        LtvUpdate memory ltvUpdate;
        // only modification of previously set ltvs require a timelock
        if (ltvFor[poolId][asset] == 0)
            ltvUpdate = LtvUpdate({ltv: ltv, validAfter: block.timestamp});
        else
            ltvUpdate = LtvUpdate({
                ltv: ltv,
                validAfter: block.timestamp + TIMELOCK_DURATION
            });

        ltvUpdateFor[poolId][asset] = ltvUpdate;

        emit LtvUpdateRequested(poolId, asset, ltvUpdate);
    }
```
This enforcement means that users are prevented from adding borrowed assets as position assets through an `addToken Action` using the PositionManager contract, leading to these assets not being considered in the health assessment calculations. Consequently, users are compelled to immediately swap borrowed assets to another asset in a batch process if they wish to borrow up to their collateral value multiplied by the LTV ratio. This limitation not only restricts borrowing flexibility but also potentially leads to unfair liquidations.
Also note that the current tests failed to catch this because in all cases swap is made from token1 (borrowed token) to token3 immidietly.


### Internal pre-conditions

The issue arises under specific conditions within the system's operational parameters:

- **User Actions:** The user must initiate a borrowing action, intending to utilize the platform's borrowing facilities.
- **Immediate Swapping Post-Borrowing:** The borrowed asset is not immediately swapped for another asset after the borrowing transaction. This condition is crucial because the current system design requires users to engage in immediate asset swaps to circumvent the limitation on borrowing up to the collateral value multiplied by the LTV ratio without considering borrowed assets in health calculations.
- **System Logic Excluding Borrowed Assets:** The system's logic, as currently implemented, excludes borrowed assets from being considered in the position's total asset value when assessing the health of the position. This exclusion is a fundamental aspect of the issue, as it directly leads to the observed limitations in borrowing flexibility and potential for unfair liquidations.

These internal preconditions highlight the specific operational and logical aspects of the system that contribute to the issue, providing a clearer understanding of the mechanisms at play and guiding efforts towards developing targeted solutions.


### External pre-conditions

none

### Attack Path

none

### Impact

The exclusion of borrowed assets from position health calculations has profound implications for users and the platform:

- **Borrowing Limitations:** Users are constrained in their ability to borrow up to their collateral value multiplied by the LTV ratio without engaging in immediate asset swaps. This limitation hampers strategic borrowing and limits the flexibility users have in managing their positions.

- **Unfair Liquidations:** Positions that should technically remain healthy, given the inclusion of borrowed assets in the calculation, may be incorrectly flagged for liquidation. For instance, consider a scenario where a user has collateral worth 1000 ethers and borrows 750 ethers of another asset. Assuming both assets have the same price and the LTV for the collateral is 0.75, the position remains healthy as the borrowed amount does not exceed the collateral value times the LTV. However, over time, interest accrues on the borrowed amount, increasing it slightly. Despite the user not leveraging beyond the initial conditions, the position could be deemed unhealthy solely because the borrowed assets are not counted towards the total asset value, leading to potential liquidation and loss of funds for the user.

These scenarios underscore the critical need for a revision in how borrowed assets are treated in position health assessments to ensure fair treatment of users and maintain trust in the platform's mechanisms.

### PoC

Three test cases demonstrate the issue and its implications:
1. **Test Case 1 - Leverage Calculation Error:** This test case illustrates a scenario where a position with sufficient collateral becomes unhealthy due to accrued interest on borrowed assets, despite not utilizing full leverage. The position is incorrectly considered for liquidation, highlighting the unfair treatment of borrowed assets in health assessments.

```javascript
function testLeverageIsWrong() public {

        //Lp provides
        vm.startPrank(lender);
        asset1.mint(lender, 1000 ether);
        asset1.approve(address(pool), 1000 ether);
        pool.deposit(fixedRatePool, 1000 ether, lender);
        assertEq(pool.getAssetsOf(fixedRatePool, lender), 1000 ether);
        vm.stopPrank();
        //user provides to pool 2
        vm.startPrank(user);
        asset1.mint(user, 1000 ether);
        asset1.approve(address(pool), 1000 ether);
        pool.deposit(fixedRatePool, 1000 ether, user);
        assertEq(pool.getAssetsOf(fixedRatePool, user), 1000 ether);
        vm.stopPrank();

        //pool 1 utilization rate goes to up
        uint256 borrowAmount = 750 ether; //asset1
        uint256 collateralAmount = 1000 ether; //asset2

        vm.startPrank(user2);
        asset2.mint(user2, collateralAmount);
        asset2.approve(address(positionManager), collateralAmount);

        // Make a new position
        (address position, Action memory _newPosition) = newPosition(
            user2,
            "test"
        );
        positionManager.process(position, _newPosition);
        Action memory addNewCollateral = addToken(address(asset2));
        Action memory depositCollateral = deposit(
            address(asset2),
            collateralAmount
        );
        Action memory borrowAct = borrow(fixedRatePool, borrowAmount);
        // Action memory addAsset3 = addToken(address(asset1));

        Action[] memory actions = new Action[](3);
        actions[0] = addNewCollateral;
        actions[1] = depositCollateral;
        // actions[2] = addAsset3;
        actions[2] = borrowAct;

        positionManager.processBatch(position, actions);
        vm.stopPrank();
        assertEq(riskEngine.isPositionHealthy(position), true);
        uint256 borrows = pool.getBorrowsOf(fixedRatePool, position);
        console2.log(borrows);

        vm.warp(block.timestamp + 30 days);
        vm.roll(block.number + 1);
        // position is not healthy althogh only 811 ethers of 1000 ethers of collateral is used,
        assertEq(riskEngine.isPositionHealthy(position), false);
        borrows = pool.getBorrowsOf(fixedRatePool, position);
        console2.log(borrows);
        assert(borrows < collateralAmount);
    }
```
2. **Test Case 2 - Addition of Pool Asset as Position Asset:** This test attempts to add a pool asset as a position asset and subsequently borrow against it, demonstrating the system's restriction against setting LTV for borrowed assets and the inability to count these assets towards position health.
```javascript
    function testMultiplePoolsWithdrawBug() public {
        //Lp provides
        vm.startPrank(lender);
        asset1.mint(lender, 1000 ether);
        asset1.approve(address(pool), 1000 ether);
        pool.deposit(fixedRatePool, 1000 ether, lender);
        assertEq(pool.getAssetsOf(fixedRatePool, lender), 1000 ether);
        vm.stopPrank();
        //user provides to pool 2
        vm.startPrank(user);
        asset1.mint(user, 1000 ether);
        asset1.approve(address(pool), 1000 ether);
        pool.deposit(fixedRatePool, 1000 ether, user);
        assertEq(pool.getAssetsOf(fixedRatePool, user), 1000 ether);
        vm.stopPrank();

        //pool 1 utilization rate goes to up
        uint256 borrowAmount = 750 ether; //asset1
        uint256 collateralAmount = 1000 ether; //asset2

        vm.startPrank(user2);
        asset2.mint(user2, collateralAmount);
        asset2.approve(address(positionManager), collateralAmount);

        // Make a new position
        (address position, Action memory _newPosition) = newPosition(
            user2,
            "test"
        );
        positionManager.process(position, _newPosition);
        Action memory addNewCollateral = addToken(address(asset2));
        Action memory depositCollateral = deposit(
            address(asset2),
            collateralAmount
        );
        Action memory borrowAct = borrow(fixedRatePool, borrowAmount);
        Action memory addAsset3 = addToken(address(asset1));

        Action[] memory actions = new Action[](4);
        actions[0] = addNewCollateral;
        actions[1] = depositCollateral;
        actions[2] = addAsset3;
        actions[3] = borrowAct;

        //process fails because tvl of asset1 is 0
        vm.expectRevert(
            abi.encodeWithSelector(
                RiskModule.RiskModule_UnsupportedAsset.selector,
                position,
                fixedRatePool,
                asset1
            )
        );
        positionManager.processBatch(position, actions);
        vm.stopPrank();

        //And if you try to add ltv to token1:
        vm.startPrank(poolOwner);
        vm.expectRevert(
            abi.encodeWithSelector(
                RiskEngine.RiskEngine_CannotBorrowPoolAsset.selector,
                fixedRatePool
            )
        );
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset1), 0.75e18);

    }
```
3. **Test Case 3 - Borrowing Limit Restriction:** This test case demonstrates the limitation users face when attempting to borrow more than 750 ethers without immediately swapping the borrowed asset to another. This scenario highlights the practical implications of the issue on users' borrowing capabilities and strategic flexibility.
```javascript
    function testMultiplePoolsWithdrawBug() public {
        //Lp provides
        vm.startPrank(lender);
        asset1.mint(lender, 1000 ether);
        asset1.approve(address(pool), 1000 ether);
        pool.deposit(fixedRatePool, 1000 ether, lender);
        assertEq(pool.getAssetsOf(fixedRatePool, lender), 1000 ether);
        vm.stopPrank();
        //user provides to pool 2
        vm.startPrank(user);
        asset1.mint(user, 1000 ether);
        asset1.approve(address(pool), 1000 ether);
        pool.deposit(fixedRatePool, 1000 ether, user);
        assertEq(pool.getAssetsOf(fixedRatePool, user), 1000 ether);
        vm.stopPrank();

        //pool 1 utilization rate goes to up
        uint256 borrowAmount = 750 ether; //asset1
        uint256 collateralAmount = 1000 ether; //asset2

        vm.startPrank(user2);
        asset2.mint(user2, collateralAmount);
        asset2.approve(address(positionManager), collateralAmount);

        // Make a new position
        (address position, Action memory _newPosition) = newPosition(
            user2,
            "test"
        );
        positionManager.process(position, _newPosition);
        Action memory addNewCollateral = addToken(address(asset2));
        Action memory depositCollateral = deposit(
            address(asset2),
            collateralAmount
        );
        Action memory borrowAct = borrow(fixedRatePool, borrowAmount + 1); // 1 wei above 750 ethers

        Action[] memory actions = new Action[](3);
        actions[0] = addNewCollateral;
        actions[1] = depositCollateral;
        actions[2] = borrowAct;

        //
        vm.expectRevert(
            abi.encodeWithSelector(
                PositionManager.PositionManager_HealthCheckFailed.selector,
                position
            )
        );
        positionManager.processBatch(position, actions);
        vm.stopPrank();

    }
```


### Mitigation

To address this issue comprehensively, the `isPositionHealthy` function should be revised to include borrowed assets in the calculation of total position assets. Additionally, the system should allow for dynamic adjustments of LTV ratios for borrowed assets, enabling users to manage their positions more effectively and strategically. By implementing these changes, the platform can enhance user experience, promote fair treatment of borrowers, and ensure the robustness of its risk management mechanisms.
