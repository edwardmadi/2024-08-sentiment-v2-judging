Rhythmic Cherry Starfish

High

# If the last `pool.totalDepositAssets` are rebalanced in a Base Pool, the next depositor will lose a portion of their deposit

## Summary

If positions liquidated through the `liquidateBadDebt()` flow rebalance the last `pool.totalDepositAssets` in a pool, then a problematic pool state is created. The next depositor is guaranteed to instantly lose a portion of their deposit. The bad debt socialized through the `liquidateBadDebt()` is essentially also socialized to the next depositor who was not part of the pool when the bad debt was incurred. 


## Vulnerability Detail

When a 'position' becomes undercollateralized, the protocol admin can liquidate this bad debt via a call to [`PositionManager::liquidateBadDebt()`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L446-L464) which in turn calls `Pool::rebalanceBadDebt()`. As shown below, the socialization of liquidated bad debt is executed by reducing the number of depositAssets in a base pool without reducing the number of depositShares in a base pool:

```javascript
    function rebalanceBadDebt(uint256 poolId, address position) external {
        ... SKIP!...

        // compute pool and position debt in shares and assets
        uint256 totalBorrowShares = pool.totalBorrowShares;
        uint256 totalBorrowAssets = pool.totalBorrowAssets;
        uint256 borrowShares = borrowSharesOf[poolId][position];
        // [ROUND] round up against lenders
        uint256 borrowAssets = _convertToAssets(borrowShares, totalBorrowAssets, totalBorrowShares, Math.Rounding.Up);

        // rebalance bad debt across lenders
        pool.totalBorrowShares = totalBorrowShares - borrowShares;
        // handle borrowAssets being rounded up to be greater than totalBorrowAssets
        pool.totalBorrowAssets = (totalBorrowAssets > borrowAssets) ? totalBorrowAssets - borrowAssets : 0;
        uint256 totalDepositAssets = pool.totalDepositAssets;
@>      pool.totalDepositAssets = (totalDepositAssets > borrowAssets) ? totalDepositAssets - borrowAssets : 0;
        borrowSharesOf[poolId][position] = 0;
    }
```


Usually this works as the remaining shares are correctly devalued, however when it's the last liquidity in the pool it creates a problematic pool state. The pool will have 0 `totalDepositAssets` and nonzero `totalDepositShares`. Due to having 0 `totalDepositAssets` the next depositor will mint deposit shares at a 1:1 ratio as shown below:

```javascript
    function deposit(uint256 poolId, uint256 assets, address receiver) public returns (uint256 shares) {
        ... SKIP!...

@>      shares = _convertToShares(assets, pool.totalDepositAssets, pool.totalDepositShares, Math.Rounding.Down);
        
        ... SKIP!...
    }

    function _convertToShares(
        uint256 assets,
        uint256 totalAssets,
        uint256 totalShares,
        Math.Rounding rounding
    ) internal pure returns (uint256 shares) {
@>      if (totalAssets == 0) return assets;
        shares = assets.mulDiv(totalShares, totalAssets, rounding);
    }

```

As soon as these shares are minted, they're worth less than the deposit value due to the latent shares in the pool with no corresponding assets. The user who deposited will not be able to withdraw their full deposit.



The preconditions for this issue are:
- A base pool having 100% utilization
- Decline in collateral value (this need not be a large decline for assets with high LTV)
- All positions on that pool liquidated through the `liquidateBadDebt()` flow

I believe this is high severity because it will cause definite loss of funds to the next depositor, without extensive limitations due to the folowing design principles:
1. Pools can be created permissionlessly so liquidity is fragmented, it wouldn't be uncommon for an entire pool's liquidity to be loaned to a single position.
2. Excess deposits in a pool would likely be removed and re-allocated to ensure the utilization of a pool is high which results in higher interest rates for depositors (under linear and kinked IRM models). 
3. The maximum LTV is 98% which provides little room for a standard liquidation, ie. a single price update where collateral declines ~3% could take a position from a healthy state to an undercollateralized state.
4. Even when multiple positions have loans from a pool:
   - the risk parameters are shared as LTV is set by the pool, and
   - due to a whitelisting of collateral assets, if the market declines, it's likely multiple positions will be undercollateralized at the same time


## Impact

- The next depositor into this pool lose funds proportional to the size of their deposit relative to the amount of remaining deposit shares in the pool before their deposit, ie. as shown in the coded POC below, if a pool has 0 assets and 1e18 shares, the next deposit of 1e18 assets causes the depositor to lose 50% of their funds.
- Bad debt which gets rebalanced is socialised to future depositors, even though they did not have a deposit in the pool when the bad debt was liquidated.
- The above losses can extend to SuperPool depositors if the pool is subsequently added to a SuperPool deposit queue (loss will be among all SuperPool share holders).
- Trying to 'fix' the pool by depositing a small value such as 1 wei could have unintended consequences elsewhere in the system, such as rounding. For example if a pool has 0 assets, and 1e18 shares, and a user deposits 1 wei minting 1 share, the next deposit of 1e18 assets will mint > 1e36 shares. 


## Code Snippet

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/Pool.sol#L528-L549
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L446-L464
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/Pool.sol#L309-L331
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/Pool.sol#L275-L283


## POC

Paste the below coded POC into LiquidateTest.t.sol. 

It shows the following scenario:
1. User deposits into pool
2. User2 opens position takes debt from pool equal to available liquidity
3. Position becomes undercollateralized due to decline in collateral price
4. Protocol liquidates bad debt
5. Original user has shares in pool but no assets exist, this is by design because pool depositors are exposed to the risk of bad debt
6. User3 deposits into pool and instantly loses funds

```javascript
    function test_POC_LastPoolLiquidityRebalanced() public {
        // 0. Setup pool -> we're using LinearRatePool as there is no existing deposits in setup.
        vm.startPrank(poolOwner); 
        riskEngine.requestLtvUpdate(linearRatePool, address(asset2), 0.9e18); 
        riskEngine.acceptLtvUpdate(linearRatePool, address(asset2)); // Need to set LTV so asset2 is accepted as collateral
        vm.stopPrank();

        // 1. User deposits into pool
        vm.startPrank(user);
        asset1.mint(user, 1e18);
        asset1.approve(address(pool), 1e18);
        pool.deposit(linearRatePool, 1e18, user);
        vm.stopPrank();

        // 2. Position takes debt from pool equal to available liquidity
        vm.startPrank(user2);
        asset2.mint(user2, 2e18);
        asset2.approve(address(positionManager), 1.5e18);
        
        Action[] memory actions = new Action[](4);
        (position, actions[0]) = newPosition(user2, bytes32(uint256(0x123456789)));
        actions[1] = deposit(address(asset2), 1.5e18);
        actions[2] = addToken(address(asset2));
        actions[3] = borrow(linearRatePool, 1e18);
        positionManager.processBatch(position, actions);
        assertTrue(riskEngine.isPositionHealthy(position));
        vm.stopPrank();

        // 3. Position becomes undercollateralized due to decline in collateral price
        FixedPriceOracle pointOneEthOracle = new FixedPriceOracle(0.5e18);
        vm.prank(protocolOwner);
        riskEngine.setOracle(address(asset2), address(pointOneEthOracle));
        riskEngine.validateBadDebt(position);
        vm.stopPrank();

        // 4. Protocol liquidates bad debt
        vm.startPrank(address(protocolOwner));
        positionManager.liquidateBadDebt(position);
        vm.stopPrank();

        // 5. User1 has shares in pool but no assets exist, this is by design because pool depositors are exposed to the risk of bad debt
        (,,,,,,,,, uint256 totalDepositAssets, uint256 totalDepositShares) = pool.poolDataFor(linearRatePool);
        uint256 user1Shares = pool.balanceOf(user, linearRatePool);
        assertEq(totalDepositAssets, 0);
        assertEq(totalDepositShares, 1e18); 
        assertEq(user1Shares, 1e18);

        // 6. User3 deposits into pool and instantly loses funds
        address victim;
        uint256 victimDeposit = 1e18;
        vm.startPrank(victim);
        asset1.mint(victim, victimDeposit);
        asset1.approve(address(pool), victimDeposit);
        pool.deposit(linearRatePool, victimDeposit, victim);
        vm.stopPrank();

        assertEq(pool.getAssetsOf(linearRatePool, victim), victimDeposit / 2); // Victim deposited 1e18 and instantly lost half the funds
    
    }
```

## Tool used

Manual Review

## Recommendation

The following recommended fix assumes the protocol wishes to retain the design where all assets on a bad debt position are sent to the protocol multisig/goverance wallet.

In the `rebalanceBadDebt()` function, implement to check if the last deposit in the pool was liquidated. If so, transfer the pool ownership to protocol admin and pause the pool.

```diff
    function setPoolOwner(uint256 poolId, address newOwner) external {
-       if (msg.sender != ownerOf[poolId]) revert Pool_OnlyPoolOwner(poolId, msg.sender);
+       if (msg.sender != ownerOf[poolId] && msg.sender != owner()) revert Pool_OnlyPoolOwner(poolId, msg.sender); // Allow the protocol owner to set the owner of a pool
        // address(0) cannot own pools since it is used to denote uninitalized pools
        if (newOwner == address(0)) revert Pool_ZeroAddressOwner();
        ownerOf[poolId] = newOwner;
        emit PoolOwnerSet(poolId, newOwner);
    }


    function rebalanceBadDebt(uint256 poolId, address position) external {
        PoolData storage pool = poolDataFor[poolId];
        accrue(pool, poolId);


        // revert if the caller is not the position manager
        if (msg.sender != positionManager) revert Pool_OnlyPositionManager(poolId, msg.sender);


        // compute pool and position debt in shares and assets
        uint256 totalBorrowShares = pool.totalBorrowShares;
        uint256 totalBorrowAssets = pool.totalBorrowAssets;
        uint256 borrowShares = borrowSharesOf[poolId][position];
        // [ROUND] round up against lenders
        uint256 borrowAssets = _convertToAssets(borrowShares, totalBorrowAssets, totalBorrowShares, Math.Rounding.Up);


        // rebalance bad debt across lenders
        pool.totalBorrowShares = totalBorrowShares - borrowShares;
        // handle borrowAssets being rounded up to be greater than totalBorrowAssets
        pool.totalBorrowAssets = (totalBorrowAssets > borrowAssets) ? totalBorrowAssets - borrowAssets : 0;
        uint256 totalDepositAssets = pool.totalDepositAssets;
-       pool.totalDepositAssets = (totalDepositAssets > borrowAssets) ? totalDepositAssets - borrowAssets : 0;
+       if (totalDepositAssets - borrowAssets > 0) {
+           pool.totalDepositAssets = totalDepositAssets - borrowAssets;
+       }
+       else {
+           pool.totalDepositAssets = 0;
+           setPoolOwner(poolId, msg.sender); // Assumes the owner of PositionManager == owner of Pool == trusted admin
+           togglePause(poolId); // Pause the pool to protect future depositors
+       }
        borrowSharesOf[poolId][position] = 0;
    }
```

- Alternatively an admin can deposit a small amount to the pool, however there may be downstream precision issues if `totalDepositShares` >> `totalDepositAssets`