Smooth Opal Chameleon

High

# The function `liquidateBadDebt` transferring the collaterals to the `owner()` without repaying assets will cause losses to `debtPools`.

## Summary
Issue High: The function `liquidateBadDebt` transferring the collaterals to the `owner()` without repaying assets will cause losses to `debtPools`.

## Vulnerability Detail

In the contract `PositionManager.sol`, the function `liquidateBadDebt` will be called by the owner of `PositionManager` to liquidate positions with bad debts. During the liquidation process, the collaterals of the position will be transferred to the owner of the `PositionManager` without repaying assets to `debtPools`. This is wrong, since the liquidators can not take all the collaterals without repaying assets. This will lead to losses to `debtPools`.

[PositionManager](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L446-L464)

```solidity
    function liquidateBadDebt(address position) external onlyOwner {
        riskEngine.validateBadDebt(position);

        // transfer any remaining position assets to the PositionManager owner
        address[] memory positionAssets = Position(payable(position)).getPositionAssets();
        uint256 positionAssetsLength = positionAssets.length;
        for (uint256 i; i < positionAssetsLength; ++i) {
            uint256 amt = IERC20(positionAssets[i]).balanceOf(position);
            try Position(payable(position)).transfer(owner(), positionAssets[i], amt) { } catch { }
        }

        // clear all debt associated with the given position
        uint256[] memory debtPools = Position(payable(position)).getDebtPools();
        uint256 debtPoolsLength = debtPools.length;
        for (uint256 i; i < debtPoolsLength; ++i) {
            pool.rebalanceBadDebt(debtPools[i], position);
            Position(payable(position)).repay(debtPools[i], type(uint256).max);
        }
    }
```

As a correct reference, the function `liquidate` invokes the function `_repayPositionDebt` to transfer repaid to the `debtPools`.

[PositionManager](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L430-L444)

```solidity
    function liquidate(
        address position,
        DebtData[] calldata debtData,
        AssetData[] calldata assetData
    ) external nonReentrant {
        riskEngine.validateLiquidation(position, debtData, assetData);

        
        // liquidate
        _transferAssetsToLiquidator(position, assetData);
        _repayPositionDebt(position, debtData);

        // position should be within risk thresholds after liquidation
        if (!riskEngine.isPositionHealthy(position)) revert PositionManager_HealthCheckFailed(position);
        emit Liquidation(position, msg.sender, ownerOf[position]);
    }
```

[PositionManager](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L484-L500)

```solidity
    function _repayPositionDebt(address position, DebtData[] calldata debtData) internal {
        // sequentially repay position debts
        // assumes the position manager is approved to pull assets from the liquidator
        uint256 debtDataLength = debtData.length;
        for (uint256 i; i < debtDataLength; ++i) {
            uint256 poolId = debtData[i].poolId;
            address poolAsset = pool.getPoolAssetFor(poolId);
            uint256 amt = debtData[i].amt;
            if (amt == type(uint256).max) amt = pool.getBorrowsOf(poolId, position);
            // transfer debt asset from the liquidator to the pool
            IERC20(poolAsset).safeTransferFrom(msg.sender, address(pool), amt);
            // trigger pool repayment which assumes successful transfer of repaid assets
            pool.repay(poolId, position, amt);
            // update position to reflect repayment of debt by liquidator
            Position(payable(position)).repay(poolId, amt);
        }
    }
```



## Impact

The function `liquidateBadDebt` transferring the collaterals to the `owner()` without repaying assets will cause losses to `debtPools`.

## Code Snippet

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L446-L464

## Tool used
Manual Review

## Recommendation

Add the following logic to the function `liquidateBadDebt`:
1.calculate the repaid amounts of assets with the liquidation discount and collaterals.
2.transfer the repaid assets to `debtPools`.
