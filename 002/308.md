Mammoth Rosewood Okapi

High

# When lenders detect bad debt, they can frontrun the liquidateBadDebt function and withdraw their share to avoid losses. Lenders on the SuperPool can perform similar actions to protect themselves from potential losses.


## Summary
When a lender detects bad debt, they can frontrun the liquidateBadDebt function, withdraw their share to avoid losses, and even collect interest on the bad debt. Lenders who don’t take such actions end up bearing a larger share of the losses.
## Vulnerability Detail
```javascript
function liquidateBadDebt(address position) external onlyOwner {
        riskEngine.validateBadDebt(position);

        // transfer any remaining position assets to the PositionManager owner
        address[] memory positionAssets = Position(payable(position)).getPositionAssets();
        uint256 positionAssetsLength = positionAssets.length;
        for (uint256 i; i < positionAssetsLength; ++i) {
            uint256 amt = IERC20(positionAssets[i]).balanceOf(position);
@>>            try Position(payable(position)).transfer(owner(), positionAssets[i], amt) { } catch { }
        }

        // clear all debt associated with the given position
        uint256[] memory debtPools = Position(payable(position)).getDebtPools();
        uint256 debtPoolsLength = debtPools.length;
        for (uint256 i; i < debtPoolsLength; ++i) {
@>>            pool.rebalanceBadDebt(debtPools[i], position);
@>>            Position(payable(position)).repay(debtPools[i], type(uint256).max);
        }
    }
```

```javascript
  function rebalanceBadDebt(uint256 poolId, address position) external {
        PoolData storage pool = poolDataFor[poolId];
        accrue(pool, poolId);

        // revert if the caller is not the position manager
        if (msg.sender != positionManager) revert Pool_OnlyPositionManager(poolId, msg.sender);

        // compute pool and position debt in shares and assets
        uint256 totalBorrowShares = pool.totalBorrowShares;
        uint256 totalBorrowAssets = pool.totalBorrowAssets;
        uint256 borrowShares = borrowSharesOf[poolId][position];
        // [ROUND] round up against lenders
        uint256 borrowAssets = _convertToAssets(borrowShares, totalBorrowAssets, totalBorrowShares, Math.Rounding.Up);

        // rebalance bad debt across lenders
@>>        pool.totalBorrowShares = totalBorrowShares - borrowShares;
        // handle borrowAssets being rounded up to be greater than totalBorrowAssets
@>>        pool.totalBorrowAssets = (totalBorrowAssets > borrowAssets) ? totalBorrowAssets - borrowAssets : 0;
        uint256 totalDepositAssets = pool.totalDepositAssets;
@>>        pool.totalDepositAssets = (totalDepositAssets > borrowAssets) ? totalDepositAssets - borrowAssets : 0;
        borrowSharesOf[poolId][position] = 0;
    }
```
We can see in the liquidateBadDebt function that all of the position’s assets are transferred to the owner, but none of the debt is actually repaid.

In the rebalanceBadDebt function, the debt is only cleared at the accounting level, without transferring the funds needed to repay the debt. By directly reducing borrowAssets from totalDepositAssets, this forces all lenders to bear the loss of the borrowed funds.

```javascript
    function getAssetsOf(uint256 poolId, address guy) public view returns (uint256) {
        PoolData storage pool = poolDataFor[poolId];
        (uint256 accruedInterest, uint256 feeShares) = simulateAccrue(pool);
@>        return _convertToAssets(
            balanceOf[guy][poolId],
@>            pool.totalDepositAssets + accruedInterest,
            pool.totalDepositShares + feeShares,
            Math.Rounding.Down
        );
    }

```
By using the pool.getAssetsOf() function, we can calculate that when the staked position balanceOf[guy][poolId] remains unchanged, the user’s share of assets will decrease due to the reduction in pool.totalDepositAssets. If the user withdraws their share before liquidateBadDebt, they will receive more funds.

#### for SuperPool
```javascript
function totalAssets() public view returns (uint256) {
        uint256 assets = ASSET.balanceOf(address(this));

        uint256 depositQueueLength = depositQueue.length;
        for (uint256 i; i < depositQueueLength; ++i) {
@>>            assets += POOL.getAssetsOf(depositQueue[i], address(this));
        }

        return assets;
    }
```
Since the SuperPool stakes its funds in the Base Pool, when a particular pool incurs bad debt, the asset value of the SuperPool will also decrease (as shown by the totalAssets calculation). Consequently, the value of the SuperPool’s shares will also decrease.


## Impact
When lenders detect bad debt, they can frontrun the liquidateBadDebt function, withdraw their share to avoid losses, and even collect the interest on the bad debt. Lenders who do not take this action end up bearing a larger portion of the losses.

## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L446

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L528

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L218C1-L227C6

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/SuperPool.sol#L180
## Tool used

Manual Review

## Recommendation
Adding a withdrawal waiting period could help prevent users from mass withdrawing their funds when bad debt occurs. This would mitigate the risk of lenders frontrunning the liquidateBadDebt process and unfairly avoiding losses, ensuring that losses are distributed more equitably.