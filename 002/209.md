Keen Jetblack Turtle

High

# New Depositors at Risk of Significant Value Loss in Certain Pools

## Summary
- The Sentiment protocol's `Pool` contract contains a  vulnerability in how it handles bad debt socialization and share-to-asset conversion, particularly after significant bad debt events. This issue can lead to substantial losses for new depositors, undermining the protocol's fairness and reliability.

## Vulnerability Detail

- The problem arises from the interaction between the bad debt socialization mechanism and the share minting process for new deposits. When a pool experiences a large bad debt event, such that the **total assets can be reduced to zero** while leaving a number of shares outstanding. This creates a situation where the share-to-asset exchange rate becomes highly unfavorable for new depositors.

```js
    function rebalanceBadDebt(uint256 poolId, address position) external {
        // some code .. 
  >>    pool.totalDepositAssets = (totalDepositAssets > borrowAssets) ? totalDepositAssets - borrowAssets : 0;
        borrowSharesOf[poolId][position] = 0;
    }
```
- Initial pool state: {`totalAssets: 10000 USD`, `totalBorrows: 6000 USD`, `totalShares: 5000`}

  - User A withdraws **4000 USD**, burning **2000 shares**

- New pool state: {`totalAssets: 6000 USD`, `totalBorrows: 6000 USD`, `totalShares: 3000`}

- The **6000 USD** becomes bad debt and is socialized

- Pool state after socialization: {`totalAssets: 0 USD`, `totalBorrows: 0 USD`, `totalShares: 3000`}

- At this point, if a new user deposits **1000 USD**, they will receive **1000 shares** due to the condition in `_convertToShares` where `totalAssets == 0`. However, these 1000 shares now represent only a fraction of the pool's total shares:

**1000 * 1000 / 4000 = 250 USD**

- This means the new depositor has immediately lost **75%** of their deposit value.

- The cause of this issue is that the bad debt socialization process doesn't handle the total shares outstanding in case asset becomes zero , creating a **mismatch between the pool's assets and shares**. This mismatch is then exploited by the share minting process for new deposits when total assets are zero.

- While the likelihood of this scenario may be low, requiring a significant bad debt event, the *impact on users is high*. 

### PoC : 
- here a coded poc shows how new depositor faced immediate lost of 91% of his funds on deposit : 
```js
 // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.24;

    import { FixedRateModel } from "../../src/irm/FixedRateModel.sol";
    import { LinearRateModel } from "../../src/irm/LinearRateModel.sol";
    import "../BaseTest.t.sol";
    import { MockERC20 } from "../mocks/MockERC20.sol";
    import { OwnableUpgradeable } from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
    import { Action, Operation } from "src/PositionManager.sol";
    import { IOracle } from "src/interfaces/IOracle.sol";
    import { FixedPriceOracle } from "src/oracle/FixedPriceOracle.sol";

  contract testPoc11 is BaseTest {
    // keccak(SENTIMENT_POOL_KEY)
    bytes32 public constant SENTIMENT_POOL_KEY = 0x1a99cbf6006db18a0e08427ff11db78f3ea1054bc5b9d48122aae8d206c09728;
    // keccak(SENTIMENT_RISK_ENGINE_KEY)
    bytes32 public constant SENTIMENT_RISK_ENGINE_KEY = 0x5b6696788621a5d6b5e3b02a69896b9dd824ebf1631584f038a393c29b6d7555;
    // keccak(SENIMENT_POSITION_BEACON_KEY)
    bytes32 public constant SENTIMENT_POSITION_BEACON_KEY = 0x6e7384c78b0e09fb848f35d00a7b14fc1ad10ae9b10117368146c0e09b6f2fa2;

    Pool pool;
    Registry registry;
    address payable position;
    RiskEngine riskEngine;
    PositionManager positionManager;

    FixedPriceOracle asset1Oracle;
    FixedPriceOracle asset2Oracle;
    FixedPriceOracle asset3Oracle;
    address attacker = makeAddr("attacker");

    function setUp() public override {
        super.setUp();

        asset1Oracle = new FixedPriceOracle(7e18);
        asset2Oracle = new FixedPriceOracle(10e18);
        asset3Oracle = new FixedPriceOracle(1e18);

        pool = protocol.pool();
        registry = protocol.registry();
        riskEngine = protocol.riskEngine();
        positionManager = protocol.positionManager();

        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset1), address(asset1Oracle));
        riskEngine.setOracle(address(asset2), address(asset2Oracle));
        riskEngine.setOracle(address(asset3), address(asset3Oracle));
        vm.stopPrank();

        asset1.mint(address(this), 10_000 ether);
        asset1.approve(address(pool), 10_000 ether);

        Action[] memory actions = new Action[](1);
        (position, actions[0]) = newPosition(attacker, bytes32(uint256(3_492_932_942)));

        PositionManager(positionManager).processBatch(position, actions);

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(linearRatePool, address(asset3), 0.75e18);
        riskEngine.acceptLtvUpdate(linearRatePool, address(asset3));
        riskEngine.requestLtvUpdate(linearRatePool, address(asset2), 0.75e18);
        riskEngine.acceptLtvUpdate(linearRatePool, address(asset2));
        vm.stopPrank();
    }

    function deposit_asset2(uint96 amount) public {
        asset2.mint(attacker, amount);
        vm.startPrank(attacker);
        Action[] memory actions = new Action[](1);
        actions[0] = addToken(address(asset2));
        PositionManager(positionManager).processBatch(position, actions);
        actions[0] = deposit(address(asset2), amount);
        asset2.approve(address(positionManager), amount);
        PositionManager(positionManager).processBatch(position, actions);
        vm.stopPrank();
    }

    function _setOracle(uint256 price1, uint256 price2) internal {
        vm.startPrank(protocolOwner);
        FixedPriceOracle testOracle1 = new FixedPriceOracle(price1);
        FixedPriceOracle testOracle2 = new FixedPriceOracle(price2);

        protocol.riskEngine().setOracle(address(asset1), address(testOracle1));
        protocol.riskEngine().setOracle(address(asset2), address(testOracle2));
        vm.stopPrank();
    }

    function test_poc11() public {
        // Initial setup
        uint256 initialDeposit = 10_000 ether;
        asset1.mint(address(this), initialDeposit);
        asset1.approve(address(pool), initialDeposit);
        pool.deposit(linearRatePool, initialDeposit, address(this));

        // Simulate a large borrow
        deposit_asset2(10_000 ether);
        vm.startPrank(attacker);
        bytes memory borrowData = abi.encode(linearRatePool, 10_000 ether);
        Action memory borrowAction = Action({ op: Operation.Borrow, data: borrowData });
        Action[] memory borrowActions = new Action[](1);
        borrowActions[0] = borrowAction;
        PositionManager(positionManager).processBatch(position, borrowActions);
        vm.stopPrank();
        // Simulate price of borrowed asset goes up  and it's a baddebt accrue :
        _setOracle(10e18, 10e18);
        // Simulate bad debt by liquidating the position
        vm.startPrank(address(protocolOwner));
        positionManager.liquidateBadDebt(position);

        // New user deposits
        address newUser = makeAddr("newUser");
        asset1.mint(newUser, 1000 ether);
        vm.startPrank(newUser);
        asset1.approve(address(pool), 1000 ether);
        uint256 newShares = pool.deposit(linearRatePool, 1000 ether, newUser);
        vm.stopPrank();

        // get new user's deposit value
        uint256 newUserAssets = pool.getAssetsOf(linearRatePool, newUser);

        // Output results
        uint256 userDeposit = 1000 ether;
        console2.log("New user deposited      :", userDeposit / 1e18, "USD");
        console2.log("New user received shares:", newShares / 1e18, "Shares");
        console2.log("New user's assets worth :", newUserAssets / 1e18, "USD");
    }
 }
```
- console after running test : 
```sh
 [PASS] test_poc11() (gas: 1072690)
Logs:
  New user deposited      : 1000 USD
  New user received shares: 1000 Shares
  New user's assets worth : 90 USD
```
## Impact
- in such event , new depositors can immediately lose up to 99.99% of their deposit value.
## Code Snippet
- https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L528-L549
## Tool used

Manual Review

## Recommendation
Solving this on the contract level might be complex to do, we would recommend to implement a mechanism to deprecate/block pool deposits if the slashing causes the totalDeposits to become 0.