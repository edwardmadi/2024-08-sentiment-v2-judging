Loud Rosewood Platypus

High

# Frontrunning Vulnerability in `PositionManager::liquidateBadDebt` Allows Liquidity Providers to Evade Losses at Others' Expense

### Summary

The `PositionManager::liquidateBadDebt` function enables the protocol owner to liquidate positions with bad debt that cannot be resolved through normal means. This action can lead to liquidity providers (LPs) losing some of their deposited assets. However, LPs can exploit this by monitoring the mempool for a `liquidateBadDebt` call and withdrawing their funds preemptively, thereby avoiding losses while causing other LPs to bear even greater losses due to the bad debt.


### Root Cause

The vulnerability arises from the current design of the bad debt liquidation system, which allows for frontrunning exploitation.


### Internal pre-conditions

- A position must have accumulated bad debt.
- The protocol owner must initiate the [liquidateBadDebt](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L446) function for the affected position.
- There must be more than one liquidity provider who has deposited into the pool.


### External pre-conditions

- This scenario typically occurs during periods of high market volatility, which makes it difficult for regular liquidators to resolve the position before it accumulates bad debt.

### Attack Path

1. Liquidity Providers monitor the blockchain's mempool to detect if a `liquidateBadDebt` call is imminent.
2. They frontrun the call by withdrawing their assets before the liquidation takes place.


### Impact

As a result of frontrunning, other LPs end up bearing the brunt of the losses associated with the bad debt.


### PoC

To demonstrate this vulnerability, first modify the `FixedPriceOracle.sol` by duplicating it and adding a `setPrice` function to create a new contract named `FixedPriceOracleSettable`. Then, integrate this modified oracle into the setup function and use it for setting oracles. Finally, add a test case to `BigTest.t.sol` and execute it to observe the frontrunning behavior and its impact on LP losses.
```javascript
contract FixedPriceOracleSettable is IOracle {
    using Math for uint256;

    /// @notice Fixed price of the asset in ETH terms scaled to 18 decimals
    uint256 public PRICE;

    /// @param price Fixed price of the asset in ETH terms scaled to 18 decimals
    constructor(uint256 price) {
        PRICE = price;
    }
    function setPrice(uint256 price) external {
        PRICE = price;
    }

    /// @notice Compute the equivalent ETH value for a given amount of a particular asset
    /// @param asset Address of the asset to be priced
    /// @param amt Amount of the given asset to be priced
    /// @return valueInEth Equivalent ETH value for the given asset and amount, scaled by 18 decimals
    function getValueInEth(
        address asset,
        uint256 amt
    ) external view returns (uint256) {
        // [ROUND] price is rounded down. this is used for both debt and asset math, neutral effect
        // value = amt * price % asset.decimals()
        return amt.mulDiv(PRICE, (10 ** IERC20Metadata(asset).decimals()));
    }
}

```
Import it and use it for setting oracles in `setUp` function:
```diff
contract BigTest is BaseTest {
    Pool pool;
    Registry registry;
    RiskEngine riskEngine;
    PortfolioLens portfolioLens;
    PositionManager positionManager;
    SuperPoolFactory superPoolFactory;

    FixedPriceOracle asset1Oracle;
    FixedPriceOracle asset2Oracle;
    FixedPriceOracle asset3Oracle;
+   FixedPriceOracleSettable settableOracle1;
+   FixedPriceOracleSettable settableOracle2;
+   FixedPriceOracleSettable settableOracle3;


function setUp() public override {
        super.setUp();

        pool = protocol.pool();
        registry = protocol.registry();
        riskEngine = protocol.riskEngine();
        portfolioLens = protocol.portfolioLens();
        positionManager = protocol.positionManager();
        superPoolFactory = protocol.superPoolFactory();

        asset1Oracle = new FixedPriceOracle(10e18);
        asset2Oracle = new FixedPriceOracle(10e18);
        asset3Oracle = new FixedPriceOracle(10e18);
+       settableOracle1 = new FixedPriceOracleSettable(10e18);
+       settableOracle2 = new FixedPriceOracleSettable(10e18);
+       settableOracle3 = new FixedPriceOracleSettable(10e18);
        .
        .
        .
```
Then add the following test case to `BigTest.t.sol` and run it:
```javascript
 function testBadDebtFrontrunned() public {
        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset1), address(settableOracle1));
        riskEngine.setOracle(address(asset2), address(settableOracle2));
        riskEngine.setOracle(address(asset3), address(settableOracle3));
        vm.stopPrank();
        //Lp provides
        vm.startPrank(lender);
        asset1.mint(lender, 1000 ether);
        asset1.approve(address(pool), 1000 ether);
        pool.deposit(fixedRatePool, 1000 ether, lender);
        assertEq(pool.getAssetsOf(fixedRatePool, lender), 1000 ether);
        vm.stopPrank();
        vm.startPrank(user);
        asset1.mint(user, 1000 ether);
        asset1.approve(address(pool), 1000 ether);
        pool.deposit(fixedRatePool, 1000 ether, user);
        assertEq(pool.getAssetsOf(fixedRatePool, user), 1000 ether);
        vm.stopPrank();
        //Borrower borrows at price: 10 (initialPrice), and makes a swap for asset3
        vm.startPrank(user2);
        asset2.mint(user2, 300 ether);
        asset2.approve(address(positionManager), 300 ether);

        // Make a new position
        (address position, Action memory _newPosition) = newPosition(
            user2,
            "test"
        );
        positionManager.process(position, _newPosition);

        Action memory addNewCollateral = addToken(address(asset2));
        Action memory depositCollateral = deposit(address(asset2), 5 ether);
        Action memory borrowAct = borrow(fixedRatePool, 15 ether);
        Action memory approveAct = approve(
            address(mockswap),
            address(asset1),
            15 ether
        );
        bytes memory data = abi.encodeWithSelector(
            SWAP_FUNC_SELECTOR,
            address(asset1),
            address(asset3),
            15 ether
        );
        Action memory execAct = exec(address(mockswap), 0, data);
        Action memory addAsset3 = addToken(address(asset3));

        Action[] memory actions = new Action[](6);
        actions[0] = addNewCollateral;
        actions[1] = depositCollateral;
        actions[2] = borrowAct;
        actions[3] = approveAct;
        actions[4] = execAct;
        actions[5] = addAsset3;

        positionManager.processBatch(position, actions);
        vm.stopPrank();
        assertEq(pool.getBorrowsOf(fixedRatePool, position), 15 ether);
        assertEq(asset3.balanceOf(position), 15 ether);

        //Time passes,asset3 Price reaches: 0.1
        vm.warp(block.timestamp + 365 days);
        vm.roll(block.number + (365 days / 12));
        settableOracle3.setPrice(1e17);
        assertEq(riskEngine.isPositionHealthy(position), false);
        // vm.expectRevert();
        riskEngine.validateBadDebt(position);
        vm.startPrank(user);
        uint256 withdrawAmount = pool.getAssetsOf(fixedRatePool, user);
        console2.log(withdrawAmount);
        pool.withdraw(fixedRatePool, withdrawAmount, user, user);
        //owner removes Bad debt
        vm.startPrank(protocolOwner);
        positionManager.liquidateBadDebt(position);
        assertEq(
            pool.getAssetsOf(fixedRatePool, lender),
            977505133470225872688
        ); // the other lp has lost money (22.5 ethers)
        assert(asset1.balanceOf(user) > 1000 ether); // its greater than 1000 because of acured intrest over one year time span
        assertEq(asset3.balanceOf(position), 0 ether);
        assertEq(asset3.balanceOf(address(protocolOwner)), 15 ether);
    }
```

### Mitigation

A potential mitigation strategy involves introducing a delay or implementing a two-step transaction scheme with a deadline for withdrawals. This approach would prevent frontrunning of the liquidation call, ensuring a fairer distribution of losses among liquidity providers.
