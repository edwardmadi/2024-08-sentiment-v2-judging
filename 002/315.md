Bitter Sandstone Worm

High

# SuperPoolFactory - Factory can be easily grieffed by sending asset tokens directly to the precomuted address

### Summary

When a user wants to deploy a `SuperPool` he calls `SuperPoolFactory::deploySuperPool()`

```solidity
 function deploySuperPool(
        address owner,
        address asset,
        address feeRecipient,
        uint256 fee,
        uint256 superPoolCap,
        uint256 initialDepositAmt,
        string calldata name,
        string calldata symbol
    ) external returns (address) {
        if (fee != 0 && feeRecipient == address(0)) revert SuperPoolFactory_ZeroFeeRecipient();
        SuperPool superPool = new SuperPool(POOL, asset, feeRecipient, fee, superPoolCap, name, symbol);
        superPool.transferOwnership(owner);
        isDeployerFor[address(superPool)] = true; 

        IERC20(asset).safeTransferFrom(msg.sender, address(this), initialDepositAmt); // assume approval 
        IERC20(asset).approve(address(superPool), initialDepositAmt);
        uint256 shares = superPool.deposit(initialDepositAmt, address(this)); 
        if (shares < MIN_BURNED_SHARES) revert SuperPoolFactory_TooFewInitialShares(shares); 
        IERC20(superPool).transfer(DEAD_ADDRESS, shares); 

        emit SuperPoolDeployed(owner, address(superPool), asset, name, symbol);
        return address(superPool); 
    }
```

You'll notice that `SuperPool` is deployed using [`CREATE`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/SuperPoolFactory.sol#L67), not `CREATE2`. Address computation of `CREATE` is as follows:
1. Originating address (address deploying the new contract)
2. Nonce, the number of contract creations (for contracts).
3. RLP encode the originating address and the nonce.
4. Compute the Keccak-256 hash of the RLP-encoded data.
5. The contract address is the last 20 bytes of the hash.

Knowing this, anyone can precompute and determine the next `SuperPool` address that the factory will deploy. The key thing about `CREATE` is that the only determining factors about the address (contract) being deployed are the originating address and the nonce of the contract, meaning that it's impossible to "skip" a nonce sort to speak.

Knowing all this, an attacker can actually brick the `SuperPool` factory by sending asset tokens directly to the precomputed address, which will affect the share computation of `deposit`.

### Root Cause

Using `CREATE` instead of `CREATE2` alongside using `balanceOf` directly to retrieve how many tokens the contract has.

### Internal pre-conditions

Attacker directly transfer asset tokens to the precomputed Super Pool address.

### External pre-conditions

None

### Attack Path


Let's examine how this happens:
- The asset of `POOL` is USDC.
- Note the attacker doesn't need to front-run the deployer.

1. Attacker precomputes the next `SuperPool` address and sends 1e6 tokens to the contract. 
2. Caller calls `deploySuperPool` and will deposit 1e6 tokens as well.
3. The `SuperPool` is deployed and we call `superPool.deposit` as we have to mint the `MIN_BURNED_SHARES` in order for the tx to finish.
4. Inside `deposit` we first hit `accrue`, which calls `simulateAccrue`.
5. `simulateAccrue` calls `totalAssets`, which will retrieve the `ASSET.balanceOf(address(this))` directly, which is 1e6.
```solidity
function totalAssets() public view returns (uint256) {
        uint256 assets = ASSET.balanceOf(address(this));

        uint256 depositQueueLength = depositQueue.length; 
        for (uint256 i; i < depositQueueLength; ++i) {
            assets += POOL.getAssetsOf(depositQueue[i], address(this)); 
        }

        return assets; 
    }
```
6. `totalAssets` will return 1e6 as `newTotalAssets`, which will then be set as `lastTotalAssets`.
```solidity
 function accrue() public {
        (uint256 feeShares, uint256 newTotalAssets) = simulateAccrue(); /
        if (feeShares != 0) ERC20._mint(feeRecipient, feeShares); 
        lastTotalAssets = newTotalAssets;
    }
```
7. We go back to `deposit` and we call, `_convertToShares(assets, lastTotalAssets, totalSupply(), Math.Rounding.Up)`.
8. This is where the attack takes form, we have the following calculation:
```solidity
 shares = _assets.mulDiv(_totalShares + 1, _totalAssets + 1, _rounding);
 shares = 1e6 * 1 / (1e6 + 1) = 0.99 = 1 (as we round up)
```
9. 1 share gets minted to the user, but after `deposit` finishes, we continue in `deployInSuperPool`
10. The tx will revert on the next line:
```solidity
                1      <    1000
        if (shares < MIN_BURNED_SHARES) revert SuperPoolFactory_TooFewInitialShares(shares);
```




### Impact

Bricking ANY `SuperPoolFactory`.
If any third party is using `isDeployerFor` from the `SuperPoolFactory` and the factory gets redeployed it might start breaking.

### PoC

_No response_

### Mitigation

In our opinion, replacing `CREATE` with `CREATE2` is the best fix. While keeping `balanceOf` in `totalAssets` still allows the attacker to pull of a similar attack, contracts deployed with `CREATE2` can easily be changed by changing the salt, which means changing the salt slightly will yield a new address. 

This makes the attack much less feasible, as now if an attacker wants to DoS someone, he can only DoS that user specifically, without affecting anyone else and also the deployer can easily change his salt around a bit to get a new address. This also limits the attack to a front-run attack, which is impossible on some chains and can be "bypassed" by using something like Flashbots.