Loud Rosewood Platypus

Medium

# `ProtocolOwner` Receives Collateral in Bad Debt Liquidation, Causing Unintended Losses for Liquidity Providers

### Summary

In the event of bad debt liquidation, the collateral intended to serve as assurance for liquidity providers (LPs) is instead transferred to the protocol owner's account rather than being returned to the LPs. This misdirection of assets leads to greater financial losses for LPs beyond what was initially anticipated.


### Root Cause

The `PositionManager::liquidateBadDebt` function is designed in such a way that it transfers all remaining collateral and other assets from liquidated positions directly to the protocol owner, bypassing the intended recipients, the LPs.
[Line to Code](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L454)

### Internal pre-conditions

- A position must have accumulated bad debt.
- The protocol owner initiates the `liquidateBadDebt` function for the affected position.


### External pre-conditions

- This situation arises during periods of high market volatility, complicating regular liquidation processes and leading to bad debt accumulation.


### Attack Path

_No response_

### Impact

Directly transferring collateral and other assets from liquidated positions to the protocol owner, instead of returning them to liquidity providers (LPs), has several negative consequences:

- **Unfair Loss Distribution:** LPs face greater losses than anticipated, discouraging participation and reducing pool liquidity.
  
- **Trust Erosion:** The platform's integrity and fairness are compromised, potentially leading to a loss of trust among users.

### PoC

To illustrate this vulnerability, modifications were made to the `FixedPriceOracle.sol` contract by adding a `setPrice` function, creating a new contract named `FixedPriceOracleSettable`. This modified oracle was integrated into the setup function for testing purposes. A test case was added to `BigTest.t.sol` to demonstrate the impact of the current `liquidateBadDebt` mechanism on LP losses.

```javascript
contract FixedPriceOracleSettable is IOracle {
    using Math for uint256;

    /// @notice Fixed price of the asset in ETH terms scaled to 18 decimals
    uint256 public PRICE;

    /// @param price Fixed price of the asset in ETH terms scaled to 18 decimals
    constructor(uint256 price) {
        PRICE = price;
    }
    function setPrice(uint256 price) external {
        PRICE = price;
    }

    /// @notice Compute the equivalent ETH value for a given amount of a particular asset
    /// @param asset Address of the asset to be priced
    /// @param amt Amount of the given asset to be priced
    /// @return valueInEth Equivalent ETH value for the given asset and amount, scaled by 18 decimals
    function getValueInEth(
        address asset,
        uint256 amt
    ) external view returns (uint256) {
        // [ROUND] price is rounded down. this is used for both debt and asset math, neutral effect
        // value = amt * price % asset.decimals()
        return amt.mulDiv(PRICE, (10 ** IERC20Metadata(asset).decimals()));
    }
}

```
Import it and use it for setting oracles in `setUp` function:
```diff
contract BigTest is BaseTest {
    Pool pool;
    Registry registry;
    RiskEngine riskEngine;
    PortfolioLens portfolioLens;
    PositionManager positionManager;
    SuperPoolFactory superPoolFactory;

    FixedPriceOracle asset1Oracle;
    FixedPriceOracle asset2Oracle;
    FixedPriceOracle asset3Oracle;
+   FixedPriceOracleSettable settableOracle1;
+   FixedPriceOracleSettable settableOracle2;
+   FixedPriceOracleSettable settableOracle3;


function setUp() public override {
        super.setUp();

        pool = protocol.pool();
        registry = protocol.registry();
        riskEngine = protocol.riskEngine();
        portfolioLens = protocol.portfolioLens();
        positionManager = protocol.positionManager();
        superPoolFactory = protocol.superPoolFactory();

        asset1Oracle = new FixedPriceOracle(10e18);
        asset2Oracle = new FixedPriceOracle(10e18);
        asset3Oracle = new FixedPriceOracle(10e18);
+       settableOracle1 = new FixedPriceOracleSettable(10e18);
+       settableOracle2 = new FixedPriceOracleSettable(10e18);
+       settableOracle3 = new FixedPriceOracleSettable(10e18);
        .
        .
        .
```
Then add the following test case to `BigTest.t.sol` and run it:
```javascript
    function testOwnerGetsTheCollateralInCaseOfBadDebt() public {
        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset3), address(settableOracle3));
        vm.stopPrank();
        //Lp provides
        vm.startPrank(lender);
        asset1.mint(lender, 1000 ether);
        asset1.approve(address(pool), 1000 ether);
        pool.deposit(fixedRatePool, 1000 ether, lender);
        assertEq(pool.getAssetsOf(fixedRatePool, lender), 1000 ether);
        vm.stopPrank();

        //Borrower puts 5 ether of asset 2 as collateral, borrows at price: 10 (initialPrice), and makes a swap for asset3
        vm.startPrank(user2);
        asset2.mint(user2, 300 ether);
        asset2.approve(address(positionManager), 300 ether);

        // Make a new position
        (address position, Action memory _newPosition) = newPosition(
            user2,
            "test"
        );
        positionManager.process(position, _newPosition);

        Action memory addNewCollateral = addToken(address(asset2));
        Action memory depositCollateral = deposit(address(asset2), 5 ether);
        Action memory borrowAct = borrow(fixedRatePool, 15 ether);
        Action memory approveAct = approve(
            address(mockswap),
            address(asset1),
            15 ether
        );
        bytes memory data = abi.encodeWithSelector(
            SWAP_FUNC_SELECTOR,
            address(asset1),
            address(asset3),
            15 ether
        );
        Action memory execAct = exec(address(mockswap), 0, data);
        Action memory addAsset3 = addToken(address(asset3));

        Action[] memory actions = new Action[](6);
        actions[0] = addNewCollateral;
        actions[1] = depositCollateral;
        actions[2] = borrowAct;
        actions[3] = approveAct;
        actions[4] = execAct;
        actions[5] = addAsset3;

        positionManager.processBatch(position, actions);
        vm.stopPrank();
        assertEq(pool.getBorrowsOf(fixedRatePool, position), 15 ether);
        assertEq(asset3.balanceOf(position), 15 ether);

        //Time passes,asset3 Price reaches: 0.1
        vm.warp(block.timestamp + 365 days);
        vm.roll(block.number + (365 days / 12));
        settableOracle3.setPrice(1e17);
        assertEq(riskEngine.isPositionHealthy(position), false);
        riskEngine.validateBadDebt(position);

        //owner removes Bad debt
        vm.startPrank(protocolOwner);
        positionManager.liquidateBadDebt(position);
        assertEq(pool.getAssetsOf(fixedRatePool, lender), 985 ether);

        // protocolOwner gets all collateral and other tokens!
        assertEq(asset3.balanceOf(address(protocolOwner)), 15 ether);
        assertEq(asset2.balanceOf(address(protocolOwner)), 5 ether);
    }
```


### Mitigation

To fix this issue, one solution would be converting the collateral and other tokens to pools main asset transfering it to pool and adding it to  Pool.totalDepositAssets and corresponding share amount Pool.totalDepositShares. but since this method is gas costly and requires working with external protcols. one method is having protocolOwner account loaded offchain with  pools main Assets and after moving other tokens to protcolOwner transfer same value of these tokens back to pool. this method would prove to be esiear.