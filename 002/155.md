Abundant Hazel Newt

High

# Pool depositors are not compensated by position collaterals when a bad debt liquidated

## Summary
Position collaterals should be used as compensation for the loss of pool depositors if there is bad debt, however, the collaterals are actually sent to protocol owner and the depositors are not compensated.

## Vulnerability Detail
Pool depositors deposit assets into a pool, borrowers who want to borrow must deposit enough collaterals into a position. When a borrower's position is unhealthy, it can be liquidated, the liquidator seized the collaterals and the borrowed assets are repaid to pool.

[PositionManager.sol#L437-L439](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L437-L439):
```solidity
        // liquidate
        _transferAssetsToLiquidator(position, assetData);
        _repayPositionDebt(position, debtData);
```
When liquidation is not profitable to the liquidators, protocol owner is supposed to handle the bad debt by calling `liquidateBadDebt()` in PositionManager contract. As a result, the collaterals in the position are sent to protocol owner and the debt associated with the given position is cleared.

[PositionManager.sol#L449-L463](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L449-L463):
```solidity
        // transfer any remaining position assets to the PositionManager owner
        address[] memory positionAssets = Position(payable(position)).getPositionAssets();
        uint256 positionAssetsLength = positionAssets.length;
        for (uint256 i; i < positionAssetsLength; ++i) {
            uint256 amt = IERC20(positionAssets[i]).balanceOf(position);
            try Position(payable(position)).transfer(owner(), positionAssets[i], amt) { } catch { }
        }

        // clear all debt associated with the given position
        uint256[] memory debtPools = Position(payable(position)).getDebtPools();
        uint256 debtPoolsLength = debtPools.length;
        for (uint256 i; i < debtPoolsLength; ++i) {
            pool.rebalanceBadDebt(debtPools[i], position);
            Position(payable(position)).repay(debtPools[i], type(uint256).max);
        }
```
The pool is not repaid, even if owner later sends assets back to the pool, the depositors will not be compensated because the pool has already been rebalanced.

[Pool.sol#L542-L547](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/Pool.sol#L542-L547):
```solidity
        // rebalance bad debt across lenders
        pool.totalBorrowShares = totalBorrowShares - borrowShares;
        // handle borrowAssets being rounded up to be greater than totalBorrowAssets
        pool.totalBorrowAssets = (totalBorrowAssets > borrowAssets) ? totalBorrowAssets - borrowAssets : 0;
        uint256 totalDepositAssets = pool.totalDepositAssets;
        pool.totalDepositAssets = (totalDepositAssets > borrowAssets) ? totalDepositAssets - borrowAssets : 0;
```

Please run the PoC in **BigTest.t.sol**:
```solidity
    function testAudit_BadDebt() public {
        address alice = makeAddr("Alice");
        address bob = makeAddr("Bob");

        asset1.mint(alice, 1000e18);
        asset2.mint(bob, 1250e18);

        // Alice creates pool and deposit 1000e18 asset1
        vm.startPrank(alice);

        bytes32 FIXED_RATE_MODEL_KEY = 0xeba2c14de8b8ca05a15d7673453a0a3b315f122f56770b8bb643dc4bfbcf326b;
        uint256 poolId = pool.initializePool(alice, address(asset1), type(uint128).max, FIXED_RATE_MODEL_KEY);

        riskEngine.requestLtvUpdate(poolId, address(asset2), 0.8e18);
        riskEngine.acceptLtvUpdate(poolId, address(asset2));

        asset1.approve(address(pool), type(uint256).max);
        pool.deposit(poolId, 1000e18, alice);

        // Alice receives 1000e18 shares
        assertEq(pool.balanceOf(alice, poolId), 1000e18);

        vm.stopPrank();

        // Bob creates position
        (address payable positionAddress, Action memory newPos) = newPosition(bob, "bob");
        positionManager.process(positionAddress, newPos);

        // Bob deposits and borrows
        Action memory addNewCollateral = addToken(address(asset2));
        Action memory depositCollateral = deposit(address(asset2), 1250e18);
        Action memory borrowAct = borrow(poolId, 1000e18);

        Action[] memory actions = new Action[](3);
        actions[0] = addNewCollateral;
        actions[1] = depositCollateral;
        actions[2] = borrowAct;
    
        vm.startPrank(bob);
        asset2.approve(address(positionManager), type(uint256).max);
        positionManager.processBatch(positionAddress, actions);
        vm.stopPrank();

        vm.warp(block.timestamp + 7889400);

        // Bad debt
        riskEngine.validateBadDebt(positionAddress);

        // Collaterals value in the position is 12500e18 (collateral price is 10e18)
        uint256 assetValue = riskEngine.getTotalAssetValue(positionAddress);
        assertEq(assetValue, 12500e18);

        // Owner liuqidates bad debt
        vm.prank(protocolOwner);
        positionManager.liquidateBadDebt(positionAddress);

        // Owner sells the collaterals and sends assets back to pool
        asset1.mint(protocolOwner, 1250e18);
        vm.prank(protocolOwner);
        asset1.transfer(address(pool), 1250e18);

        // Alice tries to withdraw but there is no liuqidity held in the pool 
        vm.prank(alice);
        vm.expectRevert(abi.encodeWithSelector(Pool.Pool_InsufficientWithdrawLiquidity.selector, poolId, 0, 1000e18));
        pool.withdraw(poolId, 1000e18, alice, alice);
    }
```

## Impact
Pool depositors will not be compensated if bad debt occurs.

## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol#L446-L464

## Tool used
Manual Review

## Recommendation
When owner liquidate a bad debt, the seized collaterals should be used for repaying the debt first, this can be achieved through a callback.
```diff
-   function liquidateBadDebt(address position) external onlyOwner {
+   function liquidateBadDebt(address position, address target, bytes calldata data) external onlyOwner {
        riskEngine.validateBadDebt(position);

        // transfer any remaining position assets to the PositionManager owner
        address[] memory positionAssets = Position(payable(position)).getPositionAssets();
        uint256 positionAssetsLength = positionAssets.length;
        for (uint256 i; i < positionAssetsLength; ++i) {
            uint256 amt = IERC20(positionAssets[i]).balanceOf(position);
-           try Position(payable(position)).transfer(owner(), positionAssets[i], amt) { } catch { }
+           try Position(payable(position)).transfer(target, positionAssets[i], amt) { 
+               // swap collaterals to pool assets
+               (bool success, bytes memory data) = target.call(data);
+               if (success) {
+                   (uint256 poolId, uint256 assetAmount) = abi.decode(data, (uint256, uint256));
+                   pool.repay(poolId, position, assetAmount);
+               }
+           } catch { }
        }

        // clear all debt associated with the given position
        uint256[] memory debtPools = Position(payable(position)).getDebtPools();
        uint256 debtPoolsLength = debtPools.length;
        for (uint256 i; i < debtPoolsLength; ++i) {
            pool.rebalanceBadDebt(debtPools[i], position);
            Position(payable(position)).repay(debtPools[i], type(uint256).max);
        }
    }
```