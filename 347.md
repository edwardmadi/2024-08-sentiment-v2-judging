Flat Navy Bee

High

# `_withdrawFromPools` does not prioritize transfer out existing asset tokens in Superpool contract

## Summary

`_withdrawFromPools` does not prioritize transfer out existing asset tokens in Superpool contract

## Vulnerability Detail

When users try to Redeem/Withdraw from a Superpool, the contract will always withdraw equal assets from basepools:

```solidity
    function withdraw(uint256 assets, address receiver, address owner) public nonReentrant returns (uint256 shares) {
        accrue();
        shares = _convertToShares(assets, lastTotalAssets, totalSupply(), Math.Rounding.Up);
        if (shares == 0) revert SuperPool_ZeroShareWithdraw(address(this), assets);
        _withdraw(receiver, owner, assets, shares);
    }
```
```solidity
    function _withdraw(address receiver, address owner, uint256 assets, uint256 shares) internal {
        _withdrawFromPools(assets);
        if (msg.sender != owner) ERC20._spendAllowance(owner, msg.sender, shares);
        ERC20._burn(owner, shares);
        lastTotalAssets -= assets;
        ASSET.safeTransfer(receiver, assets);
        emit Withdraw(msg.sender, receiver, owner, assets, shares);
    }
```
```solidity
    function _withdrawFromPools(uint256 assets) internal {
        uint256 assetsInSuperpool = ASSET.balanceOf(address(this));

        if (assetsInSuperpool >= assets) return;
        else assets -= assetsInSuperpool;

        uint256 withdrawQueueLength = withdrawQueue.length;
        for (uint256 i; i < withdrawQueueLength; ++i) {
            uint256 poolId = withdrawQueue[i];
            // withdrawAmt -> max assets that can be withdrawn from the underlying pool
            // optimistically try to withdraw all assets from this pool
            uint256 withdrawAmt = assets;

            // withdrawAmt cannot be greater than the assets deposited by the pool in the underlying pool
            uint256 assetsInPool = POOL.getAssetsOf(poolId, address(this));
            if (assetsInPool < withdrawAmt) withdrawAmt = assetsInPool;

            // withdrawAmt cannot be greater than the underlying pool liquidity
            uint256 poolLiquidity = POOL.getLiquidityOf(poolId);
            if (poolLiquidity < withdrawAmt) withdrawAmt = poolLiquidity;

            if (withdrawAmt > 0) {
                try POOL.withdraw(poolId, withdrawAmt, address(this), address(this)) {
                    assets -= withdrawAmt;
                } catch { }
            }

            if (assets == 0) return;
        }

        // We explicitly check assets == 0, and if so return, otherwise we revert directly here
        revert SuperPool_NotEnoughLiquidity(address(this));
    }
```
Even if there is a sufficient asset balance in the superpool, withdrawals from base pool will still take place - this can lead to the following issues:

1. When a pool is removed, all assets will be withdrawn and store in Superpool:
```solidity
    function removePool(uint256 poolId, bool forceRemove) external onlyOwner {
        if (poolCapFor[poolId] == 0) return; // no op if pool is not in queue
        uint256 assetsInPool = POOL.getAssetsOf(poolId, address(this));
        if (forceRemove && assetsInPool > 0) POOL.withdraw(poolId, assetsInPool, address(this), address(this));
        _removePool(poolId);
        poolCapFor[poolId] = 0;
        emit PoolCapSet(poolId, 0);
    }
```
This leads to two effects -

- When users withdraw, the portion of the base pool (that is still generating yields) is taken in preference to the portion of the Superpool that is not generating yields. Users across the protocol receive less yield as a result.
- Whales may suffer a liquidity crisis, even if there is enough liquidity in the superpool. At the end of `_withdrawFromPools`, if the mobility in all base pools cannot be satisfied, the withdraw call will revert.

2. Asset tokens accumulated in a superpool can never be withdrawn or reallocated, and these tokens participate normally in the distribution of yields.

```solidity
    function reallocate(ReallocateParams[] calldata withdraws, ReallocateParams[] calldata deposits) external {
        if (!isAllocator[msg.sender] && msg.sender != Ownable.owner()) {
            revert SuperPool_OnlyAllocatorOrOwner(address(this), msg.sender);
        }

        uint256 withdrawsLength = withdraws.length;
        for (uint256 i; i < withdrawsLength; ++i) {
            if (poolCapFor[withdraws[i].poolId] == 0) revert SuperPool_PoolNotInQueue(withdraws[i].poolId);
            POOL.withdraw(withdraws[i].poolId, withdraws[i].assets, address(this), address(this));
        }

        uint256 depositsLength = deposits.length;
        for (uint256 i; i < depositsLength; ++i) {
            uint256 poolCap = poolCapFor[deposits[i].poolId];
            // disallow deposits to pool not associated with this SuperPool
            if (poolCap == 0) revert SuperPool_PoolNotInQueue(deposits[i].poolId);
            // respect pool cap
            uint256 assetsInPool = POOL.getAssetsOf(deposits[i].poolId, address(this));
            if (assetsInPool + deposits[i].assets < poolCap) {
                ASSET.approve(address(POOL), deposits[i].assets);
                POOL.deposit(deposits[i].poolId, deposits[i].assets, address(this));
            }
        }
    }
```

## Impact

1. Users across the protocol receive less yields as a result.
2. Whales may suffer a liquidity crisis, even if there is enough liquidity in the superpool.
3. Asset tokens accumulated in a superpool can never be withdrawn or reallocated, and these tokens participate normally in the distribution of yields.

## Code Snippet

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L548

## Tool used

Manual Review

## Recommendation

1. After traversing all the pools at the time of withdrawal, `_withdrawFromPools` should not REVERT, because this does not indicate a lack of liquidity in the contract.
2. Existing assets in superpool should be transferred out first.