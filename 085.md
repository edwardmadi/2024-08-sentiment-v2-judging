Scrawny Blonde Guppy

Medium

# `SuperPoolFactory` Burns The Entirety Of The Initial Deposit

## Summary

The permissionless [`SuperPoolFactory`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol) implements defensive mechanisms to protect against vault inflation attacks by burning the initial depositor's shares to the dead address. This combats the instantiation of pools with illiquid share supplies.

However, it burns the **entirety** of their initial deposit instead of just the burn amount, resulting in potentially significant loss for the deployer.

## Vulnerability Detail

When creating a new [`SuperPool`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol), the [`SuperPoolFactory`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol) ensures that the deployer burns a minimum of [`MIN_BURNED_SHARES`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/SuperPoolFactory.sol#L20C29-L20C46) to ensure a sufficiently liquid share supply upon construction:

```solidity
function deploySuperPool(
    address owner,
    address asset,
    address feeRecipient,
    uint256 fee,
    uint256 superPoolCap,
    uint256 initialDepositAmt,
    string calldata name,
    string calldata symbol
) external returns (address) {
    if (fee != 0 && feeRecipient == address(0)) revert SuperPoolFactory_ZeroFeeRecipient();
    SuperPool superPool = new SuperPool(POOL, asset, feeRecipient, fee, superPoolCap, name, symbol);
    superPool.transferOwnership(owner);
    isDeployerFor[address(superPool)] = true;

    // burn initial deposit
    IERC20(asset).safeTransferFrom(msg.sender, address(this), initialDepositAmt); // assume approval
    IERC20(asset).approve(address(superPool), initialDepositAmt);
    uint256 shares = superPool.deposit(initialDepositAmt, address(this));

@>  if (shares < MIN_BURNED_SHARES) revert SuperPoolFactory_TooFewInitialShares(shares); 
@>  IERC20(superPool).transfer(DEAD_ADDRESS, shares);

    emit SuperPoolDeployed(owner, address(superPool), asset, name, symbol);
    return address(superPool);
}
```

This is a [common strategy](https://github.com/Uniswap/v2-core/blob/ee547b17853e71ed4e0101ccfd52e70d5acded58/contracts/UniswapV2Pair.sol#L120C13-L121C104) to protect against inflation attacks. However, Sentiment's approach is implemented incorrectly:

```solidity
uint256 shares = superPool.deposit(initialDepositAmt, address(this));

if (shares < MIN_BURNED_SHARES) revert SuperPoolFactory_TooFewInitialShares(shares); 
IERC20(superPool).transfer(DEAD_ADDRESS, shares); /// @audit Burns *all* minted shares to the `DEAD_ADDRESS`.
```

Notice that when the sender makes a sufficiently liquid deposit resulting in the minting of a number of shares greater than or equal to [`MIN_BURNED_SHARES`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/SuperPoolFactory.sol#L20C29-L20C46), **all of the shares will be burned to the `DEAD_ADDRESS`**.

This results in an entire loss for the initial depositor's deposit, since instead of sending only the necessary `MIN_BURNED_SHARES` to the `DEAD_ADDRESS`, all minted shares for the initial deposit are burned instead.

## Impact

The first depositor loses their entire deposit to the `DEAD_ADDRESS`.

## Code Snippet

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/25a0c8aeaddec273c5318540059165696591ecfb/protocol-v2/src/SuperPoolFactory.sol#L47C5-L80C6

## Tool used

Manual Review

## Recommendation

Only transfer the `MIN_BURNED_SHARES` to the burn address, and return the remaining shares to the initial depositor. To ensure sufficient deployment incentive, ensure that the initial depositor may receive non-zero shares:

```diff
-  if (shares < MIN_BURNED_SHARES) revert SuperPoolFactory_TooFewInitialShares(shares);
+  if (shares <= MIN_BURNED_SHARES) revert SuperPoolFactory_TooFewInitialShares(shares);
-  IERC20(superPool).transfer(DEAD_ADDRESS, shares);
+  IERC20(superPool).transfer(DEAD_ADDRESS, MIN_BURNED_SHARES);
+  IERC20(superPool).transfer(receiver, shares - MIN_BURNED_SHARES);
```
