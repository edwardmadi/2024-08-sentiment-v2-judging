Bald Tiger Troll

High

# Malicious actor will prevent or increase price of super pool creation.

### Summary

SuperPool's `totalAssets()` is calculated using `ASSET.balanceOf(address(this)`. This will make deployments of super pools very expensive if the pool was donated funds before its deployed - which will prevent deployment and usage of the super pools as a malicious hacker can donate `X` to the pool to make the initial deposit `1000*X`

The deployment of super pools uses the `CREATE` opcode and validates that `1000` shares are minted from the initial deposit. A malicious actor that will be able to predict because of the `CREATE` opcode the next addresses of all future super pools deployed by the `SuperPoolFactory` and donate `X` funds to increase deployment price by to `1000 * X`.

A high deployment price is not only a loss of funds for the deployer who intends to use a small amount of funds for the initial deposit but also will effect the deployed superpool because of a large amount of "dead shares" that will result in diluting shares.

A very high deployment price would render the factory useless.

Notice that initial deposit is implemented to prevent share inflation in erc4626 vaults

### Root Cause

- In [`SuperPool.sol#L181`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L181) `totalAssets()` uses `ASSET.balanceOf` instead of internal accounting.
- In [`SuperPoolFactory.sol#L67`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol#L67) the `CREATE` opcode is used to create the super pools. All super pool addresses can be predicted.
- In [`SuperPoolFactory.sol#L74-75`](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPoolFactory.sol#L74-L75) `MIN_BURNED_SHARES` (1000) shares must be minted by the initial deposit.

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

Lets consider a super pool factory for `POOL` that uses `USDC`. Here is an example of loss of funds for deployer. 
1. Hacker calculates off-chain the address of the next super pool.
2. Hacker donates 1$ USDC to the address of the next super pool. 
3. Super pool owner that wishes to deploy pool will need to pay 100$ to deploy (instead of minimal 1e5).
4. Also initial "dead shares" have a higher price therefore first depositors would gain less revenue for their deposits.

Lets consider a super pool factory for `POOL` that uses `USDC`.  Here is an example of rendering the factory useless.  
1. Hacker calculates off-chain the address of the next super pool.
2. Hacker donates 10$ USDC to the address of the next super pool. 
3. Factory is rendered useless. In order to deploy a super pool the owner needs to pay 10,000$ initial deposit of dead shares (loss of funds) and the super pool would be unusable as shares would be diluted

### Impact

- For a small price, SuperPoolFactory would be rendered useless, essentially preventing and disincentivizing any users from deploying super pools.
- For an even smaller price, owners would need to pay more then intended to deploy super pools and depositors would gain less from initial deposits as price per share is inflated. 

### PoC

Add the following to `SuperPoolFactory.t.sol`

```solidity
    function computeAddress(address deployer, uint256 _nonce) internal pure returns (address) {
        bytes memory data;
        if (_nonce == 0x00)          data = abi.encodePacked(bytes1(0xd6), bytes1(0x94), deployer, bytes1(0x80));
        else if (_nonce <= 0x7f)     data = abi.encodePacked(bytes1(0xd6), bytes1(0x94), deployer, uint8(_nonce));
        else if (_nonce <= 0xff)     data = abi.encodePacked(bytes1(0xd7), bytes1(0x94), deployer, bytes1(0x81), uint8(_nonce));
        else if (_nonce <= 0xffff)   data = abi.encodePacked(bytes1(0xd8), bytes1(0x94), deployer, bytes1(0x82), uint16(_nonce));
        else if (_nonce <= 0xffffff) data = abi.encodePacked(bytes1(0xd9), bytes1(0x94), deployer, bytes1(0x83), uint24(_nonce));
        else                         data = abi.encodePacked(bytes1(0xda), bytes1(0x94), deployer, bytes1(0x84), uint32(_nonce));
        return address(uint160(uint256(keccak256(data))));
    }    
    
    function testBrickSuperPool() public {
        address feeRecipient = makeAddr("FeeRecipient");

        // get some funds
        vm.prank(protocolOwner);
        asset1.mint(address(this), 100000e18);
        asset1.approve(address(superPoolFactory), 100000e18);

        // predict next nonce address
        address predicted = computeAddress(address(superPoolFactory), 2);
        
        // 10$ donation forces all users to use an inital deposit larger then 10_000$ (1000X)
        asset1.transfer(predicted, 10e18);

        // will revert with to few initial shares for 10_000$. 
        vm.expectRevert(abi.encodeWithSelector(SuperPoolFactory.SuperPoolFactory_TooFewInitialShares.selector, 999));
        superPoolFactory.deploySuperPool(
            poolOwner, address(asset1), feeRecipient, 0.04e18, type(uint256).max, 10000e18, "test", "test"
        );

        // Add another $1 (10_001$) and it will not revert
        superPoolFactory.deploySuperPool(
            poolOwner, address(asset1), feeRecipient, 0.04e18, type(uint256).max, 10001e18, "test", "test"
        );
    }
```

### Mitigation

For a limited mitigation: add `msg.sender` as part of the salt and use CREATE2. This will prevent a hacker from knowing before the block what address the superpool will be created. However it does not prevent front-running attacks. 

A better approach is to use internal accounting for asset balance and not `balanceOf`. 