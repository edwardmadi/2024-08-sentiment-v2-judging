Sharp Sapphire Ferret

Medium

# reallocate will revert when using USDT

## Summary
The [reallocate](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L431-L454) function will revert when using USDT, as it only uses [approve](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L450), and USDT reverts if there is already an approval for the given pool.

## Vulnerability Detail
USDT, as we know, reverts on `approve` if there is already an allowance for the approved address. This makes the following code risky, as if there is an allowance, the [reallocate](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L431-L454) function will revert. **On its own this is not an issue**, as `POOL.deposit` will use 100% of the approval, leaving the allowance at 0.

```solidity
            if (assetsInPool + deposits[i].assets < poolCap) {
                ASSET.approve(address(POOL), deposits[i].assets);
                POOL.deposit(deposits[i].poolId, deposits[i].assets, address(this));
            }
```

However, the [_supplyToPools](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L524-L543) function uses `forceApprove`, and thanks to the `try/catch` block, if the deposit fails, we are left with an allowance above 0. This means that the next time [reallocate](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L431-L454) is triggered for that pool, the **whole transaction will revert**, preventing the owner from reallocating the tokens.

```solidity
                ASSET.forceApprove(address(POOL), supplyAmt);

                try POOL.deposit(poolId, supplyAmt, address(this)) {
                    assets -= supplyAmt;
                } catch { }
```

Example:
1. The Super Pool has 5 pools with USDT.
2. pool1 fills up, but [_supplyToPools](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L524-L543) has allocated a 1000e6 allowance, but the `try/catch` block triggers, leaving a 1000e6 allowance for that pool.
3. The owner moves pool2 to the front of the deposit queue as its APR is higher.
4. pool1's cap is increased, or there are some withdrawals from it.
5. After some time, the owner decides to call [reallocate](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L431-L454) to reallocate to all the pools, however, the TX reverts.

In the above case, [reallocate](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L431-L454) will always revert until that allowance is cleared, meaning even if the owner needs to `reallocate` the funds, they are unable to do so.

## Impact
The [reallocate](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L431-L454) function will not work in most cases when used for pools with USDT. This will break core contract functionality - the Super Pool's ability to move funds across different pools to optimize APY.

## Code Snippet
```solidity
        for (uint256 i; i < depositsLength; ++i) {
            uint256 poolCap = poolCapFor[deposits[i].poolId];
            if (poolCap == 0) revert SuperPool_PoolNotInQueue(deposits[i].poolId);

            uint256 assetsInPool = POOL.getAssetsOf(deposits[i].poolId, address(this));
            if (assetsInPool + deposits[i].assets < poolCap) {
                ASSET.approve(address(POOL), deposits[i].assets);
                POOL.deposit(deposits[i].poolId, deposits[i].assets, address(this));
            }
        }
```

```solidity
        for (uint256 i; i < depositQueueLength; ++i) {
            uint256 poolId = depositQueue[i];
            uint256 assetsInPool = POOL.getAssetsOf(poolId, address(this));

            if (assetsInPool < poolCapFor[poolId]) {
                uint256 supplyAmt = poolCapFor[poolId] - assetsInPool;
                if (assets < supplyAmt) supplyAmt = assets;
                ASSET.forceApprove(address(POOL), supplyAmt);

                // skip and move to the next pool in queue if deposit reverts
                try POOL.deposit(poolId, supplyAmt, address(this)) {
                    assets -= supplyAmt;
                } catch { }

                if (assets == 0) return;
            }
        }
```
## Tool Used
Manual Review

## Recommendation
Implement `forceApprove`, like in [_supplyToPools](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L524-L543).

```diff
    if (assetsInPool + deposits[i].assets < poolCap) {
-       ASSET.approve(address(POOL), deposits[i].assets);
+       ASSET.forceApprove(address(POOL), deposits[i].assets);
        POOL.deposit(deposits[i].poolId, deposits[i].assets, address(this));
    }
```