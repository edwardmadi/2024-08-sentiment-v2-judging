Abundant Hazel Newt

Medium

# SuperPool reallocation may fail due to non-zero approval

## Summary
SuperPool reallocation may fail due to non-zero approval.

## Vulnerability Detail
When SuperPool owner or allocator calls `reallocate()` to reallocate assets between underlying pools, during the process, SuperPool grants asset token allowance to the underlying pools.
[SuperPool.sol#L442-L453](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L442-L453):
```solidity
        uint256 depositsLength = deposits.length;
        for (uint256 i; i < depositsLength; ++i) {
            uint256 poolCap = poolCapFor[deposits[i].poolId];
            // disallow deposits to pool not associated with this SuperPool
            if (poolCap == 0) revert SuperPool_PoolNotInQueue(deposits[i].poolId);
            // respect pool cap
            uint256 assetsInPool = POOL.getAssetsOf(deposits[i].poolId, address(this));
            if (assetsInPool + deposits[i].assets < poolCap) {
@>              ASSET.approve(address(POOL), deposits[i].assets);
                POOL.deposit(deposits[i].poolId, deposits[i].assets, address(this));
            }
        }
```
The problem is some token (e.g USDT) will revert if the current allowance is greater than 0 and an non-zero approval is made, and it's possible that the current allowance is greater than 0. Let's look at `_supplyToPools()` which is called when deposit.
[SuperPool.sol#L530-L541](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L530-L541):
```solidity
            if (assetsInPool < poolCapFor[poolId]) {
                uint256 supplyAmt = poolCapFor[poolId] - assetsInPool;
                if (assets < supplyAmt) supplyAmt = assets;
@>              ASSET.forceApprove(address(POOL), supplyAmt);

                // skip and move to the next pool in queue if deposit reverts
@>              try POOL.deposit(poolId, supplyAmt, address(this)) {
@>                  assets -= supplyAmt;
@>              } catch { }

                if (assets == 0) return;
            }
```
In `_supplyToPools()`, asset token allowance is granted to the underlying pool, then `deposit()` in Pool contract is called and the tokens are transferred from SuperPool to the underlying pool, however, in case of a fail revert, the asset token allowance is not used and remains greater than 0,  hence reallocation will be reverted due to non-zero approval.

Consider the following scenario:
1. An underlying pool's asset is USDT;
2. Depositor deposits USDT into the Super Pool, the Super Pool then grants USDT allowance to the underlying pool and tries to deposit into the underlying pool;
3. Before the deposit transaction is executed, the underlying pool is paused;
4. Deposit is failed but the error is caught and ignored, USDT allowance remains non-zero;
5. Later the underlying pool is unpaused;
6. Super Pool owner calls `reallocate()` to deposit USDT into the underlying pool;
7. Due to the non-zero USDT allowance, the reallocation transaction will be eventually reverted.

Please run the PoC in **BigTest.t.sol**:
```solidity
    function testAudit_Reallocate() public {
        // Pool Asset
        MockUSDT poolAsset = new MockUSDT("USDT", "USDT", 6);

        vm.startPrank(protocolOwner);
        // positionManager.toggleKnownAsset(address(poolAsset));
        riskEngine.setOracle(address(poolAsset), address(new FixedPriceOracle(1e18)));
        vm.stopPrank();

        // Create Underlying Pool
        address poolOwner = makeAddr("PoolOwner");
        vm.prank(poolOwner);
        bytes32 FIXED_RATE_MODEL_KEY = 0xeba2c14de8b8ca05a15d7673453a0a3b315f122f56770b8bb643dc4bfbcf326b;
        uint256 poolId = pool.initializePool(poolOwner, address(poolAsset), type(uint128).max, FIXED_RATE_MODEL_KEY);

        // Create Super Pool
        address superPoolOwner = makeAddr("SuperPoolOwner");
        poolAsset.mint(superPoolOwner, 1000);

        vm.startPrank(superPoolOwner);
        poolAsset.approve(address(superPoolFactory), 1000);
        address superPoolAddress = superPoolFactory.deploySuperPool(
            superPoolOwner, // owner
            address(poolAsset), // asset
            superPoolOwner, // feeRecipient
            0, // fee
            3000e6, // superPoolCap
            1000, // initialDepositAmt
            "SuperPool", // name
            "SP" // symbol
        );
        SuperPool superPool = SuperPool(superPoolAddress);
        superPool.addPool(poolId, 2000e6);
        vm.stopPrank();

        // Underlying Pool paused
        vm.prank(poolOwner);
        pool.togglePause(poolId);

        // Alice deposits into Super Pool
        address alice = makeAddr("Alice");
        poolAsset.mint(alice, 1000e6);

        vm.startPrank(alice);
        poolAsset.approve(address(superPool), 1000e6);
        superPool.deposit(1000e6, alice);
        vm.stopPrank();

        // USDT allowance > 0
        assertTrue(poolAsset.allowance(address(superPool), address(pool)) > 0);

        // Underlying Pool unpaused
        vm.prank(poolOwner);
        pool.togglePause(poolId);

        // Reallocate
        SuperPool.ReallocateParams[] memory withdraws = new SuperPool.ReallocateParams[](0);
        SuperPool.ReallocateParams[] memory deposits = new SuperPool.ReallocateParams[](1);
        SuperPool.ReallocateParams memory deposit = SuperPool.ReallocateParams({
            poolId: poolId,
            assets: 1000e6
        });
        deposits[0] = deposit;

        // reallocate fails
        vm.prank(superPoolOwner);
        vm.expectRevert("Invalid Approval");
        superPool.reallocate(withdraws, deposits);
    }
```
The `MockUSDT` contract is as below:
```solidity
contract MockUSDT is MockERC20 {
    constructor(string memory _name, string memory _symbol, uint8 _decimals) MockERC20(_name, _symbol, _decimals) {}

    function approve(address _spender, uint _value) public override returns (bool) {
        require(!((_value != 0) && (allowance[msg.sender][_spender] != 0)), "Invalid Approval");
        allowance[msg.sender][_spender] = _value;
        return true;
    }
}
```

## Impact
Reallocation will always fail.

## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L450

## Tool used
Manual Review

## Recommendation
Use `forceApprove()` instead of `approve()`.
[SuperPool.sol#L533](https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L533):
```diff
        for (uint256 i; i < depositsLength; ++i) {
            uint256 poolCap = poolCapFor[deposits[i].poolId];
            // disallow deposits to pool not associated with this SuperPool
            if (poolCap == 0) revert SuperPool_PoolNotInQueue(deposits[i].poolId);
            // respect pool cap
            uint256 assetsInPool = POOL.getAssetsOf(deposits[i].poolId, address(this));
            if (assetsInPool + deposits[i].assets < poolCap) {
-               ASSET.approve(address(POOL), deposits[i].assets);
+               ASSET.forceApprove(address(POOL), deposits[i].assets);
                POOL.deposit(deposits[i].poolId, deposits[i].assets, address(this));
            }
        }
```