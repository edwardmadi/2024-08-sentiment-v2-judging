Special Coconut Cow

Medium

# Potential for Excessive Fees and Inability to Update Fees

## Summary
Anyone can create and manage Super Pools on Sentiment v2, with the aim of making the creation and maintenance of Super Pools as frictionless as possible. The `SuperPoolFactory::deploySuperPool()` function is responsible for deploying a new Super Pool as a transparent proxy. There is a potential issue with the constructor's validation logic in the `SuperPool` contract. Specifically, the constructor is validating the state variable `fee` instead of the constructor parameter `fee_`.

## Vulnerability Detail
The constructor checks `if fee > 1e18`, but at this point, the state variable `fee` has not been assigned yet. This comparison is essentially meaningless because the state variable fee might still be at its default value, typically 0.
```solidity
     constructor(
        address pool_,
        address asset_,
        address feeRecipient_,
        uint256 fee_,
        uint256 superPoolCap_,
        string memory name_,
        string memory symbol_
    ) Ownable() ERC20(name_, symbol_) {
        POOL = Pool(pool_);
        ASSET = IERC20(asset_);
        DECIMALS = _tryGetAssetDecimals(ASSET);

@>      if (fee > 1e18) revert SuperPool_FeeTooHigh();
        fee = fee_;
        feeRecipient = feeRecipient_;
        superPoolCap = superPoolCap_;
    }
``` 
The impact of an invalid `fee` being set (i.e., a fee greater than 1e18) can have significant consequences on the contract's behavior, particularly in how fees are calculated and updated. Here's a breakdown of the potential issues:

# Scenerio 1:
The internal function `simulateAccrue()` is used to simulate the accrual of fees where 1e18 represents 100%. Since 1e18 is used as the denominator to represent 100%, any value greater than 1e18 would represent a fee greater than 100%. This implies that more than all of the interest earned is being taken as a fee, which is not logical or practical.
```solidity
     function simulateAccrue() internal view returns (uint256, uint256) {
        uint256 newTotalAssets = totalAssets();
        uint256 interestAccrued = (newTotalAssets > lastTotalAssets) ? newTotalAssets - lastTotalAssets : 0;
        if (interestAccrued == 0 || fee == 0) return (0, newTotalAssets);

@>      uint256 feeAssets = interestAccrued.mulDiv(fee, WAD);
        // newTotalAssets already includes feeAssets
        uint256 feeShares = _convertToShares(feeAssets, newTotalAssets - feeAssets, totalSupply(), Math.Rounding.Down);

        return (feeShares, newTotalAssets);
    }
```
If the `fee` is set to a value greater than 1e18, it could lead to unexpected or erroneous behavior in the contract, such as:
-   Fee = 2e18: This would suggest a fee of 200%, which means the contract would attempt to take more than twice the amount of interest earned as a fee, which is nonsensical and would likely lead to issues.

-  Taking all the interest and possibly more, which could result in negative balances or unintended consequences.

<details><summary><strong>POC for Scenario 1</strong></summary>
Extended from <a href="https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/test/core/Superpool.t.sol#L8" target="_blank">SuperPool.t.sol</a>

```solidity
       function testExcessiveInterest() public {
        address feeReceipient = makeAddr("feeReceipient");

        uint256 fee = 2e18;

        //deploy the SuperPool contract with fee > 1e18
        SuperPool superpool =
            new SuperPool(address(pool), address(asset1), feeReceipient, fee, 1_000_000 ether, "test", "test");

        superpool.addPool(linearRatePool, 100 ether);

        vm.startPrank(user);
        asset1.mint(user, 100 ether);
        asset1.approve(address(superpool), 100 ether);

        superpool.previewDeposit(100 ether);

        superpool.deposit(100 ether, user);
        vm.stopPrank();

        uint256 shares = superpool.balanceOf(user);
        uint256 assets = superpool.convertToAssets(shares);

        // coincidenal withdrawal can be covered without dipping into base pools
        asset1.mint(address(superpool), assets / 10);

        vm.prank(user);
        superpool.withdraw(assets / 10, user, user);

        //more than twice the amount of interest earned as a fee
        console2.log("feeReceipient->", superpool.balanceOf(feeReceipient));
    }
```
Run the following command to execute the POC: `forge test --match-test testExcessiveInterest -vvv`
</details>

# Scenerio 2:
The `requestFeeUpdate()` function is used to propose a new fee update for the `SuperPool`. It includes a check that reverts with the message `SuperPool_FeeTooHigh` if the `fee` is greater than 1e18:
```solidity
    function requestFeeUpdate(uint256 _fee) external onlyOwner {
@>      if (fee > 1e18) revert SuperPool_FeeTooHigh();
        pendingFeeUpdate = PendingFeeUpdate({ fee: _fee, validAfter: block.timestamp + TIMELOCK_DURATION });
        emit SuperPoolFeeUpdateRequested(_fee);
    }
```
If the `fee` was incorrectly set to a value greater than 1e18 due to the constructor validation issue, then:
-  The `requestFeeUpdate()` function will always revert if the fee exceeds 1e18 because the condition will be met.
-  This means the contract will never allow a fee update to be requested, potentially breaking functionality that depends on the ability to update the fee.

<details><summary><strong>POC for Scenario 2</strong></summary>
Extended from <a href="https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/test/core/Superpool.t.sol#L8" target="_blank">SuperPool.t.sol</a>

```solidity
    function testCannot_Update_Fee() public {
        
        uint256 fee = 2e18;

        // deploy the SuperPool contract with fee > 1e18
        SuperPool superpool =
            new SuperPool(address(pool), address(asset1), feeTo, fee, 1_000_000 ether, "test", "test");

        // Reverts once it is set to a value greater than 1e18, and no new updates can be proposed
        vm.expectRevert(SuperPool.SuperPool_FeeTooHigh.selector);
        superpool.requestFeeUpdate(0.01 ether);
        
     }
```
Run the following command to execute the POC: `forge test --match-test testCannot_Update_Fee`
</details>

## Impact
-  The contract might unintentionally enforce an incorrect fee, resulting in the system taking more than the legitimate interest.
-  if the `fee` variable is mistakenly set to a value greater than 1e18 due to incorrect validation in the constructor, the `requestFeeUpdate()` function will indeed always revert.
## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/0b472f4bffdb2c7432a5d21f1636139cc01561a5/protocol-v2/src/SuperPool.sol#L144-L161

## Tool used
Manual Review

## Recommendation
Correct the validation in the constructor to check the `fee_` parameter:
```diff
    constructor(
        address pool_,
        address asset_,
        address feeRecipient_,
        uint256 fee_,
        uint256 superPoolCap_,
        string memory name_,
        string memory symbol_
    ) Ownable() ERC20(name_, symbol_) {
        POOL = Pool(pool_);
        ASSET = IERC20(asset_);
        DECIMALS = _tryGetAssetDecimals(ASSET);

--      if (fee > 1e18) revert SuperPool_FeeTooHigh();
++      if (fee_ > 1e18) revert SuperPool_FeeTooHigh();
        fee = fee_;
        feeRecipient = feeRecipient_;
        superPoolCap = superPoolCap_;
    }
```
