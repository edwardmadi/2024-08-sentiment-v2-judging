Damaged Malachite Gibbon

Medium

# Incorrect Fee Update Invalidation

## Summary
In the `SuperPool` contract, a validation error occurs when updating the `fee` state variable. The validation process incorrectly checks the existing state variable instead of the incoming parameter.

## Vulnerability Detail
The same thing happens in both `constructor` and `requestFeeUpdate` function.

In the constructor, the validation mistakenly checks `fee` instead of the incoming `fee_` parameter, which always passes since the default value is 0.


```solidity
    constructor(
        address pool_,
        address asset_,
        address feeRecipient_,
        uint256 fee_,
        uint256 superPoolCap_,
        string memory name_,
        string memory symbol_
    ) Ownable() ERC20(name_, symbol_) {
        POOL = Pool(pool_);
        ASSET = IERC20(asset_);
        DECIMALS = _tryGetAssetDecimals(ASSET);

>       if (fee > 1e18) revert SuperPool_FeeTooHigh();
        fee = fee_;
        feeRecipient = feeRecipient_;
        superPoolCap = superPoolCap_;
    }
```

In the `requestFeeUpdate` function, the validation mistakenly checks `fee` instead of the incoming `_fee` parameter, which always passes since the default value is 0.


```solidity
    function requestFeeUpdate(uint256 _fee) external onlyOwner {
>       if (fee > 1e18) revert SuperPool_FeeTooHigh();
        pendingFeeUpdate = PendingFeeUpdate({ fee: _fee, validAfter: block.timestamp + TIMELOCK_DURATION });
        emit SuperPoolFeeUpdateRequested(_fee);
    }
```

## Impact
Because of incorrect validation, an excessively high fee value can be set, leading to an unexpected amount of funds being deducted as a fee.

## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L157

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L367

## Tool used
Manual Review

## Recommendation
Modify the validation to:
```diff
    constructor(
        address pool_,
        address asset_,
        address feeRecipient_,
        uint256 fee_,
        uint256 superPoolCap_,
        string memory name_,
        string memory symbol_
    ) Ownable() ERC20(name_, symbol_) {
        POOL = Pool(pool_);
        ASSET = IERC20(asset_);
        DECIMALS = _tryGetAssetDecimals(ASSET);

-       if (fee > 1e18) revert SuperPool_FeeTooHigh();
+       if (fee_ > 1e18) revert SuperPool_FeeTooHigh();
        fee = fee_;
        feeRecipient = feeRecipient_;
        superPoolCap = superPoolCap_;
    }
```
and
```diff
    function requestFeeUpdate(uint256 _fee) external onlyOwner {
-       if (fee > 1e18) revert SuperPool_FeeTooHigh();
+       if (_fee > 1e18) revert SuperPool_FeeTooHigh();
        pendingFeeUpdate = PendingFeeUpdate({ fee: _fee, validAfter: block.timestamp + TIMELOCK_DURATION });
        emit SuperPoolFeeUpdateRequested(_fee);
    }
```