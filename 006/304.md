Sour Holographic Kookaburra

Medium

# SuperPool fee configuration can be set very high and blocked from updating new values

## Summary
Incorrect validations in `constructor()` and function `requestFeeUpdate()` allow SuperPool owner to set fee `> 1e18` and once it is accepted with that high value, it can not be updated again

## Vulnerability Detail
The validations for fee configuration are as below:
```solidity
constructor(
        address pool_,
        address asset_,
        address feeRecipient_,
        uint256 fee_,
        uint256 superPoolCap_,
        string memory name_,
        string memory symbol_
    ) Ownable() ERC20(name_, symbol_) {
        POOL = Pool(pool_);
        ASSET = IERC20(asset_);
        DECIMALS = _tryGetAssetDecimals(ASSET);

        if (fee > 1e18) revert SuperPool_FeeTooHigh(); // @audit-issue incorrect check
        fee = fee_;
```
The validation checks for state variable `fee`, instead of checking function parameter `fee_`

```solidity
function requestFeeUpdate(uint256 _fee) external onlyOwner {
        if (fee > 1e18) revert SuperPool_FeeTooHigh(); // @question condition ? ==> can update super high fee ==> @audit-issue set high fee allowed
        pendingFeeUpdate = PendingFeeUpdate({ fee: _fee, validAfter: block.timestamp + TIMELOCK_DURATION });
        emit SuperPoolFeeUpdateRequested(_fee);
    }
```
The validation checks for state variable `fee`, instead of checking function parameter `_fee`.
Once the fee is accepted, it can not be updated to a new value because the check `if (fee > 1e18) revert SuperPool_FeeTooHigh();`


PoC:
Update test in file `SuperPool.t.sol`:
```diff
    function testInitSuperPool() public {
        SuperPool randomPoolRaw =
            new SuperPool(address(pool), address(asset1), feeTo, 0.01 ether, 1_000_000 ether, "test", "test");

        assertEq(address(randomPoolRaw.asset()), address(asset1));
        assertEq(randomPoolRaw.feeRecipient(), feeTo);
        assertEq(randomPoolRaw.fee(), 0.01 ether);
        assertEq(randomPoolRaw.superPoolCap(), 1_000_000 ether);
        assertEq(randomPoolRaw.name(), "test");
        assertEq(randomPoolRaw.symbol(), "test");

+        // 1. Init pool with high fee
+        randomPoolRaw =
+           new SuperPool(address(pool), address(asset1), feeTo, 1.5 ether, 1_000_000 ether, "test", "test");
        
+        assertEq(randomPoolRaw.fee(), 1.5 ether);

+       // 2. Update new fee value and get REVERT
+        vm.expectRevert();
+        randomPoolRaw.requestFeeUpdate(0.01 ether);
    }
...
...
    function testSetSuperPoolFee() public {
        vm.startPrank(poolOwner);
        superPool.requestFeeUpdate(0.04 ether);
        vm.warp(26 hours);
        superPool.acceptFeeUpdate();
        assertEq(superPool.fee(), 0.04 ether);
        vm.stopPrank();

        vm.startPrank(user);
        vm.expectRevert();
        superPool.requestFeeUpdate(0.04 ether);


+        // 1. Set a very high fee
+        vm.startPrank(poolOwner);
+        superPool.requestFeeUpdate(1.2 ether);
+
+        // 2. accept fee
+        skip(3 days);
+        superPool.acceptFeeUpdate();
        
+        // 3. set reasonable fee value and get REVERT
+        skip(1 days);
+        vm.expectRevert();
+        superPool.requestFeeUpdate(0.1 ether);
    }
```

Console shows:
```bash
[PASS] testInitSuperPool() (gas: 6830602)
[PASS] testInitSuperPoolFactory() (gas: 4116663)
Suite result: ok. 2 passed; 0 failed; 0 skipped; finished in 6.29ms (1.06ms CPU time)
```

## Impact
Assume the pool owner is not malicious, the pool fee could be set very high (by accident, or human error) and it could not be set to a reasonable value after that

## Code Snippet
https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L157-L158

https://github.com/sherlock-audit/2024-08-sentiment-v2/blob/main/protocol-v2/src/SuperPool.sol#L366-L370

## Tool used
Manual Review

## Recommendation
Update the fee configuration validation in `constructor()` and function `requestFeeUpdate()`